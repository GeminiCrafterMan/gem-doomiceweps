actor SSTFist : Fist replaces Fist
{
  Game Doom
  Weapon.SelectionOrder 3699
  Weapon.Kickback 100
  +WEAPON.WIMPY_WEAPON
  +WEAPON.MELEEWEAPON
  +inventory.undroppable
  +Noautofire
  +NOALERT
  +Weapon.AXEBLOOD
  //+Bloodsplatter
  Obituary "%o was beaten down by %k."
  attacksound "punches"
  Weapon.preferredSkin "FistSkin"
  States
  {
  Ready:
    FIST B 1 A_WeaponReady
    loop
  Deselect:
    FIST B 1 A_Lower
    FIST B 0 A_Lower
    loop
  Select: 
    FIST B 1 A_Raise
    FIST B 0 A_Raise
    loop
  Fire:
    FIST C 0 A_JumpIfInventory("PowerStrength",1,"Fire.Berserk")
  Fire1:
    FIST TC 1	
	FIST D 0 A_PlayWeaponSound("fistjab")
    FIST G 1 A_CustomPunch (2*random(1,10),1,0,"BulletPuff")//A_Punch
  Hold:
    FIST C 0 A_JumpIfInventory("PowerStrength",1,"Hold.Berserk")
    PLA3 A 0 A_JumpIf ((momx+momy+momz > 10) || (momx+momy+momz < -10), "RunningPunch")
    FIST G 1
    FIST G 0 A_Refire
    FIST FE 1
    FIST D 1 A_WeaponReady
    FIST CC 35 A_WeaponReady
    goto Ready
  RunningPunch:
    FIST G 3 A_CustomPunch (2*random(1,10),1,0,"BulletPuff")
    FIST G 0 A_Refire
    FIST FE 1
    FIST D 1 A_WeaponReady
    FIST CC 35 A_WeaponReady
    goto Ready
  Fire.Berserk:
    FIST TC 1	
	FIST D 0 A_PlayWeaponSound("fistjab")
    FIST G 1 A_CustomPunch (2*random(25,100),1,0,"BerserkPuff")//A_Punch
  Hold.Berserk:
    PLA3 A 0 A_JumpIf ((momx+momy+momz > 10) || (momx+momy+momz < -10), "RunningBerserk")
    FIST G 1
    FIST G 0 A_Refire
    FIST FE 1
    FIST D 1 A_WeaponReady
    FIST CC 35 A_WeaponReady
    goto Ready
  RunningBerserk:
    FIST G 3 A_CustomPunch (2*random(25,100),1,0,"BerserkPuff")
    FIST G 0 A_Refire
    FIST FE 1
    FIST D 1 A_WeaponReady
    FIST CC 35 A_WeaponReady
    goto Ready
  AltFire:
  	FIST QRST 1
  AltHold:
  	FIST U 1
  	TNT1 A 0 A_Refire
	FIST C 0 Bright A_PlayWeaponSound("fistwhoosh")
    FIST C 0 A_JumpIfInventory("PowerStrength",1,"SlugBerserk")
	
    FIST CDE 1
    FIST F 1 A_CustomPunch (20,1,0,"BulletPuff")//A_Punch
    FIST HIJKLMNOP 1
    FIST CC 35 A_WeaponReady
    goto Ready
  SlugBerserk:
    FIST CDE 1
    FIST F 1 A_CustomPunch (200,1,0,"BerserkPuff")//A_Punch
    FIST HIJKLMNOP 1
    FIST CC 35 A_WeaponReady
    goto Ready
  Spawn:
    TNT1 A -1
    Stop
  }
}
ACTOR BerserkPuff
{
	+NOBLOCKMAP
	+NOGRAVITY
	-ALLOWPARTICLES
	-RANDOMIZE
	+PUFFONACTORS
	RenderStyle Add
	Alpha 1.0
	VSpeed 0
	Mass 5
	scale 0.65
	DamageType "Rocket"
	States
	{
	Spawn:
		PUFF A 4 Bright
		PUFF B 4
		PUFF CD 4
		Stop
	Melee:
	XDeath:
		RMIS B 0 Bright
		TNT1 A 0 Radius_Quake(6,random(8,16),0,3, 0)
		RMIS B 0 Bright A_PlaySoundEx("berserkhit", "SoundSlot6")
		MISL BBBBCCCCDDDD 1 Bright A_Fadeout(0.08)
		Stop
	}
}