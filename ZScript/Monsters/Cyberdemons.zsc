class GWM_Cyberdemon : Cyberdemon
{
	Default
	{
		//$Category Monsters
		Tag "$TAG_CYBERDEMON";
	}
	States
	{
		Missile:
			CYBR E 6 A_FaceTarget;
			CYBR F 12 A_SpawnProjectile("GWM_EnemyRocket",50,-24,0);
			CYBR E 12 A_FaceTarget;
			CYBR F 12 A_SpawnProjectile("GWM_EnemyRocket",50,-24,0);
			CYBR E 12 A_FaceTarget;
			CYBR F 12 A_SpawnProjectile("GWM_EnemyRocket",50,-24,0);
			Goto See;
	}
}

class GWM_Superdemon : Actor
{
	Default
	{
		//$Category Monsters
		Tag "$TAG_SUPERDEMON";
		Obituary "$OB_SUPERDEMON";
		Health 3500;
		Radius 40;
		Height 110;
		Mass 1000;
		Speed 15;
		PainChance 20;
		SeeSound "cyber/sight";
		PainSound "cyber/pain";
		DeathSound "monsters/superdemon/death";
		ActiveSound "cyber/active";
		MONSTER;
		+BOSS
		+FLOORCLIP
		+NORADIUSDMG
		+MISSILEMORE
	}
	States
	{
		Spawn:
			SUPR AB 10 A_Look;
			Loop;
		See:
			SUPR A 3 A_Hoof;
			SUPR ABB 3 A_Chase;
			SUPR C 3 A_Hoof;
			SUPR CDD 3 A_Chase;
			Loop;
		Missile:
			SUPR Q 0 A_Jump(128,"Missile2");
			SUPR QR 4 A_FaceTarget;
			SUPR S 8 A_SpawnProjectile("GWM_SuperDemonBall", 54);
			SUPR QR 4 A_FaceTarget;
			SUPR S 8 A_SpawnProjectile("GWM_SuperDemonBall", 54);
			SUPR QR 4 A_FaceTarget;
			SUPR S 9 A_SpawnProjectile("GWM_SuperDemonBall", 54);
			Goto See;
		Missile2:
			SUPR EF 6 A_FaceTarget;
			SUPR G 10 { // This used to be a bunch of GG 0 states, but ZScript can do multiple actions at once.
				A_SpawnProjectile("GWM_SuperDemonBall", 54, 0, -10);
				A_SpawnProjectile("GWM_SuperDemonBall", 54, 0, -10);
				A_SpawnProjectile("GWM_SuperDemonBall", 54, 0, -5);
				A_SpawnProjectile("GWM_SuperDemonBall", 54, 0, -5);
				A_SpawnProjectile("GWM_SuperDemonBall", 54, 0, 5);
				A_SpawnProjectile("GWM_SuperDemonBall", 54, 0, 5);
				A_SpawnProjectile("GWM_SuperDemonBall", 54, 0, 10);
				A_SpawnProjectile("GWM_SuperDemonBall", 54, 0, 10);
				A_SpawnProjectile("GWM_SuperDemonBall", 54, 0);
			}
			Goto See;
		Pain:
			SUPR H 10 A_Pain;
			Goto See;
		Death:
			SUPR I 4 A_StartSound("monsters/superdemon/snarl", chan_5);
			SUPR J 4 A_SpawnProjectile("Blood",0,0,random(-80,-100),2,random(45,80));
			SUPR J 4 A_Scream;
			SUPR KKKK 1 A_SpawnProjectile("Blood",0,0,random(-80,-100),2,random(45,80));
			SUPR L 3 A_SpawnProjectile("GWM_SuperDemonArm", 54, -50, -50);
			SUPR MN 5 A_SpawnProjectile("Blood",0,0,random(-80,-100),2,random(45,80));
			SUPR O 7 A_StartSound("monsters/superdemon/crash");
			SUPR PPPPPPP 1 A_SpawnProjectile("Blood",0,0,random(-80,-100),2,random(45,80));
			SUPR P 7 A_NoBlocking;
			SUPR P -1 A_BossDeath;
			Stop;
	}
}
class GWM_SuperDemonBall : Actor
{
	Default
	{
		Radius 6;
		Height 16;
		Speed 16;
		DamageFactor (68);
		Scale 1.5;
		Renderstyle "Add";
		SeeSound "baron/attack";
		DeathSound "baron/shotx";
		PROJECTILE;
		+RANDOMIZE
		Decal "BigScorch";
	}
	States
	{
		Spawn:
			BARB AB 1 bright;
			Loop;
		Death:
			BARB CDE 6 bright;
			Stop;
	}
}
class GWM_SuperDemonArm : Actor
{
	Default
	{
		Radius 10;
		Height 8;
		Speed 1;
		Damage 1;
		Scale 1;
		PROJECTILE;
		-NOGRAVITY
		+LOWGRAVITY
	}
	States
	{
		Spawn:
			SUPH A 5 A_SpawnProjectile("Blood",0,0,random(-80,-100),2,random(45,80));
			SUPH B 5 A_SpawnProjectile("Blood",0,0,random(-80,-100),2,random(45,80));
			SUPH C 5 A_SpawnProjectile("Blood",0,0,random(-80,-100),2,random(45,80));
			Goto Death;
		Death:
			SUPH D 5 A_SpawnProjectile("Blood",0,0,random(-80,-100),2,random(45,80));
			SUPH E -1 A_SpawnProjectile("Blood",0,0,random(-80,-100),2,random(45,80));
			Stop;
	}
}
