class GWM_LostSoulSpawner : RandomSpawner
{
	override void BeginPlay()
	{
		if (self.curSector.lightlevel <= 112 && random(0,2) == 2)
		{
			Spawn("GWM_Blot", self.Pos);
			self.Destroy();
		}
		else Super.BeginPlay();
	}
	Default
	{
		DropItem "GWM_LostSoul";
	}
}

class GWM_LostSoul : LostSoul // to prevent recursion, i guess
{
	Default
	{
		Tag "Lost Soul";
		//$Category Monsters
	}
}

class GWM_BlotSmoke : Actor
{
	Default
	{
		+NOINTERACTION
		+CLIENTSIDEONLY
		RenderStyle "Translucent";
	}
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_Jump(256, "Blot1", "Blot2", "Blot3", "Blot4");
		Blot1:
			BLOT A 2;
			BLOT BCDE 2 A_FadeOut(0.2);
			Stop;
		Blot2:
			BLOT F 2;
			BLOT GHIJ 2 A_FadeOut(0.2);
			Stop;
		Blot3:
			BLOT K 2;
			BLOT LMNO 2 A_FadeOut(0.2);
			Stop;
		Blot4:
			BLOT P 2;
			BLOT QRST 2 A_FadeOut(0.2);
			Stop;
  }
}
class GWM_Blot : Actor
{
	Default
	{
		//$Category Monsters
		//$Angled
		//$Sprite BLOPA0

		Tag "Blot";
		Obituary "$OB_BLOT";
		Health 20;
		Radius 8;
		Height 16;
		Mass 50;
		Speed 0;
		Deathsound "monsters/blot/death";
		DamageFactor "Blot", 0;
		BloodType "None";
		Scale 0.6;
	
		MONSTER;
		-SOLID
		-CANPASS
		+STANDSTILL
		+MISSILEMORE
		+MISSILEEVENMORE
		+DONTHARMCLASS
		+NODAMAGETHRUST
		+BLOODLESSIMPACT
		+LOWGRAVITY
		+BRIGHT
		+NOEXTREMEDEATH
	}
	States
	{
		Spawn:
			EYES A 10 A_Look;
			Loop;
		IdleSpawn:
			EYES A 1 A_LookEx(LOF_NOSEESOUND, 0, 0, 0, 360);
			EYES AAA 0 A_SpawnItemEx("GWM_BlotSmoke", -4, random(-10, 10), random(-10, 10), 0, random(-1, 1), random(-1, 1));
			Loop;
		See:
			EYES A 1 A_Chase;
			EYES AAA 0 A_SpawnItemEx("GWM_BlotSmoke", -4, random(-10, 10), random(-10, 10), 0, random(-1, 1), random(-1, 1));
			Loop;
		Missile:
			EYES A 0 A_CheckSight("IdleSpawn");
		LeapSight:
			EYES B 1 A_FaceTarget;
			EYES BBB 0 A_SpawnItemEx("GWM_BlotSmoke", -4, random(-10, 10), random(-10, 10), 0, random(-1, 1), random(-1, 1));
			EYES B 1 ThrustThingZ(0, 13, 0, 0);
			EYES BBB 0 A_SpawnItemEx("GWM_BlotSmoke", -4, random(-10, 10), random(-10, 10), 0, random(-1, 1), random(-1, 1));
			TNT1 B 0 ThrustThing(angle*256/360, 8, 0, 0);
			TNT1 B 0 A_StartSound("monsters/blot/attack");
		MidLeap:
			EYES B 1 A_SpawnItem("GWM_BlotDamage");
			EYES BBB 0 A_SpawnItemEx("GWM_BlotSmoke", -4, random(-10, 10), random(-10, 10), 0, random(-1, 1), random(-1, 1));
			TNT1 B 0 A_JumpIf(Vel.Z < 0, "Falling");
			Loop;
		Falling:
			EYES A 1 A_SpawnItem("GWM_BlotDamage");
			EYES AAA 0 A_SpawnItemEx("GWM_BlotSmoke", -4, random(-10, 10), random(-10, 10), 0, random(-1, 1), random(-1, 1));
			TNT1 A 0 A_CheckFloor("Land");
			Loop;
		Land:
			EYES A 1 A_Stop;
			EYES AAA 0 A_SpawnItemEx("GWM_BlotSmoke", -4, random(-10, 10), random(-10, 10), 0, random(-1, 1), random(-1, 1));
			EYES A 0 A_ClearTarget;
			Goto IdleSpawn;
		Death:
			TNT1 A 1 A_Stop;
			TNT1 A 0 A_ScreamAndUnblock;
			TNT1 AAAAAAAA 1 A_SpawnItemEx("GWM_BlotSmoke", 0, 0, 0, random(-4, 4), 0, random(-4, 4), random(0, 360));
			Stop;
	}
}
class GWM_BlotDamage : Actor
{
	Default
	{
		Radius 2;
		Height 2;
		Speed 0;
		DamageType "Blot";
		Projectile;
		+NODAMAGETHRUST
		+BLOODLESSIMPACT
	}
	States
	{
		Spawn:
			TNT1 A 1 NoDelay A_Explode(1, 40, 0);
			Stop;
	}
}