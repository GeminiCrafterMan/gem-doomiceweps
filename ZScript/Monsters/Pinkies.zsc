// Pinky Demon =============================
class GWM_Pinky : Demon
{
	Default
	{
		//$Category Monsters
		//$Title "Pinky
		Tag "$TAG_PINKY";
	}
	States
	{
		Spawn:
			PNKY AB 10 A_Look;
			Loop;
		See:
			PNKY AABBCCDD 2 Fast A_Chase;
			Loop;
		Melee:
			PNKY EF 8 Fast A_FaceTarget;
			PNKY G 8 Fast A_CustomMeleeAttack(22);
			Goto See;
		Pain:
			PNKY H 2 Fast;
			PNKY H 2 Fast A_Pain;
			Goto See;
		Death:
			PNKY I 8;
			PNKY J 8 A_Scream;
			PNKY K 4;
			PNKY L 4 A_NoBlocking;
			PNKY M 4;
			PNKY N -1;
			Stop;
		XDeath:
			PNKY O 5;
			PNKY P 5 A_XScream;
			PNKY Q 5 A_NoBlocking;
			PNKY RSTUV 5;
			PNKY W -1;
			Stop;
		Death.Fire:
			TNT1 A 0 {
				bNOBLOOD = true;
				A_SetTranslation("GWM_Burnt");
			}
			PNKY O 5;
			PNKY P 5 A_StartSound("monsters/burndeath");
			PNKY Q 5 A_NoBlocking;
			PNKY RSTUV 5;
			PNKY W -1;
			Stop;
		Raise:
			TNT1 A 0 {bNOBLOOD = false;}
			PNKY N 5;
			PNKY MLKJI 5;
			Goto See;
	}
}
// Spectre =================================
class GWM_Spectre : GWM_Pinky
{
	Default
	{
		//$Category Monsters
		//$Title "Spectre
		+SHADOW
		RenderStyle "OptFuzzy";
		Alpha 0.5;
		SeeSound "spectre/sight";
		AttackSound "spectre/melee";
		PainSound "spectre/pain";
		DeathSound "spectre/death";
		ActiveSound "spectre/active";
		Obituary "$OB_SPECTREHIT";
		Tag "$TAG_SPECTRE";
	}
}

// Hellhog =================================
class GWM_Hellhog : Actor
{
	Default
	{
		//$Category Monsters
		//$Title "Hellhog
		BloodColor "30 67 23";
		Health 260;
		PainChance 140;
		Speed 6;
		Radius 30;
		Height 56;
		Mass 600;
		Monster;
		+FLOORCLIP
		SeeSound "monsters/hellhog/sight";
		AttackSound "monsters/hellhog/melee";
		PainSound "monsters/hellhog/pain";
		DeathSound "monsters/hellhog/death";
		ActiveSound "monsters/hellhog/active";
		Obituary "$OB_HELLHOG";
		Tag "$TAG_HELLHOG";
	}
	States
	{
		Spawn:
			HHOG AB 10 A_Look;
			Loop;
		See:
			HHOG AABBCCDD 2 Fast A_Chase;
			Loop;
		Melee:
			HHOG EF 8 Fast A_FaceTarget;
			HHOG G 8 Fast A_CustomMeleeAttack(33); // This was set to *6* for some reason...!?
			Goto See;
		Pain:
			HHOG H 2 Fast;
			HHOG H 2 Fast A_Pain;
			Goto See;
		Death:
			HHOG I 8;
			HHOG J 8 {A_Scream(); A_SpawnItemEx("GWM_HellhogArm", xvel: random(2, 8), zvel: random(0, 13), angle: random(0, 359));}
			HHOG K 4;
			HHOG L 4 A_NoBlocking;
			HHOG M 4;
			HHOG N -1;
			Stop;
		Death.Fire:
			TNT1 A 0 {
				bNOBLOOD = true;
				A_SetTranslation("GWM_Burnt");
			}
			HHOG I 8;
			HHOG J 8 A_StartSound("monsters/burndeath");
			HHOG K 4;
			HHOG L 4 A_NoBlocking;
			HHOG M 4;
			HHOG N -1;
			Stop;
		Raise:
			TNT1 A 0 {bNOBLOOD = false;}
			HHOG N 5;
			HHOG MLKJI 5;
			Goto See;
	}
}
class GWM_HellhogArm : Actor
{
	Default
	{
		Radius 8;
		Height 8;
		+NOBLOCKMAP +DROPOFF +CANNOTPUSH +SKYEXPLODE +NOBLOCKMONST
	}
	States
	{
		Spawn:
			HHOG OPQRSTUV 3 NoDelay A_CheckFloor("Hit");
			Loop;
		Hit:
			HHOG W -1;
			Stop;
	}
}

// Nightmare Demon =========================
class GWM_NightmareDemon : Actor
{
	Default
	{
		//$Category Monsters
		//$Title "Nightmare Demon
		Health 300;
		Speed 15;
		Radius 30;
		Height 56;
		PainChance 100;
		Mass 400;
		Monster;
		+FLOORCLIP
		SeeSound "monsters/nmaredemon/sight";
		AttackSound "monsters/nmaredemon/melee";
		PainSound "monsters/nmaredemon/pain";
		DeathSound "monsters/nmaredemon/death";
		ActiveSound "monsters/nmaredemon/active";
		Obituary "$OB_NIGHTMAREDEMON";
		Tag "$TAG_NIGHTMAREDEMON";
		BloodColor "4F A9 67";
	}
	States
	{
		Spawn:
			NIDM AABB 10 A_Look;
			Loop;
		See:
			NIDM AABBCCDD 2 A_Chase;
			Loop;
		Melee:
			NIDM E 6 A_FaceTarget;
			NIDM F 7 A_FaceTarget;
			NIDM G 8 A_CustomMeleeAttack(22);
			Goto See;
		Pain:
			NIDM H 2;
			NIDM H 2 A_Pain;
			Goto See;
		Death:
			NIDM I 8;
			NIDM J 8 A_Scream;
			NIDM K 4;
			NIDM L 4 A_Fall;
			NIDM M 4;
			NIDM N -1;
			Stop;
		Raise:
			NIDM NMLKJI 5;
			Goto See;
	}
}