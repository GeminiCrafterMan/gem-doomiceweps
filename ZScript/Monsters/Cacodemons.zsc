class GWM_Cacolantern : Cacodemon
{
	Default
	{
		BloodColor "blue";
		SeeSound "monsters/cacolantern/sight";
//		PainSound "monsters/cacolantern/pain";
		DeathSound "monsters/cacolantern/death";
//		ActiveSound "monsters/cacolantern/active";
	}
	States
	{
		Spawn:
			CLAN A 10 A_Look;
			Loop;
		See:
			CLAN B 2/* A_StartSound("monsters/cacolantern/awake")*/;
			Goto See2;
		See2:
			CLAN C 3 A_Chase;
			Loop;
		Missile:
			CLAN C 8 A_StartSound("monsters/cacolantern/prepare");
			CLAN D 8 A_FaceTarget;
			CLAN F 8 Bright A_FaceTarget;
			CLAN F 5 Bright A_CustomComboAttack("GWM_CacolanternBall", 32, 10 * random(1, 6));
			Goto See2;
		Pain:
			CLAN B 3;
			CLAN B 3 A_Pain;
			CLAN A 6;
			Goto See;
		Death:
			CLAN C 4 A_Scream;
			CLAN BA 4;
			CLAN K -1 {
				A_SpawnItemEx("GWM_CacolanternDebris", xvel: 4, zvel: 4, angle: 45);
				A_SpawnItemEx("GWM_CacolanternDebris", xvel: 4, zvel: 4, angle: 135);
				A_SpawnItemEx("GWM_CacolanternDebris", xvel: 4, zvel: 4, angle: 225);
				A_SpawnItemEx("GWM_CacolanternDebris", xvel: 4, zvel: 4, angle: 315);
				A_NoBlocking();
				A_SetFloorClip();
				A_StartSound("monsters/cacolantern/shatter");
			}
			Stop;
		Raise:
			CLAN KK 4;
			CLAN A 4 A_UnsetFloorCLip;
			Goto See;
	}
}
class GWM_CacolanternDebris : Actor
{
	Default
	{
		Projectile;
		BounceType "Heretic";
		Health 4;
		Radius 1;
		Height 1;
		-NOGRAVITY
	}
	States
	{
		Spawn:
			CLAN G 3;
			CLAN H 3;
			CLAN I 3;
			CLAN J 3;
			Loop;
		Death:
			#### # 35 A_FadeOut(0.1);
			Loop;
	}
}
class GWM_CacolanternBall : CacodemonBall
{
	Default
	{
		Speed 20;
		FastSpeed 40;
		SeeSound "monsters/cacolantern/shoot";
		DeathSound "monsters/cacolantern/explode";
	}
	States
	{
		Spawn:
			CLBL A 2 Bright A_SpawnItemEx("GWM_CacolanternBallGhost1");
			CLBL B 2 Bright A_SpawnItemEx("GWM_CacolanternBallGhost2");
			Loop;
		Death:
			CLBL CDEF 2 Bright;
			Stop;
	}
}
class GWM_CacolanternBallGhost1 : Actor
{
	Default
	{
		RenderStyle "Add";
		Alpha 0.5;
		+NOGRAVITY
	}
	States
	{
		Spawn:
			CLBL A 3 Bright A_FadeOut(0.1);
			Loop;
	}
}
class GWM_CacolanternBallGhost2 : GWM_CacolanternBallGhost1
{
	States
	{
		Spawn:
			CLBL B 3 Bright A_FadeOut(0.1);
			Loop;
	}
}