class GWM_DoomImp : DoomImp // literally just a normal imp
{
	Default
	{
		//$Category Monsters
		Tag "$TAG_DOOMIMP";
	}
	States
	{
		Melee:
		Missile:
			TROO EF 8 A_FaceTarget;
			TROO G 6 A_CustomComboAttack("GWM_ImpBall", 32, 14, "imp/melee");
			Goto See;
		Death.Fire:
			TNT1 A 0 {
				bNOBLOOD = true;
				A_SetTranslation("GWM_Burnt");
			}
			TROO N 5;
			TROO O 5 A_StartSound("monsters/burndeath");
			TROO P 5;
			TROO Q 5 A_NoBlocking;
			TROO RST 5;
			TROO U -1;
			Stop;
		Death:
			TNT1 A 0 A_Jump(120,"Death2");
			TROO I 8;
			TROO J 8 A_Scream;
			TROO K 6;
			TROO L 6 A_NoBlocking;
			TROO M -1;
			Stop;
			
		Death2:
			DIMP A 8;
			DIMP B 6 A_Scream;
			DIMP C 5;
			DIMP D 4 A_NoBlocking;
			DIMP E -1;
			Stop;
		Raise:
			TNT1 A 0 {bNOBLOOD = false;}
			Goto Super::Raise;
	}
}
class GWM_ImpBall : DoomImpBall
{
	Default
	{
		DamageType "Fire";
		DamageFunction (14);
	}
}

class GWM_DoomImpSpawner : RandomSpawner
{
	override void BeginPlay()
	{
		if (self.curSector.lightlevel <= 112 && random(0,3) == 3)
		{
			Spawn("GWM_Nightshade", self.Pos);
			self.Destroy();
		}
		else Super.BeginPlay();
	}
	Default
	{
		DropItem "GWM_DoomImp";
	}
}

class GWM_CyberImp : Actor
{
	Default
	{
		//$Category Monsters
		Tag "$TAG_CYBERIMP";
		Health 120;
		Radius 20;
		Height 56;
		Mass 200;
		Speed 8;
		PainChance 150;
		Monster;
		+FLOORCLIP
		SeeSound "monsters/cyberimp/sight";
		PainSound "monsters/cyberimp/pain";
		DeathSound "monsters/cyberimp/death";
		ActiveSound "monsters/cyberimp/active";
		HitObituary "$OB_CYBERIMP_SCRATCH";
		Obituary "$OB_CYBERIMP_PLASMA";
	}
	States
	{
		Spawn:
			CIMP AB 10 A_Look;
			Loop;
		See:
			CIMP AABBCCDD 3 A_Chase;
			Loop;
		Melee:
			CIMP EF 8 A_FaceTarget;
			CIMP F 6 A_CustomMeleeAttack(17, "imp/melee");
			Goto See;
		Missile:
			CIMP EF 8 A_FaceTarget;
			CIMP G 6 A_SpawnProjectile("GWM_EnemyPlasma",36,4);
			Goto See;
		Pain:
			CIMP H 2;
			CIMP H 2 A_Pain;
			Goto See;
		Death:
			CIMP I 8;
			CIMP J 8 A_Scream;
			CIMP K 6;
			CIMP L 6 A_NoBlocking;
			CIMP M -1;
			Stop;
		XDeath:
			CIMP N 5 A_SpawnProjectile("GWM_CyberImpArm",32,-10,270);
			CIMP O 5 A_XScream;
			CIMP P 5;
			CIMP Q 5 A_NoBlocking;
			CIMP RST 5;
			CIMP U -1;
			Stop;
		Death.Fire:
			TNT1 A 0 {
				bNOBLOOD = true;
				A_SetTranslation("GWM_Burnt");
			}
			CIMP N 5 A_SpawnProjectile("GWM_CyberImpArm",32,-10,270);
			CIMP O 5 A_StartSound("monsters/burndeath");
			CIMP P 5;
			CIMP Q 5 A_NoBlocking;
			CIMP RST 5;
			CIMP U -1;
			Stop;
		Raise:
			TNT1 A 0 {bNOBLOOD = false;}
			CIMP MLKJI 8;
			Goto See;
	}
}
class GWM_CyberImpArm : Cell
{
	Default
	{
		Radius 8;
		Height 8;
		Speed 4;
		+DOOMBOUNCE
		+DROPOFF
		+Missile
		Inventory.PickupMessage "$GOTCYBERIMPARM";
	}
	States
	{
		Spawn:
			CARM ABCDE 3;
			Loop;
		Death:
			CARM F -1;
			Loop;
	}
}

class GWM_Nightshade : Actor
{
	Default
	{
		//$Category Monsters
		Tag "$TAG_NIGHTSHADE";
		Obituary "$OB_NIGHTSHADE";
		Health 80;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 12;
		PainChance 160;
		SeeSound "monsters/nightshade/sight";
		PainSound "monsters/nightshade/pain";
		DeathSound "monsters/nightshade/death";
		ActiveSound "monsters/nightshade/active";
		MONSTER;
		+FLOORCLIP
	}
	States
	{
		Spawn:
			NSHA AB 10 A_Look();
			Loop;
		See:
			NSHA A 0 A_SetTranslucent(0.75,0);
			NSHA AABBCCDD 3 A_Chase();
			Goto See+1;
		Missile:
			NSHA E 0 A_SetTranslucent(1.0,0);
			NSHA EE 4 Bright A_FaceTarget();
			NSHA F 8 Bright A_SpawnProjectile("GWM_ShadeMissile",32,0,0,0,0);
			NSHA B 4 A_SetTranslucent(0.87,0);
			Goto See;
		Pain:
			NSHA G 0 A_SetTranslucent(1.0,0);
			NSHA G 2;
			NSHA G 2 A_Pain();
			Goto See;
		Death:
			NSHA H 4 A_SetTranslucent(1.0,0);
			NSHA I 4 A_SetTranslucent(0.9,0);
			NSHA I 0 A_Scream();
			NSHA J 4 A_SetTranslucent(0.8,0);
			NSHA K 0 A_NoBlocking();
			NSHA K 4 A_SetTranslucent(0.7,0);
			NSHA L 4 A_SetTranslucent(0.6,0);
			NSHA M 4 A_SetTranslucent(0.5,0);
			NSHA N 4 A_SetTranslucent(0.4,0);
			NSHA O 4 A_SetTranslucent(0.3,0);
			NSHA P 4 A_SetTranslucent(0.2,0);
			Stop;
		// No Death.Fire here since the regular death is sufficient.
		// No Raise here because it kind of disappears instead of leaving a corpse.
	}
}
Class GWM_ShadeMissile : CacodemonBall
{
	Default
	{
		DamageFunction (18);
		Speed 10;
		Alpha 0.80;
		SeeSound "monsters/nightshade/attack";
		DeathSound "monsters/nightshade/explode";
		DamageType "Normal";
		+THRUGHOST
		+SEEKERMISSILE
		+FORCEXYBILLBOARD
	}
	States
	{
		Spawn:
			NBAL A 5 Bright {
				A_SpawnItemEx("GWM_ShadePuff", 0,0,0, 0,0,0, 0, SXF_CLIENTSIDE);
				A_SeekerMissile(5,7);
			}
			NBAL B 5 Bright {
				A_SpawnItemEx("GWM_ShadePuff", 0,0,0, 0,0,0, 0, SXF_CLIENTSIDE);
				A_SeekerMissile(5,7);
			}
			Loop;
		Death:
			NBAL HIJKLM 3 Bright;
			Stop;
	}
}
Class GWM_ShadePuff : Actor
{
	Default
	{
		Radius 3;
		Height 3;
		RENDERSTYLE "Add";
		Alpha 0.67;
		+NOGRAVITY
		+NOBLOCKMAP
		+DONTSPLASH
		+FORCEXYBILLBOARD
	}
	States
	{
		Spawn:
			TNT1 A 3 Bright;
			NBAL CDEFG 3 Bright;
			Stop;
	}
}

Class GWM_Devil : DoomImp
{
	Default
	{
		Obituary "$OB_DEVIL_FIREBALL";
		HitObituary "$OB_DEVIL_SCRATCH";
		Tag "$TAG_DEVIL";
		Health 120;
		Radius 20;
		Height 56;
		Mass 120;
		Speed 10;
		PainChance 160;
		SeeSound "monsters/devil/sight";
		PainSound "monsters/devil/pain";
		DeathSound "monsters/devil/death";
		ActiveSound "monsters/devil/active";
		MONSTER;
		+FLOORCLIP
	}
 	States
	{
		Spawn:
			TRO2 AB 10 A_Look;
			Loop;
		See:
			TRO2 AABBCCDD 3 A_Chase;
			Loop;
		Melee:
		Missile:
			TRO2 EF 6 A_FaceTarget;
			TRO2 G 4 A_CustomComboAttack("GWM_ImpBall", 32, 14, "imp/melee");
			TRO2 B 2;
			TRO2 HI 6 A_FaceTarget;
			TRO2 J 4 A_CustomComboAttack("GWM_ImpBall", 32, 14, "imp/melee");		
			TRO2 B 2;
			Goto See;
		Pain:
			TRO2 K 2;
			TRO2 K 2 A_Pain;
			Goto See;
		Death:
			TRO2 L 8;
			TRO2 M 8 A_Scream;
			TRO2 N 6;
			TRO2 O 6 A_NoBlocking;
			TRO2 P -1;
			Stop;
		XDeath:
			TRO2 Q 5;
			TRO2 R 5 A_XScream;
			TRO2 S 5;
			TRO2 T 5 A_NoBlocking;
			TRO2 UVW 5;
			TRO2 X -1;
			Stop;
		Death.Fire:
			TNT1 A 0 {
				bNOBLOOD = true;
				A_SetTranslation("GWM_Burnt");
			}
			TRO2 Q 5;
			TRO2 R 5 A_StartSound("monsters/burndeath");
			TRO2 S 5;
			TRO2 T 5 A_NoBlocking;
			TRO2 UVW 5;
			TRO2 X -1;
			Stop;
		Raise:
			TNT1 A 0 {bNOBLOOD = false;}
			TRO2 PO 8;
			TRO2 NML 6;
			Goto See;
	}
}