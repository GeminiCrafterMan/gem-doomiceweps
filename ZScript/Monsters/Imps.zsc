class GWM_DoomImp : DoomImp {} // literally just a normal imp

class GWM_NightmareImpSpawner : RandomSpawner
{
	override void BeginPlay()
	{
		if (self.curSector.lightlevel <= 112 && random(0,3) == 3)
		{
			Spawn("GWM_Nightshade", self.Pos);
			self.Destroy();
		}
		else Super.BeginPlay();
	}
	Default
	{
		DropItem "GWM_NightmareImp";
	}
}

class GWM_NightmareImp : DoomImp
{
	Default
	{
		PainChance 128;
		Speed 16;
		RenderStyle "Translucent";
		Translation "GWM_NightmareImp";
		Tag "Nightmare Imp";
		Alpha 0.7;
		BloodColor "78 58 80";
		Obituary "$OB_NIGHTMAREIMP_SCRATCH";
	}
	States
	{
		Melee:
		Missile:
			TROO EF 8 A_FaceTarget;
			TROO G 6 A_CustomComboAttack("GWM_NightmareImpBall", 32, 3 * random(1, 8), "imp/melee");
			Goto See;
	}
}

class GWM_NightmareImpBall : DoomImpBall
{
	Default
	{
		Translation "GWM_NightmareImp";
		Speed 20;
		FastSpeed 40;
		Obituary "$OB_NIGHTMAREIMP_FIRE";
	}
}

class GWM_CyberImp : Actor
{
	Default
	{
		Tag "Cyber Imp";
		Health 120;
		Radius 20;
		Height 56;
		Mass 200;
		Speed 8;
		PainChance 150;
		Monster;
		+FLOORCLIP
		SeeSound "monsters/cyberimp/sight";
		PainSound "monsters/cyberimp/pain";
		DeathSound "monsters/cyberimp/death";
		ActiveSound "monsters/cyberimp/active";
		HitObituary "$OB_CYBERIMP_SCRATCH";
		Obituary "$OB_CYBERIMP_PLASMA";
	}
	States
	{
		Spawn:
			CIMP AB 10 A_Look;
			Loop;
		See:
			CIMP AABBCCDD 3 A_Chase;
			Loop;
		Melee:
			CIMP EF 8 A_FaceTarget;
			CIMP F 6 A_CustomMeleeAttack(3 * random(3, 10), "imp/melee");
			Goto See;
		Missile:
			CIMP EF 8 A_FaceTarget;
			CIMP G 6 A_SpawnProjectile("ArachnotronPlasma",36,4);
			Goto See;
		Pain:
			CIMP H 2;
			CIMP H 2 A_Pain;
			Goto See;
		Death:
			CIMP I 8;
			CIMP J 8 A_Scream;
			CIMP K 6;
			CIMP L 6 A_NoBlocking;
			CIMP M -1;
			Stop;
		XDeath:
			CIMP N 5 A_SpawnProjectile("GWM_CyberImpArm",32,-10,270);
			CIMP O 5 A_XScream;
			CIMP P 5;
			CIMP Q 5 A_NoBlocking;
			CIMP RST 5;
			CIMP U -1;
			Stop;
		Raise:
			CIMP MLKJI 8;
			Goto See;
	}
}
class GWM_CyberImpArm : Cell
{
	Default
	{
		Radius 8;
		Height 8;
		Speed 4;
		+DOOMBOUNCE
		+DROPOFF
		+Missile
		Inventory.PickupMessage "$GOTCYBERIMPARM";
	}
	States
	{
		Spawn:
			CARM ABCDE 3;
			Loop;
		Death:
			CARM F -1;
			Loop;
	}
}

class GWM_Nightshade : Actor
{
	Default
	{
		Tag "Nightshade";
		Obituary "$OB_NIGHTSHADE";
		Health 80;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 12;
		PainChance 160;
		SeeSound "monsters/nightshade/sight";
		PainSound "monsters/nightshade/pain";
		DeathSound "monsters/nightshade/death";
		ActiveSound "monsters/nightshade/active";
		MONSTER;
		+FLOORCLIP
	}
	States
	{
		Spawn:
			NSHA AB 10 A_Look();
			loop;
		See:
			NSHA A 0 A_SetTranslucent(0.75,0);
			NSHA AABBCCDD 3 A_Chase();
			goto See+1;
		Missile:
			NSHA E 0 A_SetTranslucent(1.0,0);
			NSHA EE 4 Bright A_FaceTarget();
			NSHA F 8 Bright A_SpawnProjectile("GWM_ShadeMissile",32,0,0,0,0);
			NSHA B 4 A_SetTranslucent(0.87,0);
			goto See;
		Pain:
			NSHA G 0 A_SetTranslucent(1.0,0);
			NSHA G 2;
			NSHA G 2 A_Pain();
			goto See;
		Death:
			NSHA H 4 A_SetTranslucent(1.0,0);
			NSHA I 4 A_SetTranslucent(0.9,0);
			NSHA I 0 A_Scream();
			NSHA J 4 A_SetTranslucent(0.8,0);
			NSHA K 0 A_NoBlocking();
			NSHA K 4 A_SetTranslucent(0.7,0);
			NSHA L 4 A_SetTranslucent(0.6,0);
			NSHA M 4 A_SetTranslucent(0.5,0);
			NSHA N 4 A_SetTranslucent(0.4,0);
			NSHA O 4 A_SetTranslucent(0.3,0);
			NSHA P 4 A_SetTranslucent(0.2,0);
			stop;
	}
}
Class GWM_ShadeMissile : CacodemonBall
{
	Default
	{
		Damage 4;
		Speed 10;
		Alpha 0.80;
		SeeSound "monsters/nightshade/attack";
		DeathSound "monsters/nightshade/explode";
		DamageType "Normal";
		+THRUGHOST
		+SEEKERMISSILE
		+FORCEXYBILLBOARD
	}
	States
	{
		Spawn:
			NBAL A 5 bright A_SpawnItemEx("GWM_ShadePuff", 0,0,0, 0,0,0, 0, SXF_CLIENTSIDE);
			NBAL A 0 bright A_Seekermissile(5,7);
			NBAL B 5 bright A_SpawnItemEx("GWM_ShadePuff", 0,0,0, 0,0,0, 0, SXF_CLIENTSIDE);
			NBAL B 0 bright A_Seekermissile(5,7);
			loop;
		Death:
			NBAL HIJKLM 3 bright;
			stop;
	}
}
Class GWM_ShadePuff : Actor
{
	Default
	{
		Radius 3;
		Height 3;
		RENDERSTYLE "Add";
		Alpha 0.67;
		+NOGRAVITY
		+NOBLOCKMAP
		+DONTSPLASH
		+FORCEXYBILLBOARD
	}
	States
	{
		Spawn:
			TNT1 A 3 Bright;
			NBAL CDEFG 3 BRIGHT;
			Stop;
	}
}