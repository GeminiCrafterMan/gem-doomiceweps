class GWM_ChaingunGuy : ChaingunGuy
{
	Default
	{
		//$Category Monsters
		Tag "$TAG_CHAINGUNNER";
		AttackSound "weapons/chainfire";
		DropItem "GWM_Chaingun";
	}
	States
	{
		Missile:
			CPOS E 10 A_FaceTarget;
			CPOS FE 4 Bright 
			{
			A_CustomBulletAttack(22.5, 0, 1, 9, "ZBulletPuff", 0, CBAF_NORANDOM, missile: "GWM_Tracer", spawnheight: 34, spawnofs_xy: 12);
			A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,15);	
			}
			CPOS F 1 A_CPosRefire;
			Goto Missile+1;
		Death.Fire:
			TNT1 A 0 {
				bNOBLOOD = true;
				A_SetTranslation("GWM_Burnt");
			}
			CPOS O 5;
			CPOS P 5 A_StartSound("monsters/burndeath");
			CPOS Q 5 A_NoBlocking;
			CPOS RS 5;
			CPOS T -1;
			Stop;
		Raise:
			TNT1 A 0 {bNOBLOOD = false;}
			Goto Super::Raise;
	}
}

class GWM_HeavyRifleman : ChaingunGuy
{
	Default
	{
		//$Category Monsters
		Tag "$TAG_HEAVYRIFLEMAN";
		AttackSound "weapons/riflesht";
		Obituary "$OB_HEAVYRIFLEMAN";
		DropItem "GWM_BurstRifle";
	}
	States
	{
		Spawn:
			HEVY AB 10 A_Look;
			Loop;
		See:
			HEVY AABBCCDD 4 A_Chase;
			Loop;
		Missile:
			HEVY E 2 A_FaceTarget;
			HEVY F 1 Bright 
			{
			A_CustomBulletAttack(22.5, 0, 1, 9, "ZBulletPuff", 0, CBAF_NORANDOM, missile: "GWM_Tracer", spawnofs_xy: 9);
			A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,15);	
			}
			HEVY G 2 A_FaceTarget;
			HEVY F 1 Bright 
			{
			A_CustomBulletAttack(22.5, 0, 1, 9, "ZBulletPuff", 0, CBAF_NORANDOM, missile: "GWM_Tracer", spawnofs_xy: 9);
			A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,15);	
			}
			HEVY G 2 A_FaceTarget;
			HEVY F 1 Bright 
			{
			A_CustomBulletAttack(22.5, 0, 1, 9, "ZBulletPuff", 0, CBAF_NORANDOM, missile: "GWM_Tracer", spawnofs_xy: 9);
			A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,15);	
			}
			HEVY G 6; // nerfed and also matches actual burst rifle now
			HEVY E 3 A_CPosRefire;
			Goto Missile;
		Pain:
			HEVY H 3;
			HEVY H 3 A_Pain;
			Goto See;
		Death:
			HEVY I 5;
			HEVY J 5 A_Scream;
			HEVY K 5 A_NoBlocking;
			HEVY L 5;
			HEVY M 5;
			HEVY N -1;
			Stop;
		XDeath:
			HEVY O 5;
			HEVY P 5 A_XScream;
			HEVY Q 5 A_NoBlocking;
			HEVY RS 5;
			HEVY T -1;
			Stop;
		Death.Fire:
			TNT1 A 0 {
				bNOBLOOD = true;
				A_SetTranslation("GWM_Burnt");
			}
			HEVY O 5;
			HEVY P 5 A_StartSound("monsters/burndeath");
			HEVY Q 5 A_NoBlocking;
			HEVY RS 5;
			HEVY T -1;
			Stop;
		Raise:
			TNT1 A 0 {bNOBLOOD = false;}
			HEVY M 5;
			HEVY LKJI 5;
			Goto See;
	}
}