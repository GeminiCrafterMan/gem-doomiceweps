class GWM_KahunaShotgunGuy : ShotgunGuy
{
	Default
	{
		AttackSound "weapons/skshot";
		DropItem "GWM_Shotgun";
		Obituary "$OB_KAHUNASHOTGUNGUY";
	}
	States
	{
		Missile:
			SPOS E 10 A_FaceTarget;
			SPOS F 10 BRIGHT A_CustomBulletAttack(22.5, 0, 3, random(1,5)*3, "ZBulletPuff", 0, CBAF_NORANDOM, missile: "GWM_Tracer", spawnofs_xy: 9);
			SPOS E 8 A_StartSound("weapons/skpump1");
			SPOS E 2 A_StartSound("weapons/skpump2");
			Goto See;
	}
}

class GWM_IceShotgunGuy : ShotgunGuy
{
	Default
	{
		Translation "Ice";
		DropItem "GWM_IceShotgun";
		Obituary "$OB_ICESHOTGUNGUY";
		BloodColor "76 76 8D";
	}
	States
	{
		Spawn:
			ISGZ AB 10 A_Look;
			Loop;
		See:
			ISGZ AABBCCDD 3 A_Chase;
			Loop;
		Missile:
			ISGZ E 10 A_FaceTarget;
			ISGZ F 10 BRIGHT
			{
				A_StartSound("weapons/iceshot", CHAN_WEAPON);
				A_FatAttack3("GWM_ShotgunGuyIceChunk");
				A_SpawnItemEx("IceFog2", 24, 0, 32, 0.5, 0, FRandom(-0.5, 0.5), Random(-12, 12));
				A_SpawnItemEx("IceFog2", 24, 0, 32, 0.5, 0, FRandom(-0.5, 0.5), Random(-12, 12));
				A_SpawnItemEx("IceFog2", 24, 0, 32, 0.5, 0, FRandom(-0.5, 0.5), Random(-12, 12));
				A_SpawnItemEx("IceFog2", 24, 0, 32, 0.5, 0, FRandom(-0.5, 0.5), Random(-12, 12));
//				SpawnMissile(target, "GWM_ShotgunGuyIceChunk");
			}
			ISGZ E 9;
			ISGZ E 4 {
				A_StartSound("weapons/icepump1", CHAN_BODY);
				A_StartSound("weapons/icehiss", CHAN_AUTO);
			}
			ISGZ E 4;
			ISGZ E 5 A_StartSound("weapons/icepump2", CHAN_BODY);
			Goto See;
		Pain:
			ISGZ G 3;
			ISGZ G 3 A_Pain;
			Goto See;
		Death:
			ISGZ H 5;
			ISGZ I 5 A_Scream;
			ISGZ J 5;
			ISGZ K 5;
			ISGZ L 10 A_Stop;
			ISGZ L 10 A_FreezeDeathChunks;
			Stop;
		XDeath:
			ISGZ M 5 A_IceGuyDie;
			Stop;
		Raise:
			TNT1 A 0;	// Shouldn't exist.
			Goto See;
	}
}
class GWM_ShotgunGuyIceChunk : GWM_ShotgunIceChunk
{
	Default
	{
		Damage 2;
		Obituary "$OB_ICESHOTGUNGUY";
	}
}