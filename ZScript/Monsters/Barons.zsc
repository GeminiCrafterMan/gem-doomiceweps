class GWM_BaronOfHell : BaronOfHell
{
	Default
	{
		//$Category Monsters
		Tag "Baron of Hell";
		BloodColor "93 2F 00";
	}
	States
	{
		Spawn:
			BARN AB 10 A_Look;
			Loop;
		See:
			BARN AABBCCDD 3 A_Chase;
			Loop;
		Melee:
		Missile:
			BARN EF 8 A_FaceTarget;
			BARN G 8 A_CustomComboAttack("GWM_BaronBall", 32, 10 * random(1, 8), "baron/melee");
			Goto See;
		Pain:
			BARN H 2;
			BARN H 2 A_Pain;
			Goto See;
		Death:
			BARN I 8;
			BARN J 8 A_Scream;
			BARN K 8;
			BARN L 8 A_NoBlocking;
			BARN MN 8;
			BARN O -1 A_BossDeath;
			Stop;
		Death.Fire:
			TNT1 A 0 {
				bNOBLOOD = true;
				A_SetTranslation("GWM_Burnt");
			}
			BARN I 8;
			BARN J 8 A_StartSound("monsters/burndeath");
			BARN K 8;
			BARN L 8 A_NoBlocking;
			BARN MN 8;
			BARN O -1 A_BossDeath;
			Stop;
		Raise:
			TNT1 A 0 {bNOBLOOD = false;}
			BARN O 8;
			BARN NMLKJI 8;
			Goto See;
	}
}
class GWM_BaronBall : BaronBall
{
	States
	{
		Spawn:
			BARB AB 4 BRIGHT;
			Loop;
		Death:
			BARB CDE 6 BRIGHT;
			Stop;
	}
}

class GWM_CyberBaron : GWM_BaronOfHell
{
	Default
	{
		obituary "%o was splayed by a cyber baron";
		hitobituary "%o was ripped to shreds by a cyber baron";
		health 1200;
		radius 24;
		height 64;
		mass 1000;
		speed 8;
		painchance 40;
		seesound "cbaron/sight";
		painsound "cbaron/pain";
		deathsound "cbaron/death";
		activesound "cbaron/active";
		MONSTER;
		+NORADIUSDMG
	}
	states
	{
		Spawn:
			CBOS AB 10 A_Look;
			loop;
		See:
			CBOS A 3 A_chase;
			CBOS A 3 A_chase;
			CBOS B 0 A_playsound("cbaron/metal");
			CBOS BB 3 A_chase;
			CBOS CC 3 A_chase;		
			CBOS D 3 A_chase;
			CBOS D 3 A_hoof;
			loop;
		Melee:
			CBOS PQ 8 A_FaceTarget;
			CBOS R 8 A_CustomComboAttack("GWM_BaronBall", 32, 10 * random(1, 8), "baron/melee");
			Goto See;
		Missile:
			CBOS E 0 A_jump(80,"Missile2");
			CBOS E 10 A_FaceTarget;
			CBOS E 10 A_FaceTarget;
			CBOS F 10 Bright A_SpawnProjectile("Rocket",27,16,0);
			CBOS E 10 A_FaceTarget;
			goto See;
		Missile2:
			CBOS PQ 8 A_FaceTarget;
			CBOS R 8 A_CustomComboAttack("GWM_BaronBall", 32, 10 * random(1, 8), "baron/melee");
			Goto See;
		Pain:
			CBOS H 2;
			CBOS H 2 A_Pain;
			goto See;
		Death:
			CBOS I 8;
			CBOS J 8 A_Scream;
			CBOS K 8 bright A_Playsound("world/barrelx");
			CBOS L 8 bright A_NoBlocking;
			CBOS M 8 bright;
			CBOS N 8 bright;
			CBOS O -1;
			stop;
	}
}