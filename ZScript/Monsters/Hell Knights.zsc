class GWM_HellKnight : HellKnight
{
	Default
	{
		//$Category Monsters
		Tag "$TAG_HELLKNIGHT";
		BloodColor "30 67 23";
	}
	States
	{
		Spawn:
			HKNI AB 10 A_Look;
			Loop;
		See:
			HKNI AABBCCDD 3 A_Chase;
			Loop;
		Melee:
		Missile:
			HKNI EF 8 A_FaceTarget;
			HKNI G 8 A_CustomComboAttack("GWM_KnightBall", 32, 10 * random(1, 8), "baron/melee");
			Goto See;
		Pain:
			HKNI H 2;
			HKNI H 2 A_Pain;
			Goto See;
		Death:
			HKNI I 8;
			HKNI J 8 A_Scream;
			HKNI K 8;
			HKNI L 8 A_NoBlocking;
			HKNI MN 8;
			HKNI O -1;
			Stop;
		Death.Fire:
			TNT1 A 0 {
				bNOBLOOD = true;
				A_SetTranslation("GWM_Burnt");
			}
			HKNI I 8;
			HKNI J 8 A_StartSound("monsters/burndeath");
			HKNI K 8;
			HKNI L 8 A_NoBlocking;
			HKNI MN 8;
			HKNI O -1;
			Stop;
		Raise:
			TNT1 A 0 {bNOBLOOD = false;}
			HKNI O 8;
			HKNI NMLKJI 8;
			Goto See;
	}
}
class GWM_KnightBall : BaronBall
{
	States
	{
		Spawn:
			HBAL AB 4 Bright;
			Loop;
		Death:
			HBAL CDE 6 Bright;
			Stop;
	}
}

class GWM_CyberHellKnight : GWM_HellKnight
{
	Default
	{
		Tag "$TAG_CYBERHELLKNIGHT";
		Obituary "$OB_CYBERHELLKNIGHT_FIREBALL";
		HitObituary "$OB_CYBERHELLKNIGHT_SCRATCH";
		Health 600;
		PainChance 40;
		SeeSound "monsters/chknight/sight";
		PainSound "monsters/chknight/pain";
		DeathSound "monsters/chknight/death";
		ActiveSound "monsters/chknight/active";
		+NORADIUSDMG // Might remove this later.
	}
	States
	{
		Spawn:
			CBO2 AB 10 A_Look;
			Loop;
		See:
			CBO2 A 3 A_Chase;
			CBO2 A 3 A_Chase;
			CBO2 B 0 A_StartSound("monsters/cbaron/metal");
			CBO2 BB 3 A_Chase;
			CBO2 CC 3 A_Chase;		
			CBO2 D 3 A_Chase;
			CBO2 D 3 A_Hoof;
			Loop;
		Missile:
			CBO2 E 0 A_Jump(80,"Missile2");
			CBO2 E 10 A_FaceTarget;
			CBO2 E 10 A_FaceTarget;
			CBO2 F 10 Bright A_SpawnProjectile("Rocket",27,16,0);
			CBO2 E 10 A_FaceTarget;
			Goto See;
		Melee:
		Missile2:
			CBO2 PQ 8 A_FaceTarget;
			CBO2 R 8 A_CustomComboAttack("GWM_KnightBall", 32, 10 * random(1, 8), "baron/melee");
			Goto See;
		Pain:
			CBO2 H 2;
			CBO2 H 2 A_Pain;
			Goto See;
		Death:
			CBO2 I 8;
			CBO2 J 8 A_Scream;
			CBO2 K 8 Bright A_StartSound("world/barrelx");
			CBO2 L 8 Bright A_NoBlocking;
			CBO2 MN 8 Bright;
			CBO2 O -1 A_BossDeath;
			Stop;
		Death.Fire:
			Goto Death;
		Raise:
			TNT1 A 0 {bNOBLOOD = false;}
			CBO2 O 8;
			CBO2 NMLK 8 Bright;
			CBO2 JI 8;
			Goto See;
	}
}