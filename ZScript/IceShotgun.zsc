class IceSGFlash : Actor
{
	Default
	{
		+NOGRAVITY
	}
	States
	{
		Spawn:
			TNT1 AAAAAA 1 {
				A_Warp(AAPTR_TARGET, 0, 0, 32, 0, WARPF_COPYINTERPOLATION);
			}
			Stop;
	}
}

class IceShotgun : Shotgun
{
	Default
	{
		Inventory.PickupMessage "You got the Ice Shotgun!";
		Obituary "Shouldn't see this.";
		Tag "Ice Shotgun";
	}
	States
	{
		Ready:
			ISHT A 1 A_WeaponReady;
			Loop;
		Deselect:
			ISHT A 1 A_Lower;
			Loop;
		Select:
			ISHT A 1 A_Raise;
			Loop;
		Fire:
			ISHT A 1 {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_FireProjectile("ShotgunIce");
				A_StartSound("weapons/iceshgn", CHAN_WEAPON);
				A_SpawnItemEx("IceSGFlash");
				A_Overlay(-2, "Flash");
				A_WeaponOffset(0, 12, WOF_ADD);
				A_OverlayScale(OverlayID(), 1.125, 0, WOF_INTERPOLATE);
			}
			ISHT A 2;
			ISHT AA 3 {
				A_WeaponOffset(0, -6, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			ISHT A 2;
			ISHT B 5;
			ISHT C 1 A_WeaponOffset(0.75, 1.5, WOF_ADD);
			ISHT C 1 A_WeaponOffset(1.5, 3, WOF_ADD);
			ISHT CCC 1 A_WeaponOffset(3, 6, WOF_ADD);
			ISHT D 4;
			ISHT C 1 A_WeaponOffset(-0.75, -1.5, WOF_ADD);
			ISHT C 1 A_WeaponOffset(-1.5, -3, WOF_ADD);
			ISHT CCC 1 A_WeaponOffset(-3, -6, WOF_ADD);
			ISHT B 5;
			ISHT A 3;
			ISHT A 7 A_ReFire;
			Goto Ready;
		AltFire:
		// Was going to be a kick, but I don't wanna.
			PUNG B 4;
			PUNG C 4 A_Punch;
			PUNG D 5;
			PUNG C 4;
			PUNG B 5 A_ReFire;
			Goto Ready;
		Flash:
			TNT1 A 0 A_OverlayOffset(OverlayID(), 159.5, 112);
			SGFL ABCCDE 1 Bright;
			Stop;
		Spawn:
			ISHT E -1;
			Stop;
	}
}

class ShotgunIce : Actor
{
	Default
	{
		Radius 13;
		Height 13;
		Speed 50;
		Damage 12;
		Scale 2;
		Projectile;
		+RANDOMIZE;
		+BOUNCEONFLOORS;
		+ICESHATTER;
		SeeSound "weapons/iceshot";
		DeathSound "weapons/icehit";
		Obituary "%o got frozen solid by %k's Ice Shotgun.";
		DamageType "Ice";
	}
	States
	{
		Spawn:
			SHGI A 6 Bright A_SpawnItemEx("ShotgunIceTrail");
			loop;
		Death:
			TNT1 A 1 {
				A_SpawnItemEx("ShotgunIceChunk", 0, 0, 0, -50, 0, 0, 10);
				A_SpawnItemEx("ShotgunIceChunk", 0, 0, 0, -50, 0, 5, 0);
				A_SpawnItemEx("ShotgunIceChunk", 0, 0, 0, -50, 0, 0, 0);
				A_SpawnItemEx("ShotgunIceChunk", 0, 0, 0, -50, 0, -5, 0);
				A_SpawnItemEx("ShotgunIceChunk", 0, 0, 0, -50, 0, 0, -10);
			}
			stop;
		XDeath:
			SHGI FBCDE 4 Bright;
//			TNT1 A 0 A_ScaleVelocity(0.50, AAPTR_TARGET);	// AAPTR_TARGET is the player, for some reason.
			Stop;
	}
}

class ShotgunIceChunk : ShotgunIce
{
	Default
	{
		Radius 7;
		Height 7;
		Damage 6;
		+RIPPER
		BounceType "Doom";
		BounceCount 2;
	}
	States
	{
		Spawn:
			SHGI F 6 Bright A_SpawnItemEx("ShotgunIceTrail");
			loop;
		Death:
			SHGI BCDE 4 Bright;
			stop;
	}
}

class ShotgunIceTrail : Actor
{
	Default
	{
		Radius 13;
		Height 8;
		Speed 45; // parent - 5
		Damage 0;
		Projectile;
		+RANDOMIZE;
		RenderStyle "Add";
		Alpha 0.75;
	//  SeeSound "weapons/iceshot";
		DeathSound "weapons/icehit";
		Obituary "%o got frozen solid by %k.";
	}
	States
	{
		Spawn:
			SHGI BCDE 6 Bright A_FadeOut(0.2);
			loop;
		Death:
			SHGI BCDE 4 Bright;
			stop;
	}
}
