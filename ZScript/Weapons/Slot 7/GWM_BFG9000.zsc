class GWM_BFG9000 : GWM_WeaponBase
{
	Default
	{
		Weapon.SelectionOrder 2800; // Don't waste ammo!! But also, big green ball of death, SAVE ME!!!
		Weapon.UpSound "weapons/draw_primary";
		Weapon.AmmoUse 40;
		Weapon.AmmoGive 40;
		Weapon.AmmoType "Cell";
		+WEAPON.NOAUTOFIRE
		Tag "BFG9000";
		Inventory.PickupMessage "$GOTBFG9000";
		Obituary "$KILLBFG9000";
	}
	States
	{
		Ready:
			BFUG A 1 A_WeaponReady;
			Loop;
		Deselect:
			BFUG A 1 A_Lower(12);
			Loop;
		Select:
			BFUG A 1 A_Raise(12);
			Loop;
		Fire:
			TNT1 A 0 A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
			BFUG A 5 A_BFGSound;
			BFUG B 10 A_Overlay(-2, "Flash");
			BFUG C 5;
			BFUG C 5 {
				A_FireBFG();
				// since the BFG uses two whole cell items to fire...
				ZWL_EjectCasing("GWM_CellCasing");
				ZWL_EjectCasing("GWM_CellCasing", true, offset: (24, 0, -10));
				A_WeaponOffset(0, 63, flags: WOF_ADD);
				A_OverlayScale(OverlayID(), 1.60, 0, WOF_INTERPOLATE);
			}
			BFUG CCB 2 {
				A_WeaponOffset(0, -21, flags: WOF_ADD);
				A_OverlayScale(OverlayID(), -0.20, 0, WOF_ADD);
			}
			BFUG BBB 2;
			BFUG A 5 A_ReFire;
			Goto Ready;
		Flash:
			TNT1 A 0 A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
			BFUF AB 5 Bright A_Light1;
			BFUF C 5 Bright A_Light2;
			BFUF C 5 Bright {
				A_Light2();
				A_OverlayScale(OverlayID(), 1.60, 0, WOF_INTERPOLATE);
			}
			BFUF DEF 2 Bright {
				A_OverlayScale(OverlayID(), -0.20, 0, WOF_ADD);
			}
			Goto LightDone;
		Spawn:
			BFUG Z -1;
			Stop;
	}
}
class GWM_BFGBall : BFGBall
{
	Default
	{
		Obituary "$KILLBFGBALL";
	}
}