// This is a flammenwerfer. It werfs flammen!
class GWM_Flamethrower : GWM_WeaponBase
{
	Default
	{
		// $Category Weapons
		Tag "Flamethrower";
		Weapon.UpSound "weapons/draw_primary";
		Inventory.PickupSound "misc/w_pkup";
		Inventory.PickupMessage "$GOTFLAMETHROWER";
		Obituary "$KILLFLAMETHROWER";
		Weapon.AmmoType "GWM_Gas";
		Weapon.AmmoGive 48;
		Weapon.AmmoUse 1;
		+Weapon.Explosive
	}
	States
	{
		Spawn:
			FLMG Z -1;
			Loop;
		Ready:
			TNT1 A 0 A_StartSound("weapons/ftready", CHAN_WEAPON);
		Ready2:
			FLMG BCD 3 A_WeaponReady;
			Loop;
		Deselect:
			FLMG A 1 A_Lower(18);
			Loop;
		Select:
			FLMG A 1 A_Raise(18);
			Loop;
		Fire:
			TNT1 A 0 A_StartSound("weapons/ftfiring", CHAN_WEAPON, CHANF_LOOPING);
		Hold:
			FLMG E 0 bright A_GunFlash();
			FLMG E 1 bright A_FireProjectile("GWM_FTFire", 0, 1, Random(-4, 4), 0);
			FLMG F 1 bright A_FireProjectile("GWM_FTFire", 0, 0, Random(-4, 4), 0);
			FLMG G 1 bright A_FireProjectile("GWM_FTFire", 0, 1, Random(-4, 4), 0);
			FLMG H 2 bright A_ReFire();
			FLMG A 2 A_StartSound("weapons/ftend", CHAN_WEAPON);
			Goto Ready;
		Flash:
			TNT1 A 2 A_Light1();
			TNT1 A 2 A_Light2();
			TNT1 A 1 A_Light1();
			TNT1 A 1 A_Light0();
			Stop;
	}
}
class GWM_FTFire : GWM_FlameProjectile
{
	override void Tick()
	{
		Super.Tick();
		if (self.waterlevel > 0)
		{
			SetState(ResolveState(null));
		}
	}
	Default
	{
		Radius 2;
		Height 4;
		Speed 16;
		Damage 1;
		RenderStyle "Add";
		DamageType "Fire";
		Alpha 0.67;
		Scale 0.67;
		Obituary "$KILLFLAMETHROWER";
	}
	States
	{
		Spawn:
			TNT1 A 2;
			FRFX ABCD 2 bright A_Explode(5, 8, 0);
			TNT1 A 0 A_LowGravity();
			FRFX EFG 2 bright A_Explode(5, 16, 0);
			FRFX HIJ 2 bright A_Explode(5, 32, 0);
			FRFX KLM 2 bright A_Explode(5, 64, 0);
			FRFX NO 2 bright;
			Stop;
		Death:
			FRFX HIJ 2 bright A_Explode(5, 32, 0);
			TNT1 A 0 A_SpawnProjectile("GWM_DropFire", 0, 0, 0, CMF_TRACKOWNER);
			FRFX KLM 2 bright A_Explode(5, 64, 0);
			FRFX NO 2 bright;
			Stop;
	}
}
class GWM_DropFire : GWM_FlameProjectile
{
	override void Tick()
	{
		Super.Tick();
		if (self.waterlevel > 0)
		{
			SetState(ResolveState(null));
		}
	}
	Default
	{
		-NOGRAVITY
		+USEBOUNCESTATE
		+NODAMAGETHRUST
		Damage 1;
		BounceType "Classic";
		Radius 8;
		Height 40;
		ReactionTime 70;
		RenderStyle "Add";
		DamageType "Fire";
		Alpha 0.90;
		Gravity 0.125;
		Obituary "$KILLFLAMETHROWER";
	}
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_StartSound("weapons/ftburn", flags: CHANF_LOOPING, volume: 0.4);
		Burning:
			FLME ABCDEFGHIJKLMN 2 Bright A_Countdown;
			Loop;
		Bounce:
			TNT1 A 0 A_Stop;
			Goto Burning;
		Bounce.Actor:
			TNT1 A 0 {
				A_StartSound("weapons/fthit");
				bMISSILE = false;
				bHITTARGET = false;
			}
			Goto Death;
		Death:
			FLME A 2 Bright A_SetScale(0.8);
			FLME B 2 Bright A_SetScale(0.6);
			FLME C 2 Bright A_SetScale(0.4);
			FLME D 2 Bright A_SetScale(0.2);
			TNT1 A 1 A_StopSound;
			Stop;
	}
}