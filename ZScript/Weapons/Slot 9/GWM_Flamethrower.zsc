// This is a flammenwerfer. It werfs flammen!
class GWM_Flamethrower : GWM_WeaponBase
{
	Default
	{
		//$Category Weapons
		//$Title "Flamethrower
		Tag "$TAG_FLAMETHROWER";
		Weapon.SlotNumber 9;
		Weapon.UpSound "weapons/draw_primary";
		Inventory.PickupSound "misc/w_pkup";
		Inventory.PickupMessage "$GOTFLAMETHROWER";
		Obituary "$KILLFLAMETHROWER";
		Weapon.AmmoType "GWM_GasCanister";
		Weapon.AmmoGive 48;
		Weapon.AmmoUse 1;
		+Weapon.Explosive
	}
	States
	{
		Spawn:
			FLTG Z -1;
			Loop;
		Ready:
			TNT1 A 0 A_StartSound("weapons/ftready", CHAN_WEAPON);
		Ready2:
			FLTG A 1 A_WeaponReady;
			Loop;
		Deselect:
			FLTG A 1 A_Lower(18);
			Loop;
		Select:
			FLTG A 1 A_Raise(18);
			Loop;
		Fire:
			TNT1 A 0 A_StartSound("weapons/ftfiring", CHAN_WEAPON);
			FLTG A 2 A_StartSound("weapons/ftsqueak", CHAN_BODY);
			FLGG E 0 {
				A_GunFlash();
				A_FireProjectile("GWM_FTFIREFX", frandom(-10,10), false, 0, 0,0,frandom(-3,3));
				A_FireProjectile("GWM_FTFIREFX", frandom(-10,10), false, 0, 0,0,frandom(-3,3));
				A_FireProjectile("GWM_FTFIREFX", frandom(-10,10), false, 0, 0,0,frandom(-3,3));
				A_FireProjectile("GWM_FTFIREFX", frandom(-10,10), false, 0, 0,0,frandom(-3,3));
				A_FireProjectile("GWM_FTFIREFX", frandom(-10,10), false, 0, 0,0,frandom(-3,3));
			}
			FLTG E 1 bright A_FireProjectile("GWM_FTFireShot", 0, 1, Random(-4, 4), 0);
			FLTG BC 1 bright A_setpitch(pitch-2);
			FLTG D 2 A_StartSound("weapons/ftend", CHAN_WEAPON);
			FLTG EEEAA 2;
			Goto Ready;
		Flash:
			TNT1 A 2 A_Light1();
			TNT1 A 2 A_Light2();
			TNT1 A 1 A_Light1();
			TNT1 A 1 A_Light0();
			Stop;
	}
}
Class GWM_FTFireShot : GWM_FlameProjectile
{
	override void Tick()
	{
		Super.Tick();
		if (self.waterlevel > 0)
		{
			self.Destroy();
		}
	}
	
	//actor victim;
    array<actor> hitList;

    override int SpecialMissileHit(actor victim) //G: This is weird. REALLY weird.
    {
        if (hitList.Find(victim) == hitList.Size() && !self.victim && victim.bSHOOTABLE && victim != target && victim.bDORMANT == 0)
        {
			if (victim.CountInv("FireBurnCount")== 0)
			{
				victim.A_SpawnProjectile("GWM_FlameStatusThinker");
			}
			A_StartSound("Status/FireHit");
			
			victim.A_GiveInventory("FireBurnCount",25);
            victim.DamageMobj(self,target,5,'Fire');
            hitlist.Push(victim);
        }
        return 1;
    }
	Default
	{
		Radius 24;
		Height 24;
		Speed 18;
		DamageFunction (0);
		RenderStyle "Add";
		DamageType "Fire";
		Alpha 0.75;
		Scale 1.17;
		Obituary "$KILLFLAMETHROWER";
	}
	States
	{
		Spawn:
			ZFIR AABBCCDDEEFFGGHHIIJJKKLLMM 1 bright 
			{
			A_SpawnItemEX("GWM_FTFIREFX",0,frandom(-5,5),frandom(-5,5),frandom(6,15),frandom(-1,1),frandom(-2,2));
			A_ScaleVelocity(0.98);
			}
			ZFIR NOP 2 bright A_ScaleVelocity(0.9);
			Stop;
	}
}

Class GWM_FTFIREFX : Actor
{
	override void Tick()
	{
		Super.Tick();
		if (self.waterlevel > 0)
		{
			self.Destroy();
		}
	}
	Default
	{
		+NoInteraction;
		RenderStyle "Add";
		DamageType "Fire";
		Alpha 0.67;
		Scale 1;
		Speed 10;
	}
	States
	{
	Spawn:
		ZFIR ABCDEFGHIJKLMNOP 1 bright A_ScaleVelocity(0.9);
		stop;
	}
}