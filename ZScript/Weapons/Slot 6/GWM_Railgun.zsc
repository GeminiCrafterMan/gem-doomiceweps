class GWM_Railgun : DoomWeapon
{
	Default
	{
		Weapon.AmmoType "Cell";
		Tag "Railgun";
		Inventory.PickupMessage "You got the Railgun!";
		Obituary "%o was put into %k's sick frag movie.";
		Weapon.AmmoUse 10;
		Weapon.AmmoGive 40;
	}
	States
	{
		Ready:
			RLGG A 1 A_WeaponReady;
			Loop;
		Deselect:
			RLGG A 1 A_Lower;
			Loop;
		Select:
			RLGG A 1 A_Raise;
			Loop;
		Fire:
			RLGG A 35 {
				A_StartSound("weapons/railchrg");
				A_Overlay(PSP_FLASH, "Charge");
			}
			RLGG A 6 {
				A_StartSound("weapons/railshot");
				A_GunFlash();
				A_RailAttack(random(80,120), color1: "ffa35b", color2: "ffe3d3", maxdiff: 2);
			}
			RLGG BCCCCCBB 2;
			Goto Ready;
		Spawn:
			RLGG Z -1;
			Stop;
		Charge:
			RLGL ABCDE 7;
			Stop;
		Flash:
			RLGF AB 3;
			Stop;
	}
}