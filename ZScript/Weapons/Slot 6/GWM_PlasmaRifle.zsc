class GWM_PlasmaRifle : GWM_WeaponBase
{
	action void A_FireWeavingMissile(class<Actor> missile, double spawnXY = 0, double spawnZ = 0, int weaveXY = 0, int weaveZ = 0)
	{
		Actor ball = A_FireProjectile(missile, useammo: false, spawnofs_xy: spawnXY, spawnheight: spawnZ);
		if (ball)
		{
			ball.WeaveIndexXY = weaveXY;
			ball.WeaveIndexZ = weaveZ;
		}
	}
	Default
	{
		//$Category Weapons
		//$Title "Plasma Rifle
		Weapon.UpSound "weapons/draw_primary";
		Weapon.AmmoUse 1;
		Weapon.AmmoUse2 1;
		Weapon.AmmoGive 40;
		Weapon.SelectionOrder 100;
		Weapon.AmmoType "GWM_Cell";
		Weapon.AmmoType2 "GWM_Cell";
		Tag "$TAG_PLASMARIFLE";
		Inventory.PickupMessage "$GOTPLASMARIFLE";
		Obituary "$KILLPLASMARIFLE";
		ZWeapon.MaxCharge 100;
	}
	States
	{
		Recoil:
			TNT1 AAA 1 A_SetPitch(pitch+1);
			TNT1 AAAAAA 1 A_SetPitch(pitch-0.50);
			Stop;
		Ready:
			PLSG A 1 ZWL_WeaponReady();
			Loop;
		Deselect:
			PLSG A 1 A_Lower(18);
			Loop;
		Select:
			PLSG A 1 A_Raise(18);
			Loop;
		AltFire:
			TNT1 A 0 A_StartSound("weapons/icechrg", CHAN_WEAPON);
		AltHold:
			PLSG A 2
			{
				A_WeaponOffset(0+frandom(-0.4,0.4)*(invoker.chargeLevel/10), 32+frandom(0,0.8)*(invoker.chargeLevel/10), WOF_INTERPOLATE);	// G: edited the second frandom call to not allow edges of the weapon to be seen
//				A_SpawnItemEx("IceFog", 24, 0, 32, 0.5, 0, FRandom(-0.5, 0.5), Random(-12, 12));

				switch(invoker.chargeLevel)
				{
					case 1:
						A_StartSound("weapons/icelv2", CHAN_BODY);
						break;
					case 25:
						A_StartSound("weapons/icelv2", CHAN_BODY);
						break;
					case 50:
						A_StartSound("weapons/icelv2", CHAN_BODY);
						break;
					case 75:
						A_StartSound("weapons/icelv2", CHAN_BODY);
						break;
					case 99:
						A_StartSound("weapons/icelv3", CHAN_BODY);
						break;
				}
				if (invoker.chargeLevel >= 0)
					A_SpawnItemEx("GWM_PlasmaRifleChargeFog", 24, 0, 32, 0.5, 0, FRandom(-0.5, 0.5), Random(-12, 12));
				if (invoker.chargeLevel >= 25)
					A_SpawnItemEx("GWM_PlasmaRifleChargeFog", 24, 0, 32, 0.5, 0, FRandom(-0.5, 0.5), Random(-12, 12));
				if (invoker.chargeLevel >= 50)
					A_SpawnItemEx("GWM_PlasmaRifleChargeFog", 24, 0, 32, 0.5, 0, FRandom(-0.5, 0.5), Random(-12, 12));
				if (invoker.chargeLevel >= 75)
					A_SpawnItemEx("GWM_PlasmaRifleChargeFog", 24, 0, 32, 0.5, 0, FRandom(-0.5, 0.5), Random(-12, 12));
				if (invoker.chargeLevel >= 100)
					A_SpawnItemEx("GWM_PlasmaRifleChargeFog", 24, 0, 32, 0.5, 0, FRandom(-0.5, 0.5), Random(-12, 12));
			}
			ISHT A 0
			{
				if (invoker.chargeLevel < 1 && invoker.Ammo2.Amount < 5)
					A_Refire();
				if (invoker.chargeLevel < 25 && invoker.Ammo2.Amount < 10)
					A_Refire();
				if (invoker.chargeLevel < 50 && invoker.Ammo2.Amount < 20)
					A_Refire();
				if (invoker.chargeLevel < 75 && invoker.Ammo2.Amount < 30)
					A_Refire();
				if (invoker.chargeLevel < 100 && invoker.Ammo2.Amount < 40)
					A_Refire();
			}
			ISHT A 0 ZWL_Charge();
			ISHT A 0 A_Refire();
			PLSG A 0 A_Jumpif(invoker.chargeLevel >= 100, "Fire.Level6");
			PLSG A 0 A_Jumpif(invoker.chargeLevel >= 75, "Fire.Level5");
			PLSG A 0 A_Jumpif(invoker.chargeLevel >= 50, "Fire.Level4");
			PLSG A 0 A_Jumpif(invoker.chargeLevel >= 25, "Fire.Level3");
			PLSG A 0 A_Jumpif(invoker.chargeLevel >= 1, "Fire.Level2");
			PLSG A 0 A_Jumpif(invoker.chargeLevel >= 0, "Fire.Level1");
			Goto AltFire;
		Fire:
		Fire.Level1:
			PLSG A 1 {
				invoker.chargeLevel = 0;
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_StartSound("weapons/plasshot", CHAN_WEAPON);
				A_FireProjectile("GWM_PlasmaBall");
				A_SpawnItemEx("GWM_PlasmaFlash");
				A_GunFlash();
				A_WeaponOffset(0, 8, WOF_ADD);
				A_OverlayScale(OverlayID(), 1.10, 0, WOF_INTERPOLATE);
			}
			Goto Fire.Done;
		Fire.Level2:
			PLSG A 1 {
				invoker.chargeLevel = 0;
				invoker.Ammo2.Amount -= 5;
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_StartSound("weapons/railshot", CHAN_WEAPON);
				A_SpawnItemEx("GWM_PlasmaFlash");
				A_GunFlash();
				A_WeaponOffset(0, 8, WOF_ADD);
				A_OverlayScale(OverlayID(), 1.10, 0, WOF_INTERPOLATE);
				A_FireProjectile("GWM_PlasmaBallLevel2");
			}
			Goto Fire.Done;
		Fire.Level3:
			PLSG A 1 {
				invoker.chargeLevel = 0;
				invoker.Ammo2.Amount -= 10;
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_StartSound("weapons/railshot", CHAN_WEAPON);
				A_SpawnItemEx("GWM_PlasmaFlash");
				A_GunFlash();
				A_WeaponOffset(0, 8, WOF_ADD);
				A_OverlayScale(OverlayID(), 1.10, 0, WOF_INTERPOLATE);
				A_Recoil(1);
				A_FireWeavingMissile("GWM_PlasmaBallLevel2Weaving", 0, -6, 0, 48);
			}
			Goto Fire.ChargeShotDone;
		Fire.Level4:
			PLSG A 1 {
				invoker.chargeLevel = 0;
				invoker.Ammo2.Amount -= 20;
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_StartSound("weapons/railshot", CHAN_WEAPON);
				A_SpawnItemEx("GWM_PlasmaFlash");
				A_GunFlash();
				A_WeaponOffset(0, 8, WOF_ADD);
				A_OverlayScale(OverlayID(), 1.10, 0, WOF_INTERPOLATE);
				A_Recoil(2);
				A_FireWeavingMissile("GWM_PlasmaBallLevel2Weaving", 0, -6, 0, 48);
				A_FireWeavingMissile("GWM_PlasmaBallLevel2Weaving", 0, 6, 32, 16);
			}
			Goto Fire.ChargeShotDone;
		Fire.Level5:
			PLSG A 1 {
				invoker.chargeLevel = 0;
				invoker.Ammo2.Amount -= 30;
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_StartSound("weapons/railshot", CHAN_WEAPON);
				A_SpawnItemEx("GWM_PlasmaFlash");
				A_GunFlash();
				A_WeaponOffset(0, 8, WOF_ADD);
				A_OverlayScale(OverlayID(), 1.10, 0, WOF_INTERPOLATE);
				A_Recoil(3);
				A_FireWeavingMissile("GWM_PlasmaBallLevel2Weaving", 0, -6, 0, 48);
				A_FireWeavingMissile("GWM_PlasmaBallLevel2Weaving", -5.196, 3, 21, 5);
				A_FireWeavingMissile("GWM_PlasmaBallLevel2Weaving", 5.196, 3, 42, 26);
			}
			Goto Fire.ChargeShotDone;
		Fire.Level6:
			PLSG A 1 {
				invoker.chargeLevel = 0;
				invoker.Ammo2.Amount -= 40;
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_StartSound("weapons/railshot", CHAN_WEAPON);
				A_SpawnItemEx("GWM_PlasmaFlash");
				A_GunFlash();
				A_WeaponOffset(0, 8, WOF_ADD);
				A_OverlayScale(OverlayID(), 1.10, 0, WOF_INTERPOLATE);
				A_Recoil(4);
				A_FireWeavingMissile("GWM_PlasmaBallLevel2Weaving", 0, -6, 0, 48);
				A_FireWeavingMissile("GWM_PlasmaBallLevel2Weaving", 0, 6, 32, 16);
				A_FireWeavingMissile("GWM_PlasmaBallLevel2Weaving", -6, 0, 16, 0);
				A_FireWeavingMissile("GWM_PlasmaBallLevel2Weaving", 6, 0, 48, 32);
			}
			Goto Fire.ChargeShotDone;
		Fire.Done:
			PLSG AA 1 {
				A_WeaponOffset(0, -4, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.05, 0, WOF_ADD);
			}
			TNT1 A 0 A_CheckReload;
			PLSG B 3 A_ReFire;
			PLSG C 9;
			PLSG B 3;
			Goto Ready;
		Fire.ChargeShotDone:
			PLSG AA 1 {
				A_WeaponOffset(0, -4, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.05, 0, WOF_ADD);
			}
			TNT1 A 0 A_CheckReload;
			PLSG E 7 A_StartSound("weapons/railchrg", CHAN_BODY);
			PLSG CFGH 7;
			PLSG I 5;
			Goto Ready;
		Flash:
			PLSF A 1 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayScale(OverlayID(), 1.10, 0, WOF_INTERPOLATE);
				A_Light1();
			}
			PLSF BC 1 Bright {
				A_OverlayScale(OverlayID(), -0.05, 0, WOF_ADD);
				A_Light1();
			}
			Goto LightDone;
		Spawn:
			PLSG Z -1;
			Stop;
	}
}
class GWM_PlasmaRifleChargeFog : Actor
{
	Default
	{
		Scale 0.4;
		Alpha 0.25;
		Renderstyle "Add";
		+NOGRAVITY
	}
	States
	{
		Spawn:
			PLSE CDE 3 Bright;
			Stop;
	}
}
class GWM_PlasmaBall : PlasmaBall
{
	Default
	{
		Decal "PlasmaScorch";
		DamageFunction (23);
		Renderstyle "Add";
		Alpha 1.0;
		SeeSound "weapons/plasfly";
		DeathSound "weapons/plasxpl";
	}
	States
	{
		Spawn:
			PLSS A 1 nodelay A_ChangeVelocity(frandom(-0.3,0.3),frandom(-0.3,0.3),frandom(-0.3,0.3));
		Spawnloop:
			PLSS ABADAFACAEAG 1 bright A_SpawnItemEX("GWM_PlasmaBallFlareFX");
			loop;
	}
}
class GWM_PlasmaBallFlareFX : actor
{
	Default
	{
		+NOINTERACTION;
		Renderstyle "Add";
		Scale 1.7;
		Alpha 0.15;
	}
States
	{
	Spawn:
	PLSS X 2 nodelay bright;
	stop;
	}
}

class GWM_PlasmaBallLevel2 : GWM_PlasmaBall
{
	Default
	{
		Scale 1.5;
		+SHOOTABLE
		Obituary "$KILLPLASMARIFLE_CHARGE";
		DamageFunction (115);
	}
	States
	{
		Spawn:
			PLSS A 1 nodelay A_ChangeVelocity(frandom(-0.3,0.3),frandom(-0.3,0.3),frandom(-0.3,0.3));
		Spawnloop:
			PLSS ABADAFACAEAG 1 bright A_SpawnItemEX("GWM_PlasmaBallChargedFlareFX"); // this one looks dumb with the trail
			loop;
	}
}
class GWM_PlasmaBallLevel2Weaving : GWM_PlasmaBallLevel2
{
	States
	{
		Spawn:
			PLSS ABADAFACAEAG 1 bright {
				A_SpawnItemEX("GWM_PlasmaBallChargedFlareFX");
				A_SpawnItemEX("GWM_PlasmaBallWeavingTrail");
				A_Weave(4, 4, 0.75, 0.75);
			}
			loop;
	}
}
class GWM_PlasmaBallChargedFlareFX : GWM_PlasmaBallFlareFX
{
	Default
	{
		Scale 0.85;
	}
}
class GWM_PlasmaBallWeavingTrail : GWM_PlasmaRifleChargeFog
{
	Default
	{
		Scale 0.6; // 3x size since xaser's original charge bolts were 0.5x, and this was 0.2x
	}
}
