class GWM_PlasmaRifle : GWM_WeaponBase
{
	Default
	{
		Weapon.UpSound "weapons/draw_primary";
		Weapon.AmmoUse 1;
		Weapon.AmmoUse2 20;
		Weapon.AmmoGive 40;
		Weapon.SelectionOrder 100;
		Weapon.AmmoType "GWM_Cell";
		Weapon.AmmoType2 "GWM_Cell";
		Decal "RailScorch";
		Tag "$TAG_PLASMARIFLE";
		Inventory.PickupMessage "$GOTPLASMARIFLE";
		Obituary "$KILLPLASMARIFLE_RAIL";
		AttackSound "weapons/plasmashot";
	}
	States
	{
		Recoil:
			TNT1 AAA 1 A_SetPitch(pitch+1);
			TNT1 AAAAAA 1 A_SetPitch(pitch-0.50);
			Stop;
		Ready:
			PLSG A 1 A_WeaponReady;
			Loop;
		Deselect:
			PLSG A 1 A_Lower(18);
			Loop;
		Select:
			PLSG A 1 A_Raise(18);
			Loop;
		Fire:
			PLSG A 1 {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				ZWL_FireProjectile("GWM_PlasmaBall", 0);
				A_SpawnItemEx("GWM_PlasmaFlash");
				A_GunFlash();
				A_WeaponOffset(0, 4, WOF_ADD);
				A_OverlayScale(OverlayID(), 1.10, 0, WOF_INTERPOLATE);
			}
			PLSG AA 1 {
				A_WeaponOffset(0, -2, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.05, 0, WOF_ADD);
			}
			TNT1 A 0 A_CheckReload;
			PLSG B 3 A_ReFire;
			PLSG C 9;
			PLSG B 3;
			Goto Ready;
		AltFire:
			PLSG A 6 {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_SpawnItemEx("GWM_PlasmaFlash");
				A_GunFlash("RailFlash");
				A_RailAttack(270, color1: "1baaff", color2: "73e4ff", maxdiff: 4);
				A_StartSound("weapons/railshot", CHAN_WEAPON);
				A_WeaponOffset(0, 6, WOF_ADD);
				A_Overlay(55,"Recoil");
				A_QuakeEx(3, 3, 3, 4, 0, 1, "", QF_SHAKEONLY);
				A_OverlayScale(OverlayID(), 1.30, 0, WOF_INTERPOLATE);
				A_ChangeVelocity(-Cos(pitch)*2, 0, sin(pitch)*2,CVF_RELATIVE);
			}
			PLSG D 5 {
				A_WeaponOffset(0, -2, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.15, 0, WOF_ADD);
			}
			PLSG D 5 {
				A_WeaponOffset(0, -4, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.15, 0, WOF_ADD);
			}
			TNT1 A 0 A_CheckReload;
			PLSG E 7 A_StartSound("weapons/railchrg", CHAN_BODY);
			PLSG CFGH 7;
			PLSG I 5;
			Goto Ready;
		Flash:
			PLSF A 1 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayScale(OverlayID(), 1.10, 0, WOF_INTERPOLATE);
				A_Light1();
			}
			PLSF BC 1 Bright {
				A_OverlayScale(OverlayID(), -0.05, 0, WOF_ADD);
				A_Light1();
			}
			Goto LightDone;
		RailFlash:
			PLSF A 1 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayScale(OverlayID(), 1.30, 0, WOF_INTERPOLATE);
				A_Light1();
			}
			PLSF BC 1 Bright A_Light1;
			Goto LightDone;
		Spawn:
			PLSG Z -1;
			Stop;
	}
}
class GWM_PlasmaBall : PlasmaBall
{
	Default
	{
		Obituary "$KILLPLASMARIFLE";
		DamageFunction (23);
		Renderstyle "Add";
		Alpha 1.0;
	}
	States
	{
	Spawn:
	PLSS A 1 nodelay A_ChangeVelocity(frandom(-0.3,0.3),frandom(-0.3,0.3),frandom(-0.3,0.3));
	Spawnloop:
	PLSS ABADAFACAEAG 1 bright A_SpawnItemEX("GWM_PlasmaBallFlareFX");
	loop;
	}
	
}

class GWM_PlasmaBallFlareFX : actor
{
	Default
	{
		+NOINTERACTION;
		Renderstyle "add";
		Scale 1.7;
		Alpha 0.15;
	}
States
	{
	Spawn:
	PLSS X 2 nodelay bright;
	stop;
	}
}
	
	
	
	