class GWM_PlasmaRifle : PlasmaRifle
{
	Default
	{
		Weapon.AmmoUse 1;
		Weapon.AmmoUse2 20;
		Weapon.AmmoType "Cell";
		Weapon.AmmoType2 "Cell";
		Decal "RailScorch";
	}
	States
	{
		Deselect:
			PLSG A 1 A_Lower(12);
			Loop;
		Select:
			PLSG A 1 A_Raise(12);
			Loop;
		Fire:
			PLSG A 1 {
				A_FireProjectile("PlasmaBall");
				A_StartSound("weapons/plasmashot");
				A_SpawnItemEx("GWM_PlasmaFlash");
				A_GunFlash();
				A_WeaponOffset(0, 6, WOF_ADD);
			}
			PLSG A 2 A_WeaponOffset(0, -6, WOF_ADD);
			TNT1 A 0 A_CheckReload;
			PLSG B 3 A_ReFire;
			PLSG C 9;
			PLSG B 3;
			Goto Ready;
		AltFire:
			PLSG A 6 {
				A_SpawnItemEx("GWM_PlasmaFlash");
				A_GunFlash();
				A_RailAttack(10*random(24,30), color1: "1baaff", color2: "73e4ff", maxdiff: 4);
				A_StartSound("weapons/railshot", CHAN_WEAPON);
				A_WeaponOffset(0, 6, WOF_ADD);
			}
			PLSG D 10 A_WeaponOffset(0, -6, WOF_ADD);
			TNT1 A 0 A_CheckReload;
			PLSG E 7 A_StartSound("weapons/railchrg", CHAN_BODY);
			PLSG CFGH 7;
			PLSG I 5;
			Goto Ready;
		Flash:
			PLSF ABC 1 Bright A_Light1;
			Goto LightDone;
		Spawn:
			PLSG Z -1;
			Stop;
	}
}