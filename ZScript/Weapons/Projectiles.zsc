class GWM_IceProjectile : Actor // the absolute most basic projectile base possible
{
	actor victim;
	override int SpecialMissileHit(actor victim) //thanks mengo
    {
        if (!self.victim && victim.bSHOOTABLE && victim != target && victim.bDORMANT == 0)
		{
		if (victim.health <= self.Damage)
			{
			victim.A_SpawnProjectile("GWM_IceDamageEffect");
			victim.A_Die("ice");
			return 1;
			}
		victim.A_SpawnProjectile("GWM_IceDamageEffect");
		}
        return -1; // 1 to keep going on, -1 to die the appropriate death (XDeath in this case, since ideally it hit a bleeding actor)
    }
	Default
	{
		Projectile;
		DamageType "Ice";
		+ICESHATTER;
	}
}

class GWM_ShotgunIce : GWM_IceProjectile
{
	Default
	{
		Radius 13;
		Height 13;
		Speed 50;
		Damage 30;
		Scale 2;
		+RANDOMIZE
		+FORCEXYBILLBOARD
//		SeeSound "weapons/iceshot";
		DeathSound "weapons/icehit";
		Obituary "%o got frozen solid by %k's Ice Shotgun.";
	}
	States
	{
		Spawn:
			SHGI A 6 Bright A_SpawnItemEx("GWM_ShotgunIceTrail");
			loop;
		Death:
			TNT1 A 1 {
				A_SpawnItemEx("GWM_ShotgunIceChunk", 0, 0, 0, -50, 0, 0, 10);
				A_SpawnItemEx("GWM_ShotgunIceChunk", 0, 0, 0, -50, 0, 5, 0);
				A_SpawnItemEx("GWM_ShotgunIceChunk", 0, 0, 0, -50, 0, 0, 0);
				A_SpawnItemEx("GWM_ShotgunIceChunk", 0, 0, 0, -50, 0, -5, 0);
				A_SpawnItemEx("GWM_ShotgunIceChunk", 0, 0, 0, -50, 0, 0, -10);
			}
			stop;
		XDeath:
			SHGI FBCDE 4 Bright;
//			TNT1 A 0 A_ScaleVelocity(0.50, AAPTR_TARGET);	// AAPTR_TARGET is the player, for some reason.
			Stop;
	}
}

class GWM_ShotgunIceChunk : GWM_ShotgunIce
{
	Default
	{
		Radius 7;
		Height 7;
		Damage 6;
		+RIPPER
		BounceType "Doom";
		BounceCount 2;
	}
	States
	{
		Spawn:
			SHGI F 6 Bright A_SpawnItemEx("GWM_ShotgunIceTrail");
			loop;
		Death:
			SHGI BCDE 4 Bright;
			stop;
	}
}

class GWM_ShotgunIceTrail : Actor
{
	Default
	{
		Radius 13;
		Height 8;
		Speed 45; // parent - 5
		Damage 0;
		Projectile;
		+RANDOMIZE;
		RenderStyle "Add";
		Alpha 0.75;
	//  SeeSound "weapons/iceshot";
		DeathSound "weapons/icehit";
		Obituary "%o got frozen solid by %k.";
	}
	States
	{
		Spawn:
			SHGI BCDE 6 Bright A_FadeOut(0.2);
			loop;
		Death:
			SHGI BCDE 4 Bright;
			stop;
	}
}

class GWM_PerfectFreezeBullet : GWM_IceProjectile
{
	action State PerfectFreezeTimerJump()
	{
		if (GetAge() == 3*35) {
			A_StartSound("weapons/power1");
			A_Stop();
			return ResolveState("White");
		}
		if (GetAge() >= 30*35) {A_FadeOut();}
		return State(null);
	}
	Default
	{
//		Scale 0.5;
		Radius 6;
		Height 8;
		Speed 20;
		Damage 3;
		Projectile;
		+RIPPER
		+FORCEXYBILLBOARD
		+NOBLOCKMAP
		+NOGRAVITY
		DamageType "Ice";
		+ICESHATTER;
		BounceType "Doom";
		DeathSound "weapons/pfgraze";
		RipSound "weapons/pfdamage";
	}
	States
	{
		Spawn:
			PFBU F 1 Bright A_Jump(256, "Red", "Blue", "Green", "Yellow", "Orange");
			Loop;
		Red:
			PFBU A 1 Bright PerfectFreezeTimerJump;
			Loop;
		Blue:
			PFBU B 1 Bright PerfectFreezeTimerJump;
			Loop;
		Green:
			PFBU C 1 Bright PerfectFreezeTimerJump;
			Loop;
		Yellow:
			PFBU D 1 Bright PerfectFreezeTimerJump;
			Loop;
		Orange:
			PFBU E 1 Bright PerfectFreezeTimerJump;
			Loop;
		White:
			PFBU F 1 Bright {
				bRIPPER = false;
				bBOUNCEONWALLS = false;
				bBOUNCEONFLOORS = false;
				bBOUNCEONCEILINGS = false;
				bBOUNCEAUTOOFFFLOORONLY = false;
				PerfectFreezeTimerJump();
			}
			Loop;
		Death:
			PFBU F 1 Bright A_FadeOut(0.1);
			Loop;
	}
}

class GWM_FlameProjectile : Actor // the absolute most basic projectile base possible
{
	actor victim;
	override int SpecialMissileHit(actor victim) //thanks mengo
    {
        if (!self.victim && victim.bSHOOTABLE && victim != target && victim.bDORMANT == 0)
		{
		if (victim.health <= self.Damage)
			{
			victim.A_SpawnProjectile("GWM_FlameDamageEffect");
			victim.A_Die("fire");
			return 1;
			}
		victim.A_SpawnProjectile("GWM_FlameDamageEffect");
		}
        return -1; // 1 to keep going on, -1 to die the appropriate death (XDeath in this case, since ideally it hit a bleeding actor)
    }
	Default
	{
		Projectile;
		DamageType "Fire";
	}
}

class GWM_PoisonProjectile : Actor // the absolute most basic projectile base possible
{
	actor victim;
	override int SpecialMissileHit(actor victim) //thanks mengo
    {
        if (!self.victim && victim.bSHOOTABLE && victim != target && victim.bDORMANT == 0)
		{
		if (victim.health <= self.Damage)
			{
			victim.A_SpawnProjectile("GWM_PoisonDamageEffect");
			victim.A_Die("poison");
			return 1;
			}
		victim.A_SpawnProjectile("GWM_PoisonDamageEffect");
		}
        return -1; // 1 to keep going on, -1 to die the appropriate death (XDeath in this case, since ideally it hit a bleeding actor)
    }
	Default
	{
		Projectile;
		DamageType "Poison";
	}
}

class GWM_FireDamage : CustomInventory
{
	States
	{
		Pickup:
			TNT1 A 0 A_SpawnItemEx("GWM_FlameDamageEffect",0,0,20,0,0,0,0,SXF_SETMASTER);
			Stop;
	}
}

class GWM_PoisonDamage : CustomInventory
{
	States
	{
		Pickup:
			TNT1 A 0 A_SpawnItemEx("GWM_PoisonDamageEffect",0,0,20,0,0,0,0,SXF_SETMASTER);
			Stop;
	}
}

class GWM_VenomBall : GWM_PoisonProjectile
{
	Default
	{
		Speed 20;
		Damage 80;
		Height 24;
		Radius 24;
		BounceCount 11;
		Scale 0.25;
		Gravity 0.33;
		-NOGRAVITY
		+BOUNCEONWALLS
		+BOUNCEONFLOORS
		+BOUNCEONCEILINGS
		+BOUNCEAUTOOFF
		+USEBOUNCESTATE
		+FORCEXYBILLBOARD
		+RANDOMIZE
		BounceSound "weapons/vnmcsplt";
		DeathSound "weapons/vnmcbomb";
		Obituary "%o drank %k's toxic goop.";
	}
	States
	{
		Spawn:
			VNMP A 6 A_SetScale(0.25, 0.25);
			VNMP A 2 A_SetScale(0.25, 0.24);
			VNMP A 2 A_SetScale(0.25, 0.23);
			VNMP A 2 A_SetScale(0.25, 0.22);
			VNMP A 2 A_SetScale(0.25, 0.21);
			VNMP A 2 A_SetScale(0.25, 0.20);
			VNMP A 2 A_SetScale(0.25, 0.21);
			VNMP A 2 A_SetScale(0.25, 0.22);
			VNMP A 2 A_SetScale(0.25, 0.23);
			VNMP A 2 A_SetScale(0.25, 0.24);
			VNMP A 6 A_SetScale(0.25, 0.25);
			VNMP A 2 A_SetScale(0.24, 0.25);
			VNMP A 2 A_SetScale(0.23, 0.25);
			VNMP A 2 A_SetScale(0.22, 0.25);
			VNMP A 2 A_SetScale(0.21, 0.25);
			VNMP A 2 A_SetScale(0.20, 0.25);
			VNMP A 2 A_SetScale(0.21, 0.25);
			VNMP A 2 A_SetScale(0.22, 0.25);
			VNMP A 2 A_SetScale(0.23, 0.25);
			VNMP A 2 A_SetScale(0.24, 0.25);
			Loop;
		Bounce.Floor:
			VNMP A 1 A_SetScale(0.25, 0.1875);
			VNMP A 1 A_SetScale(0.25, 0.125);
			VNMP A 1 A_SetScale(0.25, 0.1875);
			VNMP A 1 A_SetScale(0.25, 0.25);
			Goto Spawn;
		Bounce.Wall:
			TNT1 A 0 A_JumpIf(random(0, 1), "Bounce.Floor");	// Randomly jumps to the floor bounce version, for variation.
			VNMP A 1 A_SetScale(0.1875, 0.25);
			VNMP A 1 A_SetScale(0.125, 0.25);
			VNMP A 1 A_SetScale(0.1875, 0.25);
			VNMP A 1 A_SetScale(0.25, 0.25);
			Goto Spawn;
		Death:
			TNT1 A 0 {
				A_Scream();
				A_SetScale(0.25, 0.25);
				A_SpawnItemEx("GWM_SmallVenomBall", xvel: 5, zvel: 10, angle: random(1,360));
				A_SpawnItemEx("GWM_SmallVenomBall", xvel: 5, zvel: 10, angle: random(1,360));
				A_SpawnItemEx("GWM_SmallVenomBall", xvel: 5, zvel: 10, angle: random(1,360));
				A_SpawnItemEx("GWM_SmallVenomBall", xvel: 5, zvel: 10, angle: random(1,360));
				A_SpawnItemEx("GWM_SmallVenomBall", xvel: 5, zvel: 10, angle: random(1,360));
				A_SpawnItemEx("GWM_MainBallPuddleSpawner");
			}
			VNMP BCDEF 1;
			Stop;
	}
}

class GWM_SmallVenomBall : GWM_VenomBall
{
	Default
	{
		Speed 0;
		Damage 16;
		Height 12;
		Radius 12;
		Scale 0.125;
		Gravity 0.66;
	}
	States
	{
		Spawn:
			VNMP A 6 A_SetScale(0.125, 0.125);
			VNMP A 2 A_SetScale(0.125, 0.124);
			VNMP A 2 A_SetScale(0.125, 0.123);
			VNMP A 2 A_SetScale(0.125, 0.122);
			VNMP A 2 A_SetScale(0.125, 0.121);
			VNMP A 2 A_SetScale(0.125, 0.120);
			VNMP A 2 A_SetScale(0.125, 0.121);
			VNMP A 2 A_SetScale(0.125, 0.122);
			VNMP A 2 A_SetScale(0.125, 0.123);
			VNMP A 2 A_SetScale(0.125, 0.124);
			VNMP A 6 A_SetScale(0.125, 0.125);
			VNMP A 2 A_SetScale(0.124, 0.125);
			VNMP A 2 A_SetScale(0.123, 0.125);
			VNMP A 2 A_SetScale(0.122, 0.125);
			VNMP A 2 A_SetScale(0.121, 0.125);
			VNMP A 2 A_SetScale(0.120, 0.125);
			VNMP A 2 A_SetScale(0.121, 0.125);
			VNMP A 2 A_SetScale(0.122, 0.125);
			VNMP A 2 A_SetScale(0.123, 0.125);
			VNMP A 2 A_SetScale(0.124, 0.125);
			Loop;
		Bounce.Floor:
			TNT1 A 0 A_SpawnItemEx("GWM_FloorVenomPuddle");
			Goto Death;
		Bounce.Wall:
			TNT1 A 0 A_SpawnItemEx("GWM_WallVenomPuddle");
			Goto Death;
		Bounce.Ceiling:
			VNMP A 1 A_SetScale(0.125,0.09375);
			VNMP A 1 A_SetScale(0.125,0.0625);
			VNMP A 1 A_SetScale(0.125,0.09375);
			VNMP A 1 A_SetScale(0.125,0.125);
			Goto Spawn;
		Death:
			TNT1 A 0 {
				A_Scream();
				A_SetScale(0.125, 0.125);
			}
			VNMP BCDEF 1;
			Stop;
	}
}

class GWM_MainBallPuddleSpawner : GWM_SmallVenomBall
{
	Default
	{
		Scale 0.25;
		DeathSound "";
		BounceSound "";
		+NOINTERACTION
	}
	States
	{
		Spawn:
			TNT1 A 1;
			Loop;
		// No need for bounce states, because they're invisible in
		// SmallVenomBall (they all lead to Death), and the Ceiling one
		// doesn't apply, since large venom balls can't pop on ceilings.
		Death:
			Stop;
	}
}

class GWM_FloorVenomPuddle : GWM_PoisonProjectile
{
	Default
	{
		+FLATSPRITE
		Damage 1;
	}
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_JumpIf(random(0, 1), "Puddle3");
			TNT1 A 0 A_JumpIf(random(0, 1), "Puddle2");
		Puddle1:
			VNMP G 1;
			Loop;
		Puddle2:
			VNMP H 1;
			Loop;
		Puddle3:
			VNMP I 1;
			Loop;
	}
}

class GWM_WallVenomPuddle : GWM_FloorVenomPuddle
{
	Default
	{
		-FLATSPRITE
		+NOGRAVITY
		+WALLSPRITE
	}
}