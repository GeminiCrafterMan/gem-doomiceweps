class GWM_ZombieShotgun : GWM_WeaponBase
{
	Default
	{
		//$Category Weapons
		//$Title "Zombie Shotgun
		Weapon.SelectionOrder 1600; // dude this thing sucks ASS but at least it's a shotgun
		Weapon.SlotNumber 3;
		Inventory.PickupSound "weapons/zsgpickup";
		Weapon.UpSound "weapons/draw_secondary";
		Weapon.AmmoType "GWM_Shell";
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 7;
		
		ZWeapon.MagazineSize 7;
		ZWeapon.ReloadSound "weapons/shell_load";
		ZWeapon.ClickSound "weapons/skempty";

		Tag "$TAG_ZOMBIESHOTGUN";
		Inventory.PickupMessage "$GOTZOMBIESHOTGUN";
		Obituary "$KILLZOMBIESHOTGUN";
		AttackSound "weapons/zsgshot";
	}
	States
	{
		Spawn:
			ZSHG Z -1;
			Stop;
/*		Select:
			ZSHG A 1 A_Raise(18);
			Loop;
		Deselect:
			ZSHG A 1 A_Lower(18);
			Loop;*/
		Select:
			TNT1 A 0 {
				ZWL_QuickRaise();
				A_WeaponOffset(64, 160);
			}
			ZSHG NNNNNN 1 {
				A_WeaponOffset(-8, -16, WOF_ADD);
			}
			ZSHG AA 1 {
				A_WeaponOffset(-8, -16, WOF_ADD);
			}
			ZSHG H 4 {A_StartSound("weapons/zsgpump1"); ZWL_WeaponReady( );}
			ZSHG GGFF 1 ZWL_WeaponReady( );
			ZSHG A 1 {A_StartSound("weapons/zsgpump2"); ZWL_WeaponReady( );}
			Goto Ready;
		Deselect:
			ZSHG AA 1 {
				A_WeaponOffset(8, 16, WOF_ADD);
			}
			ZSHG NNNNNN 1 {
				A_WeaponOffset(8, 16, WOF_ADD);
			}
			TNT1 A 0 A_WeaponOffset(0, flags: WOF_KEEPY);
			TNT1 A 0 ZWL_QuickLower;
			Stop;
		Ready:
			ZSHG A 1 ZWL_WeaponReady( );
			Loop;
		Fire:
			ZSHG A 1 {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayPivot(OverlayID(), 0.2, flags: WOF_KEEPY);
				ZWL_FireHitscan(0, 10, pellets: 7, spread: 5.6, tracerType: "GWM_Tracer");
				A_SpawnItemEx("GWM_Flash");
				A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5);
				A_GunFlash();
				A_WeaponOffset(0, 42, WOF_INTERPOLATE);
				A_OverlayScale(OverlayID(), 1.15, 0, WOF_INTERPOLATE);
				A_QuakeEx(1.5, 1.5, 1.5, 4, 0, 1, "", QF_SHAKEONLY);
			}
			ZSHG A 1 {
				A_WeaponOffset(0, -5, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			ZSHG A 1 {
				A_WeaponOffset(0, -5, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			ZSHG A 1;
			TNT1 A 0 ZWL_CheckReload();		
			ZSHG B 6;
			ZSHG CCCDDD 1;
			ZSHG E 5 {
				A_StartSound("weapons/zsgpump1");
				ZWL_EjectCasing("GWM_ShotgunCasing", offset: (24, -6, -14));
			}
			ZSHG DDCCC 1; // These two play on CHAN_BODY because the firing sound is just too fucking long
			ZSHG B 6 A_StartSound("weapons/zsgpump2");
			ZSHG A 3;
			ZSHG A 0 ZWL_ReFire();
			Goto Ready;
		Reload:
			TNT1 A 0 {
				if (!invoker.CheckAmmo(PrimaryFire, false, true))
					player.SetPSPrite(OverlayID(), ResolveState("Ready")); // Disallow reloading if there's no more ammo
			}
			ZSHG B 6;
			TNT1 A 0 ZWL_JumpIfEmpty("Reload.Empty");
			ZSHG CCCRR 1;
			ZSHG Q 2;
			// Fallthrough
		Reload.Loop:
			ZSHG Q 2 A_WeaponOffset(1,31);
			ZSHG Q 2 A_WeaponOffset(2,31);
			ZSHG Q 2 A_WeaponOffset(2,32);
			TNT1 A 0 ZWL_Reload(1);
			ZSHG Q 2 A_WeaponOffset(1,33);
			ZSHG Q 2 A_WeaponOffset(0,33);
			ZSHG E 0 ZWL_JumpIfReloaded("Reload.End", true);
			Loop;
		Reload.End:
			ZSHG A 0 A_WeaponOffset(0,32);
			ZSHG RRCCC 1;
			ZSHG B 6;
			Goto Ready;

		Reload.Empty:
			ZSHG CCCDD 1;
			ZSHG E 5 A_StartSound("weapons/zsgpump1");
			ZSHG E 2 A_WeaponOffset(1,31);
			ZSHG E 2 A_WeaponOffset(2,31);
			ZSHG E 2 A_WeaponOffset(2,32);
			TNT1 A 0 ZWL_Reload(1);
			ZSHG E 2 A_WeaponOffset(1,33);
			ZSHG E 2 A_WeaponOffset(0,33);
			ZSHG O 4 A_WeaponOffset(0,32);
			ZSHG P 2;
			ZSHG Q 6 A_StartSound("weapons/zsgpump2");
			ZSHG E 0 ZWL_JumpIfReloaded("Reload.End", true);
			Goto Reload.Loop;

		Flash:
			ZSHF A 1 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayPivot(OverlayID(), 0.2, flags: WOF_KEEPY);
				A_Light1();
				A_OverlayScale(OverlayID(), 1.15, 0, WOF_INTERPOLATE);
			}
			ZSHF A 1 BRIGHT
			{
				A_Light1();
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			ZSHF B 1 Bright {
				A_Light2();
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			Goto LightDone;
	}
}