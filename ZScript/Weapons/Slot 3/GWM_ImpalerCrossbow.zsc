class GWM_ImpalerCrossbow : GWM_WeaponBase replaces Crossbow
{
	Default
	{
		Weapon.SelectionOrder 1300;
		Inventory.PickupMessage "$GOTIMPALERCROSSBOW";
		Tag "$TAG_IMPALERCROSSBOW";
		Weapon.AmmoGive 15;
		Weapon.AmmoType "GWM_ImpalerBolts";
		Weapon.AmmoUse 1;
		Weapon.KickBack 20;
		Weapon.SisterWeapon "GWM_ImpalerCrossbowPowered";
	}
	States
	{
		Spawn:
			CBOW Z -1;
			Stop;
		Select:
			CBOW E 1 A_Raise(18);
			Loop;
		Deselect:
			CBOW E 1 A_Lower(18);
			Loop;
		Ready:
			CBOW E 0 A_JumpIfNoAmmo("FakeReady");
			CBOW A 1 A_WeaponReady;
			Loop;
		FakeReady:
			CBOW E 1 A_WeaponReady;
			Loop;
		Fire:
			CBOW B 6 
			{
				A_StartSound("weapons/xbowrelease");
				A_FireProjectile("GWM_ImpalerArrow");
				A_Recoil(1);
			}
			CBOW C 1 {
				A_StartSound("weapons/xbowdraw", CHAN_WEAPON);
			}
			CBOW C 7;
			CBOW D 5;
			CBOW E 8 A_CheckReload;
			CBOW F 8;
			CBOW A 12;
			Goto Ready;
	}
}
class GWM_ImpalerCrossbowPowered : GWM_ImpalerCrossbow
{
	Default
	{
		Weapon.SisterWeapon "GWM_ImpalerCrossbow";
		+Weapon.Powered_Up
	}
	States
	{
		Select:
			CBOW K 1 A_Raise(18);
			Loop;
		Deselect:
			CBOW K 1 A_Lower(18);
			Loop;
		Ready:
			CBOW K 0 A_JumpIfNoAmmo("FakeReady");
			CBOW G 1 A_WeaponReady;
			Loop;
		FakeReady:
			CBOW K 1 A_WeaponReady;
			Loop;
		Fire:
			CBOW H 6 
			{
				A_StartSound("weapons/xbowrelease");
				A_FireProjectile("GWM_ImpalerArrow", -5, false);
				A_FireProjectile("GWM_ImpalerArrowPowered");
				A_FireProjectile("GWM_ImpalerArrow", 5, false);
				A_Recoil(3);
			}
			CBOW I 8 A_StartSound("weapons/xbowdraw", CHAN_WEAPON);
			CBOW J 5;
			CBOW K 8 A_CheckReload;
			CBOW L 8;
			CBOW G 12;
			Goto Ready;
	}
}
class GWM_ImpalerArrow : FastProjectile
{
	Default
	{
		Decal "BulletChip";
		PROJECTILE;
		+THRUGHOST
		+BLOODSPLATTER
		+PIERCEARMOR
		+RIPPER
		Radius 4;
		Height 8;
		DamageFunction (27);
		Speed 80;
		SeeSound "weapons/xbowfire";
		Obituary "$KILLIMPALER";
	}
	States
	{
		Spawn:
			ARRW A 1;
			Loop;
		Death:
		Crash:
			TNT1 A 0 {
				A_StartSound("weapons/xbowhit");
				A_SpawnItemEx("ZBulletPuff");
				A_SpawnItemEx("ZBulletChip");
			}
			ARRW A 70;
		Fade:
			ARRW A 1 A_FadeOut(0.05);
			Loop;
		XDeath:
			TNT1 A 10 A_StartSound("weapons/csawhit");
			Stop;
	}
}
class GWM_ImpalerArrowPowered : GWM_ImpalerArrow
{
	Default
	{
		-THRUGHOST
		DamageFunction (36);
	}
	States
	{
		Spawn:
			ARRW B 1;
			Loop;
		Death:
		Crash:
			TNT1 A 0 {
				A_StartSound("weapons/xbowhit");
				A_SpawnItemEx("ZBulletPuff");
				A_SpawnItemEx("ZBulletChip");
			}
			ARRW B 70;
		Fade:
			ARRW B 1 A_FadeOut(0.05);
			Loop;
	}
}