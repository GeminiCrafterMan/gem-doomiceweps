class GWM_SawedOffShotgun : GWM_WeaponBase
{	
	int SSGShellstoEject;
	
	double xoffset;
	double yoffset;
	
	action void A_EjectSSGCasings()
	{
		ZWL_EjectCasing("GWM_ShotgunCasing", offset: (24, 4, -12));
		ZWL_EjectCasing("GWM_ShotgunCasing", offset: (24, 8, -12));
	}

	Default
	{
		//$Category Weapons
		//$Title "Sawed Off Shotgun
		Weapon.SelectionOrder 1300;
		Inventory.PickupSound "weapons/sogpickup";
		Weapon.SlotNumber 3;
		Weapon.UpSound "weapons/draw_primary";
		Weapon.AmmoType1 "GWM_Shell";
		Weapon.AmmoUse1 2;
		Weapon.AmmoGive1 8;
		Weapon.AmmoType2 "GWM_Shell";
		Weapon.AmmoUse2 2;

		Inventory.PickupMessage "$GOTSAWEDOFFSHOTGUN";
		Obituary "$KILLSAWEDOFFSHOTGUN";
		Tag "$TAG_SAWEDOFFSHOTGUN";
	}
	States
	{
		Ready:
			SOSG A 1 A_WeaponReady;
			Loop;
		Select:
			TNT1 A 0 {
				ZWL_QuickRaise();
				
			}
			SOSG V 1 Offset(-4,40);
			SOSG V 1;
			SOSG W 3 {
				A_StartSound("weapons/sogc");
				A_WeaponReady();
			}
			SOSG XYZZ 2 A_WeaponReady;
			SOS2 ABCDE 2 A_WeaponReady;
			Goto Ready;
		Deselect:
			SOSG AA 1 {
				A_WeaponOffset(8, 16, WOF_ADD);
			}
			SOSG AAAAAA 1 {
				A_WeaponOffset(8, 16, WOF_ADD);
			}
			TNT1 A 0 A_WeaponOffset(0, flags: WOF_KEEPY);
			TNT1 A 0 ZWL_QuickLower;
			Stop;
		Recoil:
			TNT1 AA 1 A_SetPitch(pitch+0.80);
			TNT1 AAAAAA 1 A_SetPitch(pitch-0.40);
			Stop;
		
		Fire:
			TNT1 A 0 A_StopSound(CHAN_BODY);
				TNT1 A 0 A_Overlay(3,"SOGFlash");
				SOSG A 2 {
					ZWL_FireHitscan(0, 0, pellets: 24, spread: 13, pufftype: "ZBulletPuff_SSG", tracertype: "GWM_SawedShot");
					invoker.chargelevel = 0;
					A_StartSound("weapons/SOGshot", CHAN_WEAPON);
					A_SpawnItemEx("GWM_MuzzleFlashLight3",0,0,5);
					A_SpawnItemEx("GWM_Flash");
					A_Overlay(55,"Recoil");
					A_ChangeVelocity(-Cos(pitch)*4, 0, sin(pitch)*4,CVF_RELATIVE);
					A_QuakeEx(3, 3, 3, 4, 0, 1, "", QF_SHAKEONLY);
					A_OverlayScale(OverlayID(), 1, 1, WOF_INTERPOLATE);
				//invoker.SSGShellstoEject = invoker.SSGShellstoEject+1;
				}
				SOSG BCDEEE 1;
				//TNT1 A 0 A_JumpifInventory("PowerWeaponLevel2",1,"Ready");

		Reload:
			TNT1 A 0 A_CheckReload;
			SOSG FGHIJK 3;
			SOSG L 3 A_StartSound("weapons/SOGo", CHAN_BODY);
			SOSG M 2 A_EjectSSGCasings;
			SOSG N 6;
			SOSG OPQRS 2;
			SOSG S 3 A_StartSound("weapons/SOGl", CHAN_BODY);
			SOSG TUVV 2;
			SOSG W 3 A_StartSound("weapons/SOGc", CHAN_BODY);
			SOSG XYZZ 2;
			SOS2 ABCDE 2;
			Goto Ready;
			
		SOGFlash:
			SOSF AABC 1 BRIGHT;
			stop;
		Spawn:
			SOSG [ -1;
			Stop;
	}
}

class GWM_SawedShot : FastProjectile
{
	Default
	{
		RenderStyle "Add";
		Radius 2;
		Height 2;
		Speed 110;
		+BLOODSPLATTER
		+HITTRACER
		+NOINTERACTION
		Scale .5;
		DamageFunction (13);
		//MissileType "GWM_TracerTrail";
		MissileHeight 8;
		+EXTREMEDEATH;
	}
	States
	{
		Dummy:
			TRAC A 1 Bright;
			Loop;
		Spawn:
			TRAC A 1 BRIGHT;
			TNT1 A 0 {bEXTREMEDEATH = false;}
			Loop;
		Crash:
			TNT1 A 0 A_Jump(256,"Death");
		Death:
			PUFF A 2 BRIGHT A_SpawnDebris("GWM_TracerSpark");
			PUFF B 1 BRIGHT A_SetTranslucent(0.9,0);
			PUFF B 1 BRIGHT A_PlaySound("bullet/ricochet",CHAN_BODY,.5);
			PUFF CD 2 BRIGHT;
			Stop;
		XDeath:
			TNT1 A 3 A_PlaySound("bullet/hitFlesh",CHAN_BODY,.5);
			Stop;
	}
}
