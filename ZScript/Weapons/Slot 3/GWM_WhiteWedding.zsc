class GWM_WhiteWedding : GWM_WeaponBase
{
	Default
	{
		//$Category Weapons
		//$Title "\"White Wedding\" Shotgun
		Weapon.SelectionOrder 1300;
		Inventory.PickupSound "weapons/skpickup";
		Weapon.UpSound "weapons/draw_secondary";
		Weapon.AmmoType "GWM_Shell";
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 13;
		Weapon.Slotnumber 3;

		ZWeapon.MagazineSize 13;
		ZWeapon.ReloadSound "weapons/shell_load";
		ZWeapon.ClickSound "weapons/skempty";

		GWM_WeaponBase.BaseVersion "GWM_SlayersKahuna";
		GWM_WeaponBase.BaseVersion2 "GWM_ZombieShotgun";
		Tag "$TAG_WHITEWEDDING";
		Inventory.PickupMessage "$GOTWHITEWEDDING";
		Obituary "$KILLWHITEWEDDING";
	}
	States
	{
		Ready:
			WEDG A 1 ZWL_WeaponReady();
			Loop;
		Select:
			TNT1 A 0 {
				ZWL_QuickRaise();
				A_WeaponOffset(64, 160);
			}
			WEDG UUUUUU 1 {
				A_WeaponOffset(-8, -16, WOF_ADD);
			}
			WEDG AA 1 {
				A_WeaponOffset(-8, -16, WOF_ADD);
			}
			WEDG H 4 {A_StartSound("weapons/skpump1"); ZWL_WeaponReady();}
			WEDG GGFF 1 ZWL_WeaponReady();
			WEDG A 1 {A_StartSound("weapons/skpump2"); ZWL_WeaponReady();}
			Goto Ready;
		Deselect:
			WEDG AA 1 {
				A_WeaponOffset(8, 16, WOF_ADD);
			}
			WEDG UUUUUU 1 {
				A_WeaponOffset(8, 16, WOF_ADD);
			}
			TNT1 A 0 A_WeaponOffset(0, flags: WOF_KEEPY);
			TNT1 A 0 ZWL_QuickLower();
			Stop;
		Recoil:
			TNT1 AA 1 A_SetPitch(pitch+0.80);
			TNT1 AAAAAA 1 A_SetPitch(pitch-0.40);
			Stop;
		Fire:
			WEDG A 1 Bright {
				A_StartSound("weapons/wwshot1");
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayPivot(OverlayID(), 0.2, flags: WOF_KEEPY);
				ZWL_FireHitscan(0, 11, pellets: 15, spread: 6+(Countinv("GWM_KahunaSpread")*0.4), pufftype: "GWM_WeddingBulletPuff" , tracerType: "GWM_TracerWhite");
				
				A_ChangeVelocity(-Cos(pitch)*3, 0, sin(pitch)*3,CVF_RELATIVE);
				A_SpawnItemEx("GWM_Flash");
				A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5);
				A_GunFlash();
				A_WeaponOffset(0, 42, WOF_INTERPOLATE);
				A_OverlayScale(OverlayID(), 1.15, 0, WOF_INTERPOLATE);
				A_QuakeEx(1.5, 1.5, 1.5, 4, 0, 1, "", QF_SHAKEONLY);
				
				if(pitch > 70)
				{
				A_ChangeVelocity(0, 0, sin(pitch)*2,CVF_RELATIVE);
				}
			}
			WEDG A 2 Bright;
			WEDG A 1 Bright {
				A_WeaponOffset(0, -5, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			WEDG AA 1 Bright;
			WEDG A 1 {
				A_WeaponOffset(0, -5, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			TNT1 A 0 ZWL_CheckReload();
			WEDG B 5;
			WEDG CCCDD 1;
			WEDG E 4 {
				A_StartSound("weapons/skpump1");
				ZWL_EjectCasing("GWM_ShotgunCasing", offset: (24, -6, -14));
			}
			WEDG DDCCC 1;
			WEDG B 5 A_StartSound("weapons/skpump2");
			WEDG A 3;
			WEDG A 0 ZWL_ReFire();
			Goto Ready;
		AltFire:
			TNT1 A 0 A_Jumpif(GWM_Player(player.mo).WeddingCharges > 0,"Altfire2");
			
			
			TNT1 A 0 {
				if (!invoker.DepleteAmmo(false, true))
				{
					A_PlaySound(invoker.clickSound, CHAN_Auto);
					player.SetPSPrite(OverlayID(), ResolveState("Ready")); // G: Weird, fucked-up way of making it click instead.
				}
			}
			WEDG A 1 Bright {
				A_StartSound("weapons/wwshot1");
				A_Overlay(55, "Recoil");
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayPivot(OverlayID(), 0.2, flags: WOF_KEEPY);
				ZWL_FireHitscan(0, 12, pellets: 15, spread: 5+(Countinv("GWM_KahunaSpread")*0.8), pufftype: "GWM_WeddingBulletPuff" ,tracerType: "GWM_TracerWhite");
				A_ChangeVelocity(-Cos(pitch)*3.1, 0, sin(pitch)*3.1,CVF_RELATIVE);
				A_SpawnItemEx("GWM_Flash");
				A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5);
				A_GunFlash("Flash2");
				A_WeaponOffset(0, 52, WOF_INTERPOLATE);
				A_OverlayScale(OverlayID(), 1.30, 0, WOF_INTERPOLATE);
				A_QuakeEx(1.5, 1.5, 1.5, 4, 0, 1, "", QF_SHAKEONLY);
				A_GiveInventory("GWM_KahunaSpread",2);
				A_GiveInventory("GWM_WeddingDelay",1);
				
				if(pitch > 70)
				{
				A_ChangeVelocity(0, 0, sin(pitch)*2,CVF_RELATIVE);
				}
			}
			WEDG A 1 Bright;
			WEDG A 1 Bright {
				A_WeaponOffset(0, -10, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.15, 0, WOF_ADD);
			}
			WEDG A 1 Bright;
			WEDG A 1 {
				A_WeaponOffset(0, -10, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.15, 0, WOF_ADD);
			}
			WEDG AAAA 1;
			TNT1 A 0 {
				ZWL_CheckReload();
				ZWL_EjectCasing("GWM_ShotgunCasing", offset: (20, 4, -14));
			}
			TNT1 A 0 ZWL_ReFire();
			WEDG FG 1;
			WEDG H 3 A_StartSound("weapons/skpump1");
			WEDG GGFF 1;
			WEDG A 3 A_StartSound("weapons/skpump2");
			WEDG A 0 ZWL_ReFire();
			Goto Ready;
			
		Altfire2:
			TNT1 A 0 {
				if (!invoker.DepleteAmmo(false, true))
				{
					A_PlaySound(invoker.clickSound, CHAN_Auto);
					player.SetPSPrite(OverlayID(), ResolveState("Ready")); // G: Weird, fucked-up way of making it click instead.
				}
			}
			WEDG A 1 Bright {
				A_StartSound("weapons/wwshot2");
				A_Overlay(55, "Recoil");
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayPivot(OverlayID(), 0.2, flags: WOF_KEEPY);
				ZWL_FireHitscan(0, 12, pellets: 18, spread: 7, pufftype: "GWM_WeddingBulletPuff2" , tracerType: "GWM_TracerWhite2");
				A_ChangeVelocity(-Cos(pitch)*3.5, 0, sin(pitch)*3.5,CVF_RELATIVE);
				A_SpawnItemEx("GWM_Flash");
				A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5);
				A_GunFlash("Flash3");
				A_WeaponOffset(0, 52, WOF_INTERPOLATE);
				A_OverlayScale(OverlayID(), 1.30, 0, WOF_INTERPOLATE);
				A_QuakeEx(1.5, 1.5, 1.5, 4, 0, 1, "", QF_SHAKEONLY);
				A_GiveInventory("GWM_KahunaSpread",2);
				GWM_Player(player.mo).WeddingCharges = GWM_Player(player.mo).WeddingCharges-1;
			
				if(pitch > 70)
				{
					if(vel.x < 0)
					{A_ChangeVelocity(vel.x, vel.y, 0,CVF_REPLACE);}
				A_ChangeVelocity(0, 0, sin(pitch)*3,CVF_RELATIVE);
				}
			}
			WEDG A 1 Bright {
				A_WeaponOffset(0, -10, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.15, 0, WOF_ADD);
			}
			WEDG A 1 {
				A_WeaponOffset(0, -10, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.15, 0, WOF_ADD);
			}
			WEDG AAA 1;
			TNT1 A 0 {
				ZWL_CheckReload();
				ZWL_EjectCasing("GWM_ShotgunCasing", offset: (20, 4, -14));
			}
			TNT1 A 0 ZWL_ReFire();
			WEDG FG 1 A_GiveInventory("GWM_WeddingDelay",1); 
			WEDG H 3 A_StartSound("weapons/skpump1");
			WEDG GGFF 1;
			WEDG A 3 A_StartSound("weapons/skpump2");
			WEDG A 0 ZWL_ReFire();
			Goto Ready;
			
		Spawn:
			WEDG Z -1;
			Stop;
		Flash:
			WEDF A 1 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayPivot(OverlayID(), 0.2, flags: WOF_KEEPY);
				A_OverlayScale(OverlayID(), 1.15, 0, WOF_INTERPOLATE);
				A_Light1();
			}
			WEDF A 2 Bright A_Light1;
			WEDF B 3 Bright {
				A_Light2();
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			Goto LightDone;
		Flash2:
			WEDF A 1 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayPivot(OverlayID(), 0.2, flags: WOF_KEEPY);
				A_Light1();
				A_OverlayScale(OverlayID(), 1.30, 0, WOF_INTERPOLATE);
			}
			WEDF A 1 Bright A_Light1;
			WEDF A 1 Bright {
				A_Light1();
				A_OverlayScale(OverlayID(), -0.15, 0, WOF_ADD);
			}
			WEDF A 1 Bright A_Light1;
			WEDF B 1 Bright {
				A_Light2();
				A_OverlayScale(OverlayID(), -0.15, 0, WOF_ADD);
			}
			WEDF B 2 Bright A_Light2;
			Goto LightDone;
		Flash3:
			WEDF A 1 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayPivot(OverlayID(), 0.2, flags: WOF_KEEPY);
				A_Light1();
				A_OverlayScale(OverlayID(), 1.30, 0, WOF_INTERPOLATE);
			}
			WEDF AA 1 Bright {
				A_Light1();
				A_OverlayScale(OverlayID(), -0.15, 0, WOF_ADD);
			}
			WEDF A 2 Bright A_Light1;
			WEDF B 3 Bright A_Light2;
			Goto LightDone;
		Reload:
			TNT1 A 0 {
				if (!invoker.CheckAmmo(PrimaryFire, false, true))
					player.SetPSPrite(OverlayID(), ResolveState("Ready")); // G: Disallow reloading if there's no more ammo
			}
			TNT1 A 0 ZWL_JumpIfEmpty("Reload.Empty");
			WEDG B 5;
			WEDG CCCNN 1;
			WEDG O 2 A_StartSound("weapons/skpump1");
			// Fallthrough
		Reload.Loop:
			WEDG PQR 2;
			TNT1 A 0 ZWL_Reload(1);
			WEDG ST 2;
			WEDG E 0 ZWL_JumpIfReloaded("Reload.End", true);
			Loop;
		Reload.End:
			WEDG NNCCC 1;
			WEDG B 5 A_StartSound("weapons/skpump2");
			Goto Ready;

		Reload.Empty:
			WEDG B 5;
			WEDG CCCDD 1;
			WEDG E 2 A_StartSound("weapons/skpump1");
			WEDG IJK 2;
			TNT1 A 0 ZWL_Reload(1);
			WEDG LM 2;
			WEDG E 4;
			WEDG O 5 A_StartSound("weapons/skpump2");
			ZSHG E 0 ZWL_JumpIfReloaded("Reload.End", true);
			Goto Reload.Loop;
	}
}

class GWM_WeddingBulletPuff : ZBulletPuff
{
	Default
	{
		Decal "BulletChip";
		Height 8;
		Renderstyle "Add";
		Alpha 1.0;
		Scale 0.15;
	}
	States
	{
		Spawn:
			TNT1 A 2 A_SpawnItemEx("ZBulletChip");
			WEFX AAAAAAA 1 Bright 
			{
				A_Stop();
				A_FadeOut(0.1);
				A_Setscale(Scale.X*0.8,Scale.X*0.8);
			}
			stop;
	}
}

class GWM_WeddingBulletPuff2 : ZBulletPuff
{
	Default
	{
		Decal "BulletChip";
		Height 8;
		Renderstyle "Add";
		Alpha 1.0;
		Scale 0.25;
	}
	States
	{
		Spawn:
			TNT1 A 2 A_SpawnItemEx("ZBulletChip");
			WEFX AAAAAAAAA 1 Bright 
			{
				A_Stop();
				A_FadeOut(0.1);
				A_Setscale(Scale.X*0.8,Scale.X*0.8);
			}
			stop;
	}
}