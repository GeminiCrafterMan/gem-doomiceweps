class GWM_SuperShotgun : GWM_WeaponBase
{
	bool OneBarrel;
	bool Fired;
	Default
	{
		Weapon.SelectionOrder 1300;
		Inventory.PickupSound "weapons/ssgpickup";
		Weapon.UpSound "weapons/draw_primary";
		Inventory.PickupMessage "$GOTSUPERSHOTGUN";
		Obituary "$KILLSUPERSHOTGUN";
		Tag "Super Shotgun";
		Weapon.AmmoUse 2;
		Weapon.AmmoGive 8;
		Weapon.AmmoType "GWM_Shell";
		Weapon.AmmoUse2 1;
		Weapon.AmmoType2 "GWM_Shell";
	}
	States
	{
		Ready:
			SSGG A 1 A_WeaponReady(WRF_ALLOWRELOAD);
			Loop;
		Deselect:
			SSGG A 1 A_Lower(12);
			Loop;
		Select:
			SSGG A 1 A_Raise(12);
			Loop;
		Fire:
			TNT1 A 0 {if (invoker.OneBarrel) {player.SetPSprite(OverlayID(), ResolveState("AltFire"));}}
			SSGG A 1 {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				ZWL_FireHitscan(0, 6*Random(2,3), pellets: 20, spread: 8, pufftype: "ZBulletPuff_SSG", tracertype: "GWM_Tracer");
				A_StartSound("weapons/ssgshot1", CHAN_WEAPON);
				A_Overlay(PSP_FLASH, "FlashBoth");
				A_SpawnItemEx("GWM_Flash");
				A_WeaponOffset(0, 24, WOF_ADD);
				A_OverlayScale(OverlayID(), 1.30, 0, WOF_INTERPOLATE);
				invoker.Fired = true;
			}
			SSGG A 2;
			SSGG AA 2 {
				A_WeaponOffset(0, -12, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.15, 0, WOF_ADD);
			}
			TNT1 A 0 A_CheckReload;
			SSGG A 4 {A_WeaponOffset(0, 32, WOF_INTERPOLATE); A_OverlayScale(OverlayID(), 1, 0, WOF_INTERPOLATE);}
			Goto Reload.Empty;
		AltFire:
			SSGG A 1 {
//				invoker.DepleteAmmo(true); // depletes one shell
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				invoker.OneBarrel = !invoker.OneBarrel;
				if (invoker.OneBarrel) {
					A_Overlay(PSP_FLASH, "FlashLeft");
				}
				else {
					A_Overlay(PSP_FLASH, "FlashRight");
				}
				ZWL_FireHitscan(0, 6*Random(2,3), pellets: 10, spread: 4, tracertype: "GWM_Tracer");
				A_StartSound("weapons/ssgshot2", CHAN_WEAPON);
				A_SpawnItemEx("GWM_Flash");
				A_WeaponOffset(0, 12, WOF_ADD);
				A_OverlayScale(OverlayID(), 1.15, 0, WOF_INTERPOLATE);
				invoker.Fired = true;
			}
			SSGG A 2;
			SSGG AA 2 {
				A_WeaponOffset(0, -6, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			TNT1 A 0 A_CheckReload;
			SSGG A 4 {
				A_WeaponOffset(0, 32, WOF_INTERPOLATE);
				A_OverlayScale(OverlayID(), 1, 0, WOF_INTERPOLATE);
				if (!invoker.OneBarrel)
				{
					player.SetPSprite(OverlayID(), ResolveState("Reload"));
				}
			}
			Goto Ready;
		Reload:
			TNT1 A 0 {if (!invoker.Fired) {player.SetPSPrite(OverlayID(), ResolveState("Ready"));}}
			TNT1 A 0 {if (!invoker.OneBarrel) {player.SetPSprite(OverlayID(), ResolveState("Reload.Empty"));}}
			SSGG DC 2;
			SSGG B 3;
			SSGG C 4;
			SSGG D 3;
			SSGG P 3 {
				A_StartSound("weapons/ssgo", CHAN_BODY);
				ZWL_EjectCasing("GWM_ShotgunCasing", true);
//				ZWL_EjectCasing("GWM_ShotgunCasing", true, offset: (24, 4, -10));
			}
			SSGG Q 2;
			SSGG QRST 2;
			SSGG J 3 A_StartSound("weapons/ssgl", CHAN_BODY);
			SSGG KLL 2 A_OverlayPivotAlign(OverlayID(), PSPA_LEFT, PSPA_BOTTOM);
			SSGG MMM 1 {
				A_OverlayOffset(OverlayID(), 5, 10, WOF_ADD);
				A_OverlayRotate(OverlayID(), 5, WOF_ADD);
			}
			SSGG N 3 {
				A_StartSound("weapons/ssgc", CHAN_BODY);
				A_OverlayRotate(OverlayID(), 0);
				A_OverlayOffset(OverlayID(), 0);
				invoker.OneBarrel = false;
				invoker.Fired = false;
			}
			SSGG O 5;
			Goto Ready;
		Reload.Empty:
			SSGG DC 2;
			SSGG B 3;
			SSGG C 4;
			SSGG D 3;
			SSGG E 3 {
				A_StartSound("weapons/ssgo", CHAN_BODY);
				ZWL_EjectCasing("GWM_ShotgunCasing", true);
				ZWL_EjectCasing("GWM_ShotgunCasing", true, offset: (24, 4, -10));
			}
			SSGG F 2;
			SSGG FGHI 2;
			SSGG J 3 A_StartSound("weapons/ssgl", CHAN_BODY);
			SSGG KLL 2 A_OverlayPivotAlign(OverlayID(), PSPA_LEFT, PSPA_BOTTOM);
			SSGG MMM 1 {
				A_OverlayOffset(OverlayID(), 5, 10, WOF_ADD);
				A_OverlayRotate(OverlayID(), 5, WOF_ADD);
			}
			SSGG N 3 {
				A_StartSound("weapons/ssgc", CHAN_BODY);
				A_OverlayRotate(OverlayID(), 0);
				A_OverlayOffset(OverlayID(), 0);
				invoker.OneBarrel = false;
				invoker.Fired = false;
			}
			SSGG O 5;
			Goto Ready;
		FlashBoth:
			TNT1 A 0 A_Overlay(-2,"BothBarrelsFlash");
			SSGF A 1 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayScale(OverlayID(), 1.30, 0, WOF_INTERPOLATE);
			}
			SSGF A 2 Bright;
			SSGF BB 2 Bright {
				A_OverlayScale(OverlayID(), -0.15, 0, WOF_ADD);
			}
			Stop;
		FlashLeft:
			TNT1 A 0 A_Overlay(-2,"LeftBarrelFlash");
			Goto Flash;
		FlashRight:
			TNT1 A 0 A_Overlay(-2,"RightBarrelFlash");
		Flash:
			SSGF A 1 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayScale(OverlayID(), 1.15, 0, WOF_INTERPOLATE);
			}
			SSGF A 2 Bright;
			SSGF BB 2 Bright {
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			Stop;
		BothBarrelsFlash:
			SSGF C 1 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayScale(OverlayID(), 1.30, 0, WOF_INTERPOLATE);
			}
			SSGF C 2 Bright;
			SSGF DD 2 Bright {
				A_OverlayScale(OverlayID(), -0.15, 0, WOF_ADD);
			}
			Stop;
		LeftBarrelFlash:
			SSGF E 1 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayScale(OverlayID(), 1.15, 0, WOF_INTERPOLATE);
			}
			SSGF E 2 Bright;
			SSGF FF 2 Bright {
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			Stop;
		RightBarrelFlash:
			SSGF G 1 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayScale(OverlayID(), 1.15, 0, WOF_INTERPOLATE);
			}
			SSGF G 2 Bright;
			SSGF HH 2 Bright {
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			Stop;
		Spawn:
			SSGG Z -1;
			Stop;
	}
}
class ZBulletPuff_SSG : ZBulletPuff
{
    Default
    {
        +PUFFGETSOWNER
        +PUFFONACTORS
        +HITTRACER
    }
    States
    {
    XDeath:
        TNT1 A 1
        {
            if (tracer && target && target.Distance3D(tracer) < 80)
            {
                bExtremeDeath = true;
            }
        }
        Stop;
    }
}