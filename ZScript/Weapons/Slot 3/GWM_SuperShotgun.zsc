class GWM_SuperShotgun : GWM_WeaponBase
{
	bool OneBarrel;
	bool Fired;
	Default
	{
		Weapon.SelectionOrder 1300;
		Inventory.PickupSound "weapons/ssgpickup";
		Weapon.UpSound "weapons/draw_primary";
		Inventory.PickupMessage "$GOTSUPERSHOTGUN";
		Obituary "$KILLSUPERSHOTGUN";
		Tag "Super Shotgun";
		Weapon.AmmoUse 2;
		Weapon.AmmoGive 8;
		Weapon.AmmoType "GWM_Shell";
		Weapon.AmmoUse2 1;
		Weapon.AmmoType2 "GWM_Shell";
	}
	States
	{
		Ready:
			SSGG A 1 A_WeaponReady(WRF_ALLOWRELOAD);
			Loop;
		Deselect:
			SSGG A 1 A_Lower(18);
			Loop;
		Select:
			SSGG A 1 A_Raise(18);
			Loop;
		Fire:
			TNT1 A 0 {if (invoker.OneBarrel) {player.SetPSprite(OverlayID(), ResolveState("AltFire"));}}
			SSGG A 2 {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				ZWL_FireHitscan(0, 6*Random(2,3), pellets: 20, spread: 7, pufftype: "ZBulletPuff_SSG", tracertype: "GWM_Tracer");
				A_StartSound("weapons/ssgshot1", CHAN_WEAPON);
				A_GunFlash();
				A_SpawnItemEx("GWM_Flash");
				A_QuakeEx(3, 3, 3, 4, 0, 1, "", QF_SHAKEONLY);
				invoker.Fired = true;
			}
			SSGG AA 2;
			TNT1 A 0 A_CheckReload;
			Goto Reload.Empty;
		AltFire:
			SSGG A 2 {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				invoker.OneBarrel = !invoker.OneBarrel;
				if (invoker.OneBarrel) {
					A_Overlay(PSP_FLASH, "FlashLeft");
				}
				else {
					A_Overlay(PSP_FLASH, "FlashRight");
				}
				ZWL_FireHitscan(0, 6*Random(2,3), pellets: 10, spread: 3.5, tracertype: "GWM_Tracer");
				A_StartSound("weapons/ssgshot2", CHAN_WEAPON);
				A_SpawnItemEx("GWM_Flash");
				A_WeaponOffset(0, 12, WOF_ADD);
				A_OverlayScale(OverlayID(), 1.15, 0, WOF_INTERPOLATE);
				A_QuakeEx(1.5, 1.5, 1.5, 4, 0, 1, "", QF_SHAKEONLY);
				invoker.Fired = true;
			}
			SSGG AA 2 {
				A_WeaponOffset(0, -6, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			TNT1 A 0 A_CheckReload;
			SSGG A 4 {
				A_WeaponOffset(0, 32, WOF_INTERPOLATE);
				A_OverlayScale(OverlayID(), 1, 0, WOF_INTERPOLATE);
				if (!invoker.OneBarrel)
				{
					player.SetPSprite(OverlayID(), ResolveState("Reload"));
				}
			}
			Goto Ready;
		Reload:
			TNT1 A 0 {if (!invoker.Fired) {player.SetPSPrite(OverlayID(), ResolveState("Ready"));}}
			TNT1 A 0 {if (!invoker.OneBarrel) {player.SetPSprite(OverlayID(), ResolveState("Reload.Empty"));}}
			SSGG D 2;
			SSGG C 2;
			SSGG B 3;
			SSGG C 4;
			SSGG D 3;
			SSGG E 3 A_StartSound("weapons/ssgo", CHAN_BODY);
			SSGG P 2 ZWL_EjectCasing("GWM_ShotgunCasing", true);
			SSGG PQRS 2;
			SSGG J 3 A_StartSound("weapons/ssgl", CHAN_BODY);
			SSGG KLL 2;
			SSGG MMM 1;
			SSGG N 3 {
				A_StartSound("weapons/ssgc", CHAN_BODY);
				invoker.OneBarrel = false;
				invoker.Fired = false;
			}
			SSGG O 5;
			SSGG A 1;
			Goto Ready;
		Reload.Empty:
			SSGG D 2;
			SSGG C 2;
			SSGG B 3;
			SSGG C 4;
			SSGG D 3;
			SSGG E 3 A_StartSound("weapons/ssgo", CHAN_BODY);
			SSGG F 2 {
				ZWL_EjectCasing("GWM_ShotgunCasing", true);
				ZWL_EjectCasing("GWM_ShotgunCasing", true, offset: (24, 4, -10));
			}
			SSGG FGHI 2;
			SSGG J 3 A_StartSound("weapons/ssgl", CHAN_BODY);
			SSGG KLL 2;
			SSGG MMM 1;
			SSGG N 3 {
				A_StartSound("weapons/ssgc", CHAN_BODY);
				invoker.OneBarrel = false;
				invoker.Fired = false;
			}
			SSGG O 5;
			SSGG A 1;
			Goto Ready;
		Flash:
			SSGF A 2 Bright A_Light1;
			SSGF B 1 Bright A_Light2;
			Goto LightDone;
		FlashLeft:
			SSGF C 2 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayScale(OverlayID(), 1.15, 0, WOF_INTERPOLATE);
				A_Light2();
			}
			SSGF DD 2 Bright {
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
				A_Light2();
			}
			Goto LightDone;
		FlashRight:
			SSGF E 2 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayScale(OverlayID(), 1.15, 0, WOF_INTERPOLATE);
				A_Light2();
			}
			SSGF FF 2 Bright {
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
				A_Light2();
			}
			Goto LightDone;
		Spawn:
			SSGG Z -1;
			Stop;
	}
}
class ZBulletPuff_SSG : ZBulletPuff
{
    Default
    {
        +PUFFGETSOWNER
        +PUFFONACTORS
        +HITTRACER
    }
    States
    {
    XDeath:
        TNT1 A 1
        {
            if (tracer && target && target.Distance3D(tracer) < 80)
            {
                bExtremeDeath = true;
            }
        }
        Stop;
    }
}