class GWM_SuperShotgun : SuperShotgun
{
	/*
	Ejects a bullet casing to the side.
	Params:
	 - casingType: Type of actor to eject.
	 - left: If true, casing ejects to the left. Otherwise it ejects to the right.
	 - ejectPitch: Pitch at which casing ejects, relative to view direction.
	 - speed: Speed at which casing ejects.
	 - accuracy: Random spread, in degrees.
	 - offset: Offset from which casing is ejected, relative to center of view.
	*/
	action void ZWL_EjectCasing(class<Actor> casingType, bool left = false, double ejectPitch = -45, double speed = 4,
								double accuracy = 8, Vector3 offset = (24, 0, -10))
	{
		// Find offset vector
		// +y axis is to the right for offsets
		offset.y *= -1;

		// Rotate offset by pitch
		Vector2 xz = RotateVector((offset.x, offset.z), -pitch);
		offset.x = xz.x;
		offset.z = xz.y;

		// Rotate vector by angle
		offset.xy = RotateVector(offset.xy, angle);

		// Move to player camera
		offset.xy += pos.xy;
		offset.z += player.viewZ;

		// Find velocity vector
		Vector3 side = (Cos(angle + (left ? 90 : -90)), Sin(angle + (left ? 90 : -90)), 0);
		Vector3 up = (Cos(pitch-90) * Cos(angle), Cos(pitch-90) * Sin(angle), -Sin(pitch-90));
		Vector3 baseDirection = Cos(-ejectPitch) * side + Sin(-ejectPitch) * up;

		double baseAngle = VectorAngle(baseDirection.x, baseDirection.y);
		baseDirection.xy = RotateVector(baseDirection.xy, -baseAngle);
		double basePitch = -VectorAngle(baseDirection.x, baseDirection.z);

		double casAngle, casPitch;
		[casAngle, casPitch] = invoker.BulletAngle(accuracy, baseAngle, basePitch);

		let casing = Spawn(casingType, offset);
		casing.Vel3dFromAngle(speed, casAngle, casPitch);
		casing.vel += vel;
	}
	// Returns random angle and pitch within cone
	// I have no idea if there's a better way of doing this ¯\_(ツ)_/¯
	// Params:
	//  - accuracy: maximum angle b/w cone's axis, and bullet trajectory
	//  - angle: angle of axis
	//  - pitch: pitch of axis
	double, double BulletAngle(double accuracy, double angle, double pitch)
	{
		Vector3 v = (0, 0, 0);

		if (accuracy > 10)
		{
			// Generate random vector in sphere section
			Vector3 axis = (Cos(pitch) * Cos(angle), Cos(pitch) * Sin(angle), -Sin(pitch));
			while (v == (0, 0, 0) || v.Length() > 1 || ACos(axis dot v.Unit()) > accuracy)
			{
				v = (FRandom(-1, 1), FRandom(-1, 1), FRandom(-1, 1));
			}

			// Extract angle and pitch from trajectory
			angle = VectorAngle(v.x, v.y);
			pitch = -ASin(v.z / v.Length());
		}
		else if (accuracy > 0)
		{
			// Generate random vector in sphere around end of axis
			double r = Sin(accuracy);
			while (v == (0, 0, 0) || v.Length() > r)
			{
				v = (FRandom(-r, r), FRandom(-r, r), FRandom(-r, r));
			}

			Vector3 axis = (Cos(pitch) * Cos(angle), Cos(pitch) * Sin(angle), -Sin(pitch));
			v += axis;

			// Extract angle and pitch from trajectory
			angle = VectorAngle(v.x, v.y);
			pitch = -ASin(v.z / v.Length());
		}

		return angle, pitch;
	}
	bool OneBarrel;
	Default
	{
		Inventory.PickupMessage "Picked up the Super Shotgun! Oh, yes.";
		Obituary "%o was turned into Swiss cheese by %k's Super Shotgun.";
		Tag "Super Shotgun";
		Weapon.AmmoUse 2;
		Weapon.AmmoType "GWM_Shell";
		Weapon.AmmoUse2 1;
		Weapon.AmmoType2 "GWM_Shell";
	}
	States
	{
		Ready:
			SSGG A 1 A_WeaponReady;
			Loop;
		Deselect:
			SSGG A 1 A_Lower;
			SSGG A 0 A_Lower;
			Loop;
		Select:
			SSGG A 1 A_Raise;
			SSGG A 0 A_Raise;
			Loop;
		Fire:
			TNT1 A 0 {if (invoker.OneBarrel) {player.SetPSprite(OverlayID(), ResolveState("AltFire"));}}
			SSGG A 1 {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_FireBullets(20, 6.64, 20, 6, "ZBulletPuff");
				A_StartSound("weapons/ssgshot1", CHAN_WEAPON);
				A_Overlay(PSP_FLASH, "FlashBoth");
				A_SpawnItemEx("GWM_Flash");
				A_WeaponOffset(0, 24, WOF_ADD);
				A_OverlayScale(OverlayID(), 1.30, 0, WOF_INTERPOLATE);
			}
			SSGG A 2;
			SSGG AA 2 {
				A_WeaponOffset(0, -12, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.15, 0, WOF_ADD);
			}
			TNT1 A 0 A_CheckReload;
			SSGG A 4 {A_WeaponOffset(0, 32, WOF_INTERPOLATE); A_OverlayScale(OverlayID(), 1, 0, WOF_INTERPOLATE);}
			Goto Reload;
		AltFire:
			SSGG A 1 {
//				invoker.DepleteAmmo(true); // depletes one shell
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				invoker.OneBarrel = !invoker.OneBarrel;
				if (invoker.OneBarrel) {
					A_Overlay(PSP_FLASH, "FlashLeft");
				}
				else {
					A_Overlay(PSP_FLASH, "FlashRight");
				}
				A_FireBullets(10, 3.32, 10, 6, "ZBulletPuff");
				A_StartSound("weapons/ssgshot2", CHAN_WEAPON);
				A_SpawnItemEx("GWM_Flash");
				A_WeaponOffset(0, 12, WOF_ADD);
				A_OverlayScale(OverlayID(), 1.15, 0, WOF_INTERPOLATE);
			}
			SSGG A 2;
			SSGG AA 2 {
				A_WeaponOffset(0, -6, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			TNT1 A 0 A_CheckReload;
			SSGG A 4 {A_WeaponOffset(0, 32, WOF_INTERPOLATE); A_OverlayScale(OverlayID(), 1, 0, WOF_INTERPOLATE); if (!invoker.OneBarrel) {player.SetPSprite(OverlayID(), ResolveState("Reload"));}}
			Goto Ready;
		Reload:
			SSGG DC 2;
			SSGG B 3;
			SSGG C 4;
			SSGG D 3;
			SSGG E 3 {
				A_StartSound("weapons/ssgo", CHAN_BODY);
				ZWL_EjectCasing("GWM_ShotgunCasing", true);
				ZWL_EjectCasing("GWM_ShotgunCasing", true, offset: (24, 4, -10));
			}
			SSGG F 2;
			SSGG FGHI 2;
			SSGG J 3 A_StartSound("weapons/ssgl", CHAN_BODY);
			SSGG KLL 2 A_OverlayPivotAlign(OverlayID(), PSPA_LEFT, PSPA_BOTTOM);
			SSGG MMM 1 {
				A_OverlayOffset(OverlayID(), 5, 10, WOF_ADD);
				A_OverlayRotate(OverlayID(), 5, WOF_ADD);
			}
			SSGG N 3 {
				A_StartSound("weapons/ssgc", CHAN_BODY);
				A_OverlayRotate(OverlayID(), 0);
				A_OverlayOffset(OverlayID(), 0);
				invoker.OneBarrel = false;
			}
			SSGG O 5;
			Goto Ready;
		FlashBoth:
			TNT1 A 0 A_Overlay(-2,"BothBarrelsFlash");
			SSGF A 1 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayScale(OverlayID(), 1.30, 0, WOF_INTERPOLATE);
			}
			SSGF A 2 Bright;
			SSGF BB 2 Bright {
				A_OverlayScale(OverlayID(), -0.15, 0, WOF_ADD);
			}
			Stop;
		FlashLeft:
			TNT1 A 0 A_Overlay(-2,"LeftBarrelFlash");
			Goto Flash;
		FlashRight:
			TNT1 A 0 A_Overlay(-2,"RightBarrelFlash");
		Flash:
			SSGF A 1 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayScale(OverlayID(), 1.15, 0, WOF_INTERPOLATE);
			}
			SSGF A 2 Bright;
			SSGF BB 2 Bright {
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			Stop;
		BothBarrelsFlash:
			SSGF C 1 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayScale(OverlayID(), 1.30, 0, WOF_INTERPOLATE);
			}
			SSGF C 2 Bright;
			SSGF DD 2 Bright {
				A_OverlayScale(OverlayID(), -0.15, 0, WOF_ADD);
			}
			Stop;
		LeftBarrelFlash:
			SSGF E 1 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayScale(OverlayID(), 1.15, 0, WOF_INTERPOLATE);
			}
			SSGF E 2 Bright;
			SSGF FF 2 Bright {
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			Stop;
		RightBarrelFlash:
			SSGF G 1 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayScale(OverlayID(), 1.15, 0, WOF_INTERPOLATE);
			}
			SSGF G 2 Bright;
			SSGF HH 2 Bright {
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			Stop;
		Spawn:
			SSGG Z -1;
			Stop;
	}
}