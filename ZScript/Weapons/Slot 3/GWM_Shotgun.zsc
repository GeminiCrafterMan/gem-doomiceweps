class GWM_Shotgun : GWM_WeaponBase
{
	Default
	{
		Weapon.SelectionOrder 1400; // kinda bad for getting stuff out of your face, so +100 for it
		Inventory.PickupSound "weapons/skpickup";
		Weapon.UpSound "weapons/draw_secondary";
		Weapon.AmmoType "GWM_Shell";
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 8;

		ZWeapon.MagazineSize 8;
		ZWeapon.ReloadSound "weapons/shell_load";
		ZWeapon.ClickSound "weapons/skempty";

		Tag "Slayer's Kahuna";
		Inventory.PickupMessage "$GOTSLAYERSKAHUNA";
		Obituary "$KILLSLAYERSKAHUNA";
		AttackSound "weapons/skshot";
	}
	States
	{
		Ready:
			SKSG A 1 ZWL_WeaponReady(ZRF_ExtraRound);
			Loop;
		Deselect:
			SKSG A 1 A_Lower(12);
			Loop;
		Select:
			SKSG A 1 A_Raise(12);
			Loop;
		Fire:
			SKSG A 1 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayPivot(OverlayID(), 0.2, flags: WOF_KEEPY);
				ZWL_FireHitscan(0, 6*Random(1,3), pellets: 8, spread: 3, tracerType: "GWM_Tracer");
				A_SpawnItemEx("GWM_Flash");
				A_GunFlash();
				A_WeaponOffset(0, 10, WOF_ADD);
				A_OverlayScale(OverlayID(), 1.15, 0, WOF_INTERPOLATE);
			}
			SKSG A 3 Bright;
			SKSG A 3 Bright {
				A_WeaponOffset(0, -5, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			SKSG A 3 {
				A_WeaponOffset(0, -5, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			TNT1 A 0 ZWL_CheckReload();
			SKSG B 5;
			SKSG CCDD 1;
			SKSG E 4 {
				A_StartSound("weapons/skpump1");
				ZWL_EjectCasing("GWM_ShotgunCasing", offset: (24, -6, -14));
			}
			SKSG DDCC 1; // These two play on CHAN_BODY because the firing sound is just too fucking long
			SKSG B 5 A_StartSound("weapons/skpump2");
			SKSG A 3;
			SKSG A 7 ZWL_ReFire();
			Goto Ready;
		AltFire:
			TNT1 A 0 {
				if (!invoker.DepleteAmmo(false, true))
				{
					A_PlaySound(invoker.clickSound, CHAN_Auto);
					player.SetPSPrite(OverlayID(), ResolveState("Ready")); // Weird, fucked-up way of making it click instead.
				}
			}
			SKSG A 1 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayPivot(OverlayID(), 0.2, flags: WOF_KEEPY);
				ZWL_FireHitscan(0, 6*Random(1,3), pellets: 8, spread: 6, tracerType: "GWM_Tracer");
				A_SpawnItemEx("GWM_Flash");
				A_GunFlash("Flash2");
				A_WeaponOffset(0, 20, WOF_ADD);
				A_OverlayScale(OverlayID(), 1.30, 0, WOF_INTERPOLATE);
			}
			SKSG A 3 Bright;
			SKSG A 3 Bright {
				A_WeaponOffset(0, -10, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.15, 0, WOF_ADD);
			}
			SKSG A 3 {
				A_WeaponOffset(0, -10, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.15, 0, WOF_ADD);
			}
			TNT1 A 0 ZWL_CheckReload();
			SKSG FFGG 1;
			SKSG H 4 {
				A_StartSound("weapons/skpump1");
				ZWL_EjectCasing("GWM_ShotgunCasing", offset: (20, 4, -14));
			}
			SKSG GGFF 1; // These two play on CHAN_BODY because the firing sound is just too fucking long
			SKSG A 5 A_StartSound("weapons/skpump2");
			SKSG A 7 ZWL_ReFire();
			Goto Ready;
		Spawn:
			SKSG Z -1;
			Stop;
		Flash:
			SKSF A 1 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayPivot(OverlayID(), 0.2, flags: WOF_KEEPY);
				A_Light1();
				A_OverlayScale(OverlayID(), 1.15, 0, WOF_INTERPOLATE);
			}
			SKSF A 3 Bright A_Light1;
			SKSF B 3 Bright {
				A_Light2();
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			Goto LightDone;
		Flash2:
			SKSF A 1 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayPivot(OverlayID(), 0.2, flags: WOF_KEEPY);
				A_Light1();
				A_OverlayScale(OverlayID(), 1.30, 0, WOF_INTERPOLATE);
			}
			SKSF A 3 Bright A_Light1;
			SKSF B 3 Bright {
				A_Light2();
				A_OverlayScale(OverlayID(), -0.15, 0, WOF_ADD);
			}
			Goto LightDone;
		Reload:
			TNT1 A 0 {
				if (!invoker.CheckAmmo(PrimaryFire, false, true))
					player.SetPSPrite(OverlayID(), ResolveState("Ready")); // Disallow reloading if there's no more ammo
			}
			TNT1 A 0 ZWL_JumpIfEmpty("Reload.Empty"); // I'm pretty sure that, for a shotgun, reloading while empty would be the same...
			SKSG B 5;
			SKSG CD 2;
			SKSG E 2 {
				A_StartSound("weapons/skpump1");
				ZWL_EjectCasing("GWM_ShotgunCasing", offset: (24, -6, -14));
			}
			// Fallthrough
		Reload.Loop:
			SKSG IJK 2;
			TNT1 A 0 ZWL_Reload(1);
			SKSG LIE 2;
			SKSG E 0 ZWL_JumpIfReloaded("Reload.End", true, true);
			Loop;
		Reload.End:
			SKSG DC 2;
			SKSG B 5 A_StartSound("weapons/skpump2");
			Goto Ready;

		Reload.Empty:
			SKSG B 5;
			SKSG CD 2;
			SKSG E 2 {
				A_StartSound("weapons/skpump1");
				ZWL_EjectCasing("GWM_ShotgunCasing", offset: (24, -6, -14));
			}
			// Fallthrough
		Reload.Empty.Loop:
			SKSG IJK 2;
			TNT1 A 0 ZWL_Reload(1);
			SKSG LIE 2;
			SKSG E 0 ZWL_JumpIfReloaded("Reload.Empty.End", true);
			Loop;
		Reload.Empty.End:
			SKSG DC 2;
			SKSG B 5 A_StartSound("weapons/skpump2");
			Goto Ready;
	}
}