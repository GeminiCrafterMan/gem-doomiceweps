Class GWM_KahunaSpread : Inventory { Default {Inventory.Maxamount 24;}}

class GWM_SlayersKahuna : GWM_WeaponBase
{
	int kahunaspread;
	Default
	{
		//$Category Weapons
		//$Title ""Slayer's Kahuna" Shotgun
		Weapon.SelectionOrder 1300;
		Inventory.PickupSound "weapons/skpickup";
		Weapon.UpSound "weapons/draw_secondary";
		Weapon.AmmoType "GWM_Shell";
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 9;

		ZWeapon.MagazineSize 9;
		ZWeapon.ReloadSound "weapons/shell_load";
		ZWeapon.ClickSound "weapons/skempty";

		GWM_WeaponBase.BaseVersion "GWM_ZombieShotgun";
		Tag "$TAG_SLAYERSKAHUNA";
		Inventory.PickupMessage "$GOTSLAYERSKAHUNA";
		Obituary "$KILLSLAYERSKAHUNA";
		AttackSound "weapons/skshot";
	}
	States
	{
		Ready:
			SKSG AAAA 1 ZWL_WeaponReady();
			Loop;
		Select:
			TNT1 A 0 {
				ZWL_QuickRaise();
				A_WeaponOffset(64, 160);
			}
			SKSG PPPPPP 1 {
				A_WeaponOffset(-8, -16, WOF_ADD);
			}
			SKSG AA 1 {
				A_WeaponOffset(-8, -16, WOF_ADD);
			}
			SKSG H 4 {A_StartSound("weapons/skpump1"); ZWL_WeaponReady( );}
			SKSG IIJJ 1 ZWL_WeaponReady( );
			SKSG A 1 {A_StartSound("weapons/skpump2"); ZWL_WeaponReady( );}
			Goto Ready;
		Deselect:
			SKSG AA 1 {
				A_WeaponOffset(8, 16, WOF_ADD);
			}
			SKSG PPPPPP 1 {
				A_WeaponOffset(8, 16, WOF_ADD);
			}
			TNT1 A 0 A_WeaponOffset(0, flags: WOF_KEEPY);
			TNT1 A 0 ZWL_QuickLower;
			Stop;
		Recoil:
			TNT1 AA 1 A_SetPitch(pitch+0.40);
			TNT1 AAAAAA 1 A_SetPitch(pitch-0.20);
			Stop;
		Recoil2:
			TNT1 AA 1 A_SetPitch(pitch+0.80);
			TNT1 AAAAAA 1 A_SetPitch(pitch-0.40);
			Stop;
		Fire:
			SKSG A 1 Bright {
				A_Overlay(55, "Recoil");
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayPivot(OverlayID(), 0.2, flags: WOF_KEEPY);
				ZWL_FireHitscan(0, 10, pellets: 12, spread: 5+(Countinv("GWM_KahunaSpread")*0.4), tracerType: "GWM_Tracer");
				
				A_ChangeVelocity(-Cos(pitch)*2, 0, sin(pitch)*2,CVF_RELATIVE);
				A_SpawnItemEx("GWM_Flash");
				A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5);
				A_GunFlash();
				A_WeaponOffset(0, 42, WOF_INTERPOLATE);
				A_OverlayScale(OverlayID(), 1.15, 0, WOF_INTERPOLATE);
				A_QuakeEx(1.5, 1.5, 1.5, 4, 0, 1, "", QF_SHAKEONLY);
			}
			SKSG A 1 Bright {
				A_WeaponOffset(0, -5, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			SKSG A 1 {
				A_WeaponOffset(0, -5, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			SKSG A 1 BRIGHT;
			TNT1 A 0 ZWL_CheckReload();
			SKSG B 5;
			SKSG CCCDD 1;
			SKSG E 4 {
				A_StartSound("weapons/skpump1");
				ZWL_EjectCasing("GWM_ShotgunCasing", offset: (24, -6, -14));
			}
			SKSG DDCCC 1;
			SKSG B 5 A_StartSound("weapons/skpump2");
			SKSG A 3;
			SKSG A 0 ZWL_ReFire();
			Goto Ready;
		AltFire:
			TNT1 A 0 {
				if (!invoker.DepleteAmmo(false, true))
				{
					A_PlaySound(invoker.clickSound, CHAN_Auto);
					player.SetPSPrite(OverlayID(), ResolveState("Ready")); // G: Weird, fucked-up way of making it click instead.
				}
			}
			SKSG A 1 Bright {
				A_Overlay(55, "Recoil2");
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayPivot(OverlayID(), 0.2, flags: WOF_KEEPY);
				ZWL_FireHitscan(0, 11, pellets: 12, spread: 4+(Countinv("GWM_KahunaSpread")*0.8), tracerType: "GWM_Tracer");
				A_ChangeVelocity(-Cos(pitch)*2.5, 0, sin(pitch)*2.5,CVF_RELATIVE);
				A_SpawnItemEx("GWM_Flash");
				A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5);
				A_GunFlash("Flash2");
				A_WeaponOffset(0, 52, WOF_INTERPOLATE);
				A_OverlayScale(OverlayID(), 1.30, 0, WOF_INTERPOLATE);
				A_QuakeEx(1.5, 1.5, 1.5, 4, 0, 1, "", QF_SHAKEONLY);
				A_GiveInventory("GWM_KahunaSpread",2);
			}
			SKSG A 1 Bright {
				A_WeaponOffset(0, -10, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.15, 0, WOF_ADD);
			}
			SKSG A 1 Bright {
				A_WeaponOffset(0, -10, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.15, 0, WOF_ADD);
			}
			SKSG AA 1;
			SKSG QRSSAA 1;
			TNT1 A 0 {
				ZWL_CheckReload();
				ZWL_EjectCasing("GWM_ShotgunCasing", offset: (20, 4, -14));
			}
			TNT1 A 0 ZWL_ReFire();
			SKSG FG 2;
			// G: wh-
			// G: okay, i guess
			// M: OLD CODE
			// M: Did you delete the old code? I don't mind but these comments are gonna look MAD odd.
			// G: i think it was about you leaving in old code past the Goto Ready but it's been reworked since so it looks like i'm insane :D
			SKSG H 4 A_StartSound("weapons/skpump1");
			SKSG IIJJ 1;
			SKSG A 5 A_StartSound("weapons/skpump2");
			SKSG A 0 ZWL_ReFire();
			Goto Ready;
		Spawn:
			SKSG Z -1;
			Stop;
		Flash:
			SKSF A 1 Bright {
				A_OverlayFlags(OverlayID(), PSPF_FORCEAlpha, true);
				A_OverlayAlpha(OverlayID(), 0.6);
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayPivot(OverlayID(), 0.2, flags: WOF_KEEPY);
				A_Light1();
				A_OverlayScale(OverlayID(), 1.15, 0, WOF_INTERPOLATE);
			}
			SKSF A 1 Bright {
				A_Light1();
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			SKSF B 1 Bright {
				A_Light2();
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
			Goto LightDone;
		Flash2:
			SKSF A 1 Bright {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_OverlayPivot(OverlayID(), 0.2, flags: WOF_KEEPY);
				A_Light1();
				A_OverlayScale(OverlayID(), 1.30, 0, WOF_INTERPOLATE);
			}
			SKSF A 1 Bright 
			{
				A_Light1();
				A_OverlayScale(OverlayID(), -0.15, 0, WOF_ADD);
			}
			SKSF B 1 Bright {
				A_Light2();
				A_OverlayScale(OverlayID(), -0.15, 0, WOF_ADD);
			}
			Goto LightDone;
		Reload:
			TNT1 A 0 {
				if (!invoker.CheckAmmo(PrimaryFire, false, true))
					player.SetPSPrite(OverlayID(), ResolveState("Ready")); // G: Disallow reloading if there's no more ammo
			}
			TNT1 A 0 ZWL_JumpIfEmpty("Reload.Empty");
			SKSG B 5;
			SKSG CCCTT 1;
			SKSG U 2;
			// Fallthrough
		Reload.Loop:
			SKSR ABC 2;
			TNT1 A 0 ZWL_Reload(1);
			SKSR DE 2;
			SKSG E 0 ZWL_JumpIfReloaded("Reload.End", true);
			Loop;
		Reload.End:
			SKSG TTCCC 1;
			SKSG B 5;
			Goto Ready;

		Reload.Empty:
			SKSG B 5;
			SKSG CCCDD 1;
			SKSG E 2 A_StartSound("weapons/skpump1");
			SKSG KLM 2;
			TNT1 A 0 ZWL_Reload(1);
			SKSG NO 2;
			SKSG E 4;
			SKSG U 5 A_StartSound("weapons/skpump2");
			ZSHG E 0 ZWL_JumpIfReloaded("Reload.End", true);
			Goto Reload.Loop;
	}
}