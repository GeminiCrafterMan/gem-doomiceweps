class GWM_IceShotgun : Shotgun
{
	Default
	{
		Weapon.AmmoType "GWM_IceShell";
		Inventory.PickupMessage "You got the Ice Shotgun!";
		Obituary "Shouldn't see this.";
		Tag "Ice Shotgun";
	}
	States
	{
		Ready:
			ISHT A 1 A_WeaponReady;
			Loop;
		Deselect:
			ISHT A 1 A_Lower;
			ISHT A 0 A_Lower;
			Loop;
		Select:
			ISHT A 1 A_Raise;
			ISHT A 0 A_Raise;
			Loop;
		Fire:
			ISHT A 1 {
				A_OverlayPivotAlign(OverlayID(), PSPA_CENTER, PSPA_BOTTOM);
				A_FireProjectile("GWM_ShotgunIce");
				A_StartSound("weapons/iceshgn", CHAN_WEAPON);
				A_SpawnItemEx("GWM_IceFlash");
				A_Overlay(-2, "Flash");
				A_WeaponOffset(0, 20, WOF_ADD);
				A_OverlayScale(OverlayID(), 1.15, 0, WOF_INTERPOLATE);
			}
			ISHT A 2;
			ISHT AA 4 {
				A_WeaponOffset(0, -10, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.075, 0, WOF_ADD);
			}
//			ISHT A 2;
			ISHT B 5;
			ISHT CCC 2;// A_WeaponOffset(5, 10, WOF_ADD);
			ISHT D 4;
			ISHT CCC 2;// A_WeaponOffset(-5, -10, WOF_ADD);
			ISHT B 5;
			ISHT A 3;
			ISHT A 7 A_ReFire;
			Goto Ready;
		AltFire:
		// Was going to be a kick, but I don't wanna.
		// Update: I wanna.
			KICK A 4;
			KICK B 4 A_Punch;
			KICK C 5;
			KICK B 4;
			KICK A 5 A_ReFire;
			Goto Ready;
		Flash:
			TNT1 A 0 A_OverlayOffset(OverlayID(), 159.5, 112);
			SGFL ABCCDE 1 Bright;
			Stop;
		Spawn:
			ISHT Z -1;
			Stop;
	}
}