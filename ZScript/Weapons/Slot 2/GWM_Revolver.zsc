class GWM_Revolver : GWM_WeaponBase
{
	Default
	{
		Weapon.SelectionOrder 1900;
		Weapon.UpSound "weapons/draw_secondary";
		Weapon.AmmoUse1 1;
		Weapon.AmmoType1 "GWM_LowCaliberClip";
		Weapon.AmmoGive 6;

		ZWeapon.MagazineSize 6;
		ZWeapon.ReloadSound "weapons/rvlvreload2";
		ZWeapon.ClickSound "weapons/92fsdryfire";

		+WEAPON.NOAUTOFIRE
		Inventory.PickupSound "weapons/rvlvpickup";
		Inventory.PickupMessage "$GOTREVOLVER";
		AttackSound "weapons/rvlvfire";
		Obituary "$KILLREVOLVER";
		Tag "Revolver";
	}
	States
	{
		Spawn:
			RVLG Z -1;
			Stop;
		Select:
			RVLG A 1 A_Raise(12);
			Loop;
		Ready:
			RVLG A 1 ZWL_WeaponReady();
			Loop;
		Deselect:
			RVLG A 1 A_Lower(12);
			Loop;
		Fire:
			TNT1 A 0 {return ZWL_JumpIfEmpty("Fire.Empty");}
			RVLG B 2 {
				ZWL_FireHitscan(3, 14*Random(2,3), tracerType: "GWM_Tracer");
				A_SpawnItemEx("GWM_Flash");
				A_GunFlash();
				A_SetPitch(pitch-0.33);
			}
			RVLG CCD 1 A_SetPitch(pitch-0.11);
			RVLG DCA 2 A_SetPitch(pitch+0.22);
			TNT1 A 0 ZWL_ReFire(null, true);
			Goto Ready;
		Fire.Empty:
			RVLG BBCCDDCCAA 1;
			TNT1 A 0 ZWL_CheckReload();
			Goto Reload;
		Reload:
			TNT1 A 0 {
				if (!invoker.CheckAmmo(PrimaryFire, false, true))
					player.SetPSPrite(OverlayID(), ResolveState("Ready")); // Disallow reloading if there's no more ammo
			}
			RVLG D 1;
			RVLG E 1 A_StartSound("weapons/rvlvreload1", CHAN_BODY);
			RVLG FGHIJK 1;
			RVLG L 5 {
				ZWL_EjectCasing("GWM_PistolCasing", true, 45, offset: (22, -2, -12));
				ZWL_EjectCasing("GWM_PistolCasing", true, 45, offset: (22, 2, -12));
				ZWL_EjectCasing("GWM_PistolCasing", true, 45, offset: (24, -4, -12));
				ZWL_EjectCasing("GWM_PistolCasing", true, 45, offset: (26, -2, -12));
				ZWL_EjectCasing("GWM_PistolCasing", true, 45, offset: (26, 2, -12));
				ZWL_EjectCasing("GWM_PistolCasing", true, 45, offset: (24, 4, -12));
			}
			RVLG MNOPQ 2;
			RVLG R 4 ZWL_Reload();
			RVLG STUVWX 2;
			RVLG HGFE 2;
			TNT1 A 0 A_StartSound("weapons/rvlvreload3", CHAN_BODY);
			RVLG D 4;
			Goto Ready;
		Flash:
			RVLF A 1 Bright A_Light2;
			RVLF B 1 Bright A_Light1;
			Goto LightDone;
	}
}