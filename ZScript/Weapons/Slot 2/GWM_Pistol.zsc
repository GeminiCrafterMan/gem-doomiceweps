class GWM_Pistol : GWM_WeaponBase replaces Pistol
{
	Default
	{
		Weapon.SelectionOrder 1900;
		Weapon.UpSound "weapons/draw_secondary";
		Weapon.AmmoUse1 1;
		Weapon.AmmoType1 "GWM_LowCaliberClip";
		Weapon.AmmoGive 16;

		ZWeapon.MagazineSize 15;
		ZWeapon.ReloadSound "weapons/92fsclipin";
		ZWeapon.ClickSound "weapons/92fsdryfire";

		+WEAPON.WIMPY_WEAPON
		+WEAPON.NOAUTOFIRE
		Inventory.PickupSound "weapons/92fspickup";
		Inventory.PickupMessage "$GOT92FS";
		AttackSound "weapons/92fsfire";
		Obituary "$KILL92FS";
		Tag "$TAG_92FS";
	}
	States
	{
		Spawn:
			92FG Z -1;
			Stop;
		Select:
			92FG A 1 A_Raise(18);
			Loop;
		Deselect:
			92FG A 1 A_Lower(18);
			Loop;
		Ready:
			92FG A 1 ZWL_WeaponReady(ZRF_ExtraRound);
			Loop;
		Fire:
			TNT1 A 0 {return ZWL_JumpIfEmpty("Fire.Empty");}
			92FG B 2 {
				ZWL_FireHitscan(2, 16, tracerType: "GWM_Tracer");
				A_SpawnItemEx("GWM_Flash");
				A_SpawnItemEx("GWM_MuzzleFlashLight",0,0,5);
				A_GunFlash();
			}
			92FG C 1 {
				ZWL_EjectCasing("GWM_PistolCasing", offset: (24, 0, -6));
			}
			92FG DEEEDDCCBB 1 ZWL_WeaponReady(ZRF_ExtraRound|ZRF_NoBob);
			TNT1 A 0 ZWL_ReFire(null, true);
			Goto Ready;
		Fire.Empty:
			92FG B 2;
			92FG C 1;
			92FG DEEEDDCCBB 1;
			TNT1 A 0 ZWL_CheckReload();
			Goto Reload;
		Reload:
			TNT1 A 0 {
				if (!invoker.CheckAmmo(PrimaryFire, false, true))
					player.SetPSPrite(OverlayID(), ResolveState("Ready")); // Disallow reloading if there's no more ammo
			}
			92FG CDE 2;
			TNT1 A 0 A_StartSound("weapons/92fsslideback", CHAN_AUTO);
			92FG FG 1;
			92FG JKON 1;
			92FG G 1 ZWL_EjectCasing("GWM_PistolEmptyMag", true, 45, 2, 16, (24, 0, -24));
			92FG NOPQ 1;
			TNT1 A 0 ZWL_Reload(-1);
			92FG QRGGG 1;
			92FG H 3 A_StartSound("weapons/92fsslidefwd", CHAN_AUTO);
			92FG HIA 2;
			Goto Ready;
		Flash:
			92FF A 1 Bright A_Light2;
			92FF B 1 Bright A_Light1;
			Goto LightDone;
	}
}