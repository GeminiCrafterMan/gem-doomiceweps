class GWM_Pistol : GWM_WeaponBase replaces Pistol
{
	Default
	{
		//$Category Weapons
		//$Title "Pistol
		Weapon.SelectionOrder 1900;
		Weapon.UpSound "weapons/draw_secondary";
		Weapon.AmmoUse1 1;
		Weapon.AmmoType1 "GWM_LowCaliberMagazine";
		Weapon.AmmoGive 16;

		ZWeapon.MagazineSize 15;
		ZWeapon.ReloadSound "weapons/pistolclipin";
		ZWeapon.ClickSound "weapons/pistoldryfire";

		+WEAPON.WIMPY_WEAPON
		+WEAPON.NOAUTOFIRE
		Inventory.PickupSound "weapons/pistolpickup";
		Inventory.PickupMessage "$GOTPISTOL";
		AttackSound "weapons/pistolfire";
		Obituary "$KILLPISTOL";
		Tag "$TAG_PISTOL";
	}
	States
	{
		Spawn:
			PSTG Z -1;
			Stop;
		Select:
			TNT1 A 0 {
				ZWL_QuickRaise();
				A_WeaponOffset(-64, 160);
			}
			PSTG [[[[[[ 1 {
				A_WeaponOffset(8, -16, WOF_ADD);
			}
			PSTG AA 1 {
				A_WeaponOffset(8, -16, WOF_ADD);
			}
			Goto Ready;
		Deselect:
			PSTG AA 1 {
				A_WeaponOffset(-8, 16, WOF_ADD);
			}
			PSTG [[[[[[ 1 {
				A_WeaponOffset(-8, 16, WOF_ADD);
			}
			TNT1 A 0 A_WeaponOffset(0, flags: WOF_KEEPY);
			TNT1 A 0 ZWL_QuickLower;
			Stop;
/*		Select:
			PSTG A 1 A_Raise(18);
			Loop;
		Deselect:
			PSTG A 1 A_Lower(18);
			Loop;*/
		Ready:
			PSTG A 1 ZWL_WeaponReady(ZRF_ExtraRound);
			Loop;
		Fire:
			PSTG B 2 {
				ZWL_FireHitscan(2, 16, tracerType: "GWM_Tracer");
				A_SpawnItemEx("GWM_Flash");
				A_SpawnItemEx("GWM_MuzzleFlashLight",0,0,5);
				A_Setpitch(pitch-0.6);
				A_GunFlash();
			}
			PSTG C 1 {
				ZWL_EjectCasing("GWM_PistolCasing", offset: (24, 0, -6));
			}
			PSTG DEEE 1
			{
				A_Setpitch(pitch+0.15);
				ZWL_WeaponReady(ZRF_ExtraRound|ZRF_NoBob);
			}
			PSTG DDCCBB 1 ZWL_WeaponReady(ZRF_ExtraRound|ZRF_NoBob);
			TNT1 A 0 ZWL_ReFire(null, true);
			Goto Ready;
		Reload:
			TNT1 A 0 {
				if (!invoker.CheckAmmo(PrimaryFire, false, true))
					player.SetPSPrite(OverlayID(), ResolveState("Ready")); // Disallow reloading if there's no more ammo
			}
			TNT1 A 0 A_StartSound("weapons/pistolslideback", CHAN_AUTO);
			PSTG CDE 2;
			PSTG FGHI 2;
			TNT1 A 0 A_StartSound("weapons/pistolclipout", CHAN_AUTO); 
			PSTG JKLM 2;
			TNT1 A 0 ZWL_EjectCasing("GWM_PistolEmptyMag", true, 45, 2, 16, (24, 0, -24));
			PSTG M 4;
			PSTG NOPQR 2;
			TNT1 A 0 ZWL_Reload(-1);
			PSTG R 4;
			TNT1 A 0 A_StartSound("weapons/pistolslidefwd", CHAN_AUTO);
			PSTG STU 2;
			PSTG V 3;
			PSTG WXY 2;
			Goto Ready;
		Flash:
			PSTF A 1 Bright A_Light2;
			PSTF B 1 Bright A_Light1;
			Goto LightDone;
	}
}