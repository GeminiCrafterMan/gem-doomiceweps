class GWM_MP40 : GWM_WeaponBase
{
	Default
	{
		Weapon.SelectionOrder 1800;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 20;
		Weapon.AmmoType "Clip";
		+WEAPON.WIMPY_WEAPON
		Weapon.UpSound "weapons/draw_secondary";
		Obituary "$KILLMP40";
		Inventory.PickupMessage "$GOTMP40";
		Tag "MP40";
		Scale 0.5;
	}
	States
	{
		Ready:
			MP4G A 1 A_WeaponReady;
			Loop;
		Deselect:
			MP4G A 1 A_Lower(12);
			Loop;
		Select:
			MP4G A 1 A_Raise(12);
			Loop;
		Fire:
			MP4G A 1 {
				A_FireBullets(7.6, 0, 1, 4, "ZBulletPuff", missile: "GWM_Tracer", spawnheight: 0);
				A_StartSound("weapons/mp40fire", CHAN_WEAPON);
				A_SpawnItemEx("GWM_Flash");
				A_Overlay(-2, "Flash");
				A_WeaponOffset(0, 3, WOF_ADD);
				ZWL_EjectCasing("GWM_PistolCasing");//, offset: (24, 0, -10));
			}
			MP4G C 2 A_WeaponOffset(0, -3, WOF_ADD);
			MP4G B 1;
			MP4G A 2 A_ReFire;
			Goto Ready;
		Flash:
			TNT1 A 0 A_OverlayOffset(OverlayID(), 0, 9, WOF_ADD);
			MP4F A 2 Bright A_Light1;
			MP4F B 2 Bright A_Light2;
			Goto LightDone;
		Spawn:
			MP4G Z -1;
			Loop;
	}
}