class GWM_SMG : GWM_WeaponBase
{
	Default
	{
		Weapon.SelectionOrder 1800;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 50;
		Weapon.AmmoType "GWM_LowCaliberMagazine";

		ZWeapon.MagazineSize 50;
		ZWeapon.ReloadSound "weapons/smgclipin";
		ZWeapon.ClickSound "weapons/92fsdryfire";

		+WEAPON.WIMPY_WEAPON
		Weapon.UpSound "weapons/draw_smg";
		AttackSound "weapons/smgfire";
		Obituary "$KILLSMG";
		Inventory.PickupSound "weapons/smgpickup";
		Inventory.PickupMessage "$GOTSMG";
		Tag "$TAG_SMG";
	}
	States
	{
		Ready:
			SMGG A 1 ZWL_WeaponReady(ZRF_ExtraRound);
			Loop;
		Deselect:
			SMGG A 1 A_Lower(18);
			Loop;
		Select:
			SMGG A 1 A_Raise(18);
			Loop;
		Fire:
			SMGG A 1 {
				ZWL_FireHitscan(6, 12, tracerType: "GWM_Tracer");
				A_SpawnItemEx("GWM_MuzzleFlashLight",0,0,5);
				A_SpawnItemEx("GWM_Flash");
				A_GunFlash();
				A_Setpitch(pitch+0.75);
				A_WeaponOffset(0, 2, WOF_ADD);
				ZWL_EjectCasing("GWM_PistolCasing");
			}
			SMGG AA 1 {A_WeaponOffset(0, -1, WOF_ADD); A_Setpitch(pitch-.375);}
				
			SMGG A 0 { return ZWL_JumpIfEmpty("Fire.Empty"); }
			SMGG A 1 ZWL_ReFire(null, true);
			Goto Ready;
		Fire.Empty:
			TNT1 A 0 ZWL_CheckReload();
			Goto Reload;
		Flash:
			SMGF A 1 Bright A_Light1;
			SMGF B 1 Bright A_Light2;
			Goto LightDone;
		Spawn:
			SMGG Z -1;
			Loop;
		Reload:
			TNT1 A 0 {
				if (!invoker.CheckAmmo(PrimaryFire, false, true))
					player.SetPSPrite(OverlayID(), ResolveState("Ready")); // Disallow reloading if there's no more ammo
			}
			SMGG BCDEF 2;
			SMGG G 2 A_StartSound("weapons/smgclipout");
			SMGG HIJK 2;
			SMGG K 2 ZWL_EjectCasing("GWM_PistolEmptyMag", true, 45, 2, 16, (24, 0, -24));
			SMGG KJIH 2;
			SMGG G 2 ZWL_Reload(-1);
			SMGG FE 2;
			TNT1 A 0 A_StartSound("weapons/smgclipend");
			SMGG DCB 2;
			Goto Ready;
	}
}