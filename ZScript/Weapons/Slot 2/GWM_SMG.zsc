class GWM_SMG : GWM_WeaponBase
{
	Default
	{
		Weapon.SelectionOrder 1800;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 20;
		Weapon.AmmoType "Clip";
		+WEAPON.WIMPY_WEAPON
		Weapon.UpSound "weapons/draw_smg";
		Obituary "$KILLSMG";
		Inventory.PickupMessage "$GOTSMG";
		Tag "SMG";
	}
	States
	{
		Ready:
			SMGG A 1 A_WeaponReady;
			Loop;
		Deselect:
			SMGG A 1 A_Lower(12);
			Loop;
		Select:
			SMGG A 1 A_Raise(12);
			Loop;
		Fire:
			SMGG A 1 {
				A_FireBullets(8.9, 0, 1, 7, "ZBulletPuff", missile: "GWM_Tracer", spawnheight: 0);
				A_StartSound("weapons/smgfire", CHAN_WEAPON);
				A_SpawnItemEx("GWM_Flash");
				A_GunFlash();
				A_WeaponOffset(0, 2, WOF_ADD);
				ZWL_EjectCasing("GWM_PistolCasing");
			}
			SMGG A 2 A_WeaponOffset(0, -2, WOF_ADD);
			SMGG A 1;
			SMGG A 2 A_ReFire;
			Goto Ready;
		Flash:
			SMGF A 1 Bright A_Light1;
			SMGF B 1 Bright A_Light2;
			Goto LightDone;
		Spawn:
			SMGG Z -1;
			Loop;
	}
}