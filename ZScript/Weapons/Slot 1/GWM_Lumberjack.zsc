class GWM_Lumberjack : GWM_WeaponBase
{
	Default
	{
		//$Title Chainsaw
		Weapon.Kickback 0;
		Weapon.SelectionOrder 2200;
		Weapon.UpSound "weapons/draw_csaw";
		Weapon.ReadySound "weapons/csawidle";
		+WEAPON.MELEEWEAPON
		Obituary "$KILLLUMBERJACK";
		Inventory.PickupMessage "$GOTLUMBERJACK";
		Inventory.Icon "LJAKZ0";
		Tag "$TAG_LUMBERJACK";
	}
	States
	{
		Spawn:
			LJAK Z -1;
			Stop;
		Ready:
			LJAK ABAB 4 A_WeaponReady;
			Loop;
		Deselect:
			TNT1 A 0 A_StopSound(CHAN_BODY);
			LJAK AAAABBBB 1 A_Lower(18);
			Loop;
		Select:
			LJAK AAAABBBB 1 A_Raise(18);
			Loop;
		Fire:
			LJAK C 3 A_StartSound("weapons/csawup", CHAN_BODY);
			LJAK D 3;
			TNT1 A 0 {A_StartSound("weapons/csawloop", CHAN_BODY, CHANF_LOOPING); A_ReFire();}
			Goto Release;
		Hold:
			LJAK C 2 A_Saw("", "weapons/csawhit", 22, "ZBulletPuff", SF_NORANDOM);
			LJAK D 2;
			TNT1 A 0 {A_StartSound("weapons/csawloop", CHAN_BODY, CHANF_LOOPING); A_ReFire();}
			Goto Release;
		Release:
			LJAK C 3 A_StartSound("weapons/csawdown", CHAN_BODY);
			LJAK D 3;
			Goto Ready;
	}
}