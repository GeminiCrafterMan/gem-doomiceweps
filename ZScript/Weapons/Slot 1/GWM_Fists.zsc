class GWM_Fists : GWM_AkimboWeaponBase replaces Fist
{
	Default
	{
		Weapon.SelectionOrder 3700;
		Weapon.UpSound "weapons/draw_fist";
		+INVENTORY.UNDROPPABLE
		+WEAPON.NOAUTOFIRE
		+WEAPON.NOALERT
		+WEAPON.WIMPY_WEAPON
		+WEAPON.MELEEWEAPON
		+WEAPON.NOAUTOSWITCHTO
		Obituary "$KILLFISTS";
		Tag "$TAG_FISTS";
		AttackSound "weapons/fist_hit";
	}
	States
	{
		Spawn:
			TNT1 A -1;
			Stop;
		// Redefine only if you want to significantly change selection animation
		Select:
			TNT1 A 0
			{
				A_overlay(PSP_RIGHTGUN, "Select.Right");
				A_Overlay(PSP_LEFTGUN, "Select.Left");
				A_OverlayFlags(PSP_LEFTGUN, PSPF_FLIP|PSPF_MIRROR, true);
			}
			TNT1 A 1 A_Raise(invoker.raisespeed);
			wait;
		/*////////////////////////
			LEFT HAND STATES
		*/////////////////////////
		Ready.Left:
			FIST C 1 Left_WeaponReady();
			Loop;
		Deselect.Left:
			FIST C 1;
			Loop;
		Select.Left:
			FIST C 1 Left_Raise();
			Loop;
		Fire.Left:
			FIST C 0 A_JumpIfInventory("PowerStrength", 1, "Fire.Left.Berserk");
			FIST TC 1;
			FIST D 0 A_StartSound("weapons/fistjab", CHAN_LEFTGUN);
			FIST G 2 Left_CustomPunch(11, 1, 0);
			FIST FEDDCCC 2;
			TNT1 A 0 Left_ReFire();
			Goto Ready.Left;
		Fire.Left.Berserk:
			FIST TC 1;
			FIST D 0 A_StartSound("weapons/fistjab", CHAN_LEFTGUN);
			FIST G 2 Left_CustomPunch(125, 1, 0, "GWM_BerserkPuff");
			FIST FEDDCCC 2;
			TNT1 A 0 Left_ReFire();
			Goto Ready.Left;
		/*////////////////////////
			RIGHT HAND STATES
		*/////////////////////////
		Ready.Right:
			FIST B 1 Right_WeaponReady();
			Loop;
		Deselect.Right:
			FIST B 1;
			Loop;
		Select.Right:
			FIST B 1 Right_Raise();
			Loop;
		Fire.Right:
			FIST C 0 A_JumpIfInventory("PowerStrength", 1, "Fire.Right.Berserk");
			FIST TC 1;
			FIST D 0 A_StartSound("weapons/fistjab", CHAN_RIGHTGUN);
			FIST G 2 Right_CustomPunch(11, 1, 0);
			FIST FEDDCCC 2;
			TNT1 A 0 Right_ReFire();
			Goto Ready.Right;
		Fire.Right.Berserk:
			FIST TC 1;
			FIST D 0 A_StartSound("weapons/fistjab", CHAN_RIGHTGUN);
			FIST G 2 Right_CustomPunch(125, 1, 0, "GWM_BerserkPuff");
			FIST FEDDCCC 2;
			TNT1 A 0 Right_ReFire();
			Goto Ready.Right;
	}
}
class GWM_FistPuff : Actor
{
	Default
	{
		+NOBLOCKMAP
		+NOGRAVITY
		-ALLOWPARTICLES
		-RANDOMIZE
		+ZDOOMTRANS
		RenderStyle "Translucent";
		Alpha 0.5;
		VSpeed 1;
		Mass 5;
	}
	States
	{
		Melee:
			TNT1 A 0 A_StartSound("weapons/fist_hit_world", 6);
			PUFF CD 4;
			Stop;
	}
}
class GWM_BerserkPuff : Actor
{
	Default
	{
		+NOBLOCKMAP
		+NOGRAVITY
		-ALLOWPARTICLES
		-RANDOMIZE
		+PUFFONACTORS
		RenderStyle "Add";
		Alpha 0.5;
		Scale 0.65;
		VSpeed 0;
		Mass 5;
		DamageType "Rocket";
	}
	States
	{
		Spawn:
			PUFF A 4 Bright;
			PUFF BCD 4;
			Stop;
		Melee:
			TNT1 A 0 {
				Radius_Quake(6, random(8,16), 0, 3, 0);
				A_StartSound("weapons/berserk_hit_world", 6);
			}
			EXPL ABCDEFGHIJ 1 Bright A_FadeOut(0.05);
			Stop;
		XDeath:
			TNT1 A 0 {
				Radius_Quake(6, random(8,16), 0, 3, 0);
				A_StartSound("weapons/berserk_hitbod", 6);
			}
			EXPL ABCDEFGHIJ 1 Bright A_FadeOut(0.05);
			Stop;
	}
}