class GWM_Levenbrech : GWM_WeaponBase replaces Gauntlets // not that it needs to be this way...
{
	Default
	{
		Weapon.SelectionOrder 2200;
		+WEAPON.MELEEWEAPON
		+WEAPON.AXEBLOOD
		Tag "Levenbrech";
		Inventory.Icon "LBREZ0";
		Obituary "$KILLLEVENBRECH"; // hehe lifebreaker
		Inventory.PickupMessage "$GOTLEVENBRECH";
		Weapon.SisterWeapon "GWM_LevenbrechPowered";
		Weapon.UpSound "weapons/draw_sword";
		AttackSound "weapons/swordhit";
	}
	States
	{
		Spawn:
			LBRE Z -1;
			Stop;
		Select:
			LBRE A 1 A_Raise(12);
			Loop;
		Deselect:
			LBRE A 1 A_Lower(12);
			Loop;
		Ready:
			LBRE A 1 A_WeaponReady;
			Loop;
		Fire:
			LBRE BCD 2;
			TNT1 A 0 {
				A_StartSound("weapons/swordslash");
				A_CustomPunch(4*random(1, 10), true, pufftype: "ZBulletPuff");
			}
			LBRE EFG 2;
			LBRE A 1;
			Goto Ready;
	}
}
class GWM_LevenbrechPowered : GWM_Levenbrech
{
	Default
	{
		Weapon.SisterWeapon "GWM_Levenbrech";
		-THRUGHOST
		+WEAPON.POWERED_UP
		Obituary "$KILLLEVENBRECH_POWERED";
	}
	States
	{
		Select:
			LBRE H 1 Bright A_Raise;
			LBRE H 0 A_Raise;
			Loop;
		Deselect:
			LBRE H 1 Bright A_Lower;
			LBRE H 0 A_Lower;
			Loop;
		Ready:
			LBRE HHHHIIIIJJJJ 1 Bright A_WeaponReady;
			Loop;
		Fire:
			LBRE KLM 2 Bright;
			TNT1 A 0 {
				A_StartSound("weapons/swordslash");
				A_CustomPunch(8*random(1, 10), true, pufftype: "ZBulletPuff", lifesteal: 0.5);
			}
			LBRE NOP 2 Bright;
			LBRE H 1 Bright;
			Goto Ready;
	}
}

class GWM_LevenbrechBlood : AxeBlood replaces AxeBlood
{
	States
	{
		Spawn:
			SLIC ABCDEF 2;
			Stop;
	}
}