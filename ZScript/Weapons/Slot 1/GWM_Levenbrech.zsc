class GWM_Levenbrech : Weapon replaces Gauntlets
{
	Default
	{
		+WEAPON.MELEEWEAPON
		+THRUGHOST
		Tag "Levenbrech";
		Inventory.Icon "LBREZ0";
		Obituary "%o had their life broken by %k's Levenbrech."; // hehe lifebreaker
		Inventory.PickupMessage "Levenbrech";
		Weapon.SisterWeapon "GWM_LevenbrechPowered";
		Weapon.UpSound "weapons/swordup";
		AttackSound "weapons/swordhit";
	}
	States
	{
		Spawn:
			LBRE Z -1;
			Stop;
		Select:
			LBRE A 1 A_Raise(12);
			Loop;
		Deselect:
			LBRE A 1 A_Lower(12);
			Loop;
		Ready:
			LBRE A 1 A_WeaponReady;
			Loop;
		Fire:
			LBRE BCD 2;
			TNT1 A 0 {
				A_StartSound("weapons/swordslash");
				A_CustomPunch(4*random(1, 10), true, pufftype: "ZBulletPuff");
			}
			LBRE EFG 2;
			LBRE A 1;
			Goto Ready;
	}
}
class GWM_LevenbrechPowered : GWM_Levenbrech
{
	Default
	{
		Weapon.SisterWeapon "GWM_Levenbrech";
		-THRUGHOST
		+WEAPON.POWERED_UP
	}
	States
	{
		Select:
			LBRE H 1 Bright A_Raise;
			LBRE H 0 A_Raise;
			Loop;
		Deselect:
			LBRE H 1 Bright A_Lower;
			LBRE H 0 A_Lower;
			Loop;
		Ready:
			LBRE HHHHIIIIJJJJ 1 Bright A_WeaponReady;
			Loop;
		Fire:
			LBRE KLM 2 Bright;
			TNT1 A 0 {
				A_StartSound("weapons/swordslash");
				A_CustomPunch(8*random(1, 10), true, pufftype: "ZBulletPuff", lifesteal: 0.5);
			}
			LBRE NOP 2 Bright;
			LBRE H 1 Bright;
			Goto Ready;
	}
}