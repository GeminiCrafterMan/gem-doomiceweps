class GWM_Mine : Weapon
{
	Default
	{
		Tag "Mine";
		Weapon.AmmoUse 1;
		Weapon.AmmoType "GWM_MineAmmo";
		Weapon.AmmoGive 2;
		Inventory.PickupMessage "$GOTMINES";
		Obituary "$KILLPLACEHOLDER";
		+WEAPON.NOAUTOFIRE
	}
	States
	{
		Spawn:
			MINE Z -1;
			Stop;
		Ready:
			MINE A 1 A_WeaponReady;
			Loop;
		Deselect:
			MINE A 1 A_Lower(12);
			Loop;
		Select:
			MINE A 1 A_Raise(12);
			Loop;
		Fire:
		// Toss a mine!
			MINE B 4 A_FireProjectile("GWM_ThrownMine");
			FIST B 4 A_CheckReload;
			Goto Select;
	}
}
class GWM_ThrownMine : Actor // Okay, so like, I was going to do this myself, but then I saw there was a better mine on the wiki, so fuck it
{
	override bool Used(Actor user)
	{
		// add a sound
		SetState(ResolveState("Disarm"));
		return true;
	}
	// This will the mine's activation radius:
	const DETECTRADIUS = 80;
	Default
	{
		Radius 16;
		Height 4;
		Health 1;
		Speed 24;
		Obituary "$KILLMINES";
		BounceType "Hexen";
		DeathSound "weapons/explode";
		BounceSound "weapons/grndimp";
		Projectile;
		+USEBOUNCESTATE +NOBLOOD +SHOOTABLE -NOGRAVITY -NOBLOCKMAP
	}
	States
	{
		Spawn:
			MINE CD 2;
			Loop;
		Bounce:
			MINE CD 1;
			Loop;
		Bounce.Floor:
			MINE # 0 {
				self.Vel.X = 0;
				self.Vel.Y = 0;
				self.Vel.Z = 0;
				bBOUNCEONFLOORS = false;
				bBOUNCEONWALLS = false;
				bBOUNCEONCEILINGS = false;
				bBOUNCEONACTORS = false;
				bMOVEWITHSECTOR = true;
			}
		Ground:
			MINE # 1
			{
				// Create an iterator to cover the effective radius:
				BlockThingsIterator it = BlockThingsIterator.Create(self, DETECTRADIUS);
				while (it.Next())
				{
					// Get a shorter pointer to the found actor (for convenience):
					let obj = it.thing;
					
					// Check the object is either a monster or a player, isn't the same
					// as the projectile's target (so that the shooter can't trigger it),
					// then checks that it's alive and within distance:
					if ((obj.bISMONSTER || obj.player) && (!target || obj != target) && obj.health > 0 && Distance3D(obj) <= DETECTRADIUS)
					{
						// Go to the Boom state sequence if the check passes:
						return ResolveState("Detected");
					}
				}
				
				// Once a second it'll play a DSTINK sound to emulate beeping:
				if (GetAge() % 35 == 0)
				{
					A_StartSound("weapons/minewait", volume: 0.8, attenuation: 5);
				}
				
				// If the jump didn't happen, proceed to loop:
				return ResolveState(null);
			}
			Loop;
		Detected:
			// Make the mine jump up as a visual cue,
			// then proceed to the explosion sequence:
			MINE # 2 {vel.z += 8; A_StartSound("weapons/minedet");}
			MINE ### 2 A_StartSound("weapons/minedet");
		Bounce.Actor:
		Death:
			EXPL A 1 Bright; // so it won't explode in timestop
			TNT1 A 0 {
				bSPECIAL = false;
				bMISSILE = true;
				bNOGRAVITY = true;
				self.Vel.X = 0;
				self.Vel.Y = 0;
				self.Vel.Z = 0;
				A_Scream();
				A_SetRenderstyle(alpha, STYLE_Add);
				A_Explode();
			}
			EXPL BCDEFGHIJ 1 Bright;
			Stop;
		Disarm:
			TNT1 A 0 A_SpawnItemEx("GWM_MineAmmo");
			Stop;
	}
}