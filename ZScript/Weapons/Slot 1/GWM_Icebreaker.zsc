class GWM_Icebreaker : GWM_WeaponBase // not that it needs to be this way...
{
	Default
	{
		Weapon.SelectionOrder 2200;
		+WEAPON.MELEEWEAPON
		Tag "Icebreaker Drill";
		Inventory.Icon "IBRKZ0";
		Weapon.Kickback 0;
		Obituary "$KILLICEBREAKER";
		Inventory.PickupMessage "$GOTICEBREAKER";
	}
	States
	{
		Spawn:
			IBRK Z -1;
			Stop;
		Select:
			IBRK A 1 A_Raise(12);
			Loop;
		Deselect:
			TNT1 A 0 A_StopSound(CHAN_BODY);
			IBRK A 1 A_Lower(12);
			Loop;
		Ready:
			IBRK A 1 A_WeaponReady;
			Loop;
		Fire:
			IBRK A 2 A_StartSound("weapons/drillup", CHAN_BODY);
			IBRK AABBBBCCCDDDAABBCD 2;
			IBRK A 0 {A_StartSound("weapons/drillloop", CHAN_BODY, CHANF_LOOPING); A_ReFire();}
			IBRK A 0 A_ReFire("Hold");
			Goto Release;
		Hold:
			IBRK A 2 A_Saw("", "weapons/drillhit", 3, "ZBulletPuff");
			IBRK B 2;
			IBRK A 0 A_ReFire("Hold2");
			Goto Release;
		Hold2:
			IBRK C 2 A_Saw("", "weapons/drillhit", 3, "ZBulletPuff");
			IBRK D 2;
			IBRK A 0 A_ReFire("Hold");
			Goto Release;
		Release:
			IBRK # 0 A_StopSound(CHAN_BODY);
			IBRK # 2 A_StartSound("weapons/drilldown", CHAN_BODY);
			IBRK BCDDAABBBCCCDDDDAAA 1;
			Goto Ready;
	}
}