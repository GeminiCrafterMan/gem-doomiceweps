class GWM_AssaultRifle : GWM_WeaponBase
{
	action void A_FireRifle()
	{
		A_SpawnItemEx("GWM_Flash");
		ZWL_FireHitscan(2, 25, tracerType: "GWM_Tracer");
		ZWL_EjectCasing("GWM_RifleCasing");
		A_WeaponOffset(0, 6, WOF_ADD);
		A_SetPitch(pitch-0.25);
		A_Setangle(angle+frandom(-.25,.25));
		A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5);
	}
	Default
	{
		//$Category Weapons
		//$Title "Assault Rifle"
		Weapon.SelectionOrder 700;
		Weapon.SlotNumber 4;
		Weapon.AmmoUse 1;
		Weapon.AmmoType "GWM_FMJMagazine";
		Weapon.AmmoGive 16;
		Weapon.UpSound "weapons/draw_primary";

		ZWeapon.MagazineSize 32;
		ZWeapon.ReloadSound "weapons/riflein";
		ZWeapon.ClickSound "weapons/92fsdryfire";

		Inventory.PickupMessage "$GOTASSAULTRIFLE";

		Tag "$TAG_ASSAULTRIFLE";
		AttackSound "weapons/riflesht";
		Obituary "$KILLASSAULTRIFLE";
		+ALLOWTHRUFLAGS
		+THRUGHOST
	}
	States
	{
		Ready:
			RIFL A 1 ZWL_WeaponReady();
			Loop;
		Select:
			TNT1 A 0 {
				ZWL_QuickRaise();
				A_WeaponOffset(-64, 160);
			}
			RIFL QQQQQQ 1 {
				A_WeaponOffset(8, -16, WOF_ADD);
			}
			RIFL QQ 1 {
				A_WeaponOffset(8, -16, WOF_ADD);
			}
			RIFL TTSS 1 ZWL_WeaponReady();
			RIFL R 4 {A_StartSound("weapons/riflebak"); ZWL_WeaponReady();}
			RIFL SSTT 1 ZWL_WeaponReady();
			RIFL Q 1 {A_StartSound("weapons/riflefwd"); ZWL_WeaponReady();}
			Goto Ready;
		Deselect:
			RIFL AA 1 {
				A_WeaponOffset(-8, 16, WOF_ADD);
			}
			RIFL QQQQQQ 1 {
				A_WeaponOffset(-8, 16, WOF_ADD);
			}
			TNT1 A 0 A_WeaponOffset(0, flags: WOF_KEEPY);
			TNT1 A 0 ZWL_QuickLower;
			Stop;
		Fire:
			TNT1 A 0 ZWL_CheckReload();
			RIFL B 1 Bright A_FireRifle;
			RIFL CA 1 Bright A_WeaponOffset(0, -3, WOF_ADD);
			RIFL A 1;
			RIFL A 4 ZWL_ReFire(null, true);
			Goto Ready;
		Spawn:
			RIFL Z -1;
			Stop;
		Reload:
			TNT1 A 0 {
				if (!invoker.CheckAmmo(PrimaryFire, false, true))
					player.SetPSPrite(OverlayID(), ResolveState("Ready")); // Disallow reloading if there's no more ammo
			}
			RIFL D 4;
			RIFL PO 2;
			RIFL N 4 A_StartSound("weapons/riflebak");
			TNT1 A 0 A_StartSound("weapons/rifleout");
			RIFL EF 2;
			TNT1 A 0 ZWL_EjectCasing("GWM_PistolEmptyMag", true, 45, 2, 16, (24, 0, -24));
			RIFL GHIJK 3;
			RIFL L 4 ZWL_Reload();
			RIFL MD 2;
			RIFL NO 2;
			RIFL P 4 A_StartSound("weapons/riflefwd");
			RIFL D 2;
			Goto Ready;
	}
}