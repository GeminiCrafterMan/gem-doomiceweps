class GWM_Chaingun : Chaingun
{
	/*
	Ejects a bullet casing to the side.
	Params:
	 - casingType: Type of actor to eject.
	 - left: If true, casing ejects to the left. Otherwise it ejects to the right.
	 - ejectPitch: Pitch at which casing ejects, relative to view direction.
	 - speed: Speed at which casing ejects.
	 - accuracy: Random spread, in degrees.
	 - offset: Offset from which casing is ejected, relative to center of view.
	*/
	action void ZWL_EjectCasing(class<Actor> casingType, bool left = false, double ejectPitch = -45, double speed = 4,
								double accuracy = 8, Vector3 offset = (24, 0, -10))
	{
		// Find offset vector
		// +y axis is to the right for offsets
		offset.y *= -1;

		// Rotate offset by pitch
		Vector2 xz = RotateVector((offset.x, offset.z), -pitch);
		offset.x = xz.x;
		offset.z = xz.y;

		// Rotate vector by angle
		offset.xy = RotateVector(offset.xy, angle);

		// Move to player camera
		offset.xy += pos.xy;
		offset.z += player.viewZ;

		// Find velocity vector
		Vector3 side = (Cos(angle + (left ? 90 : -90)), Sin(angle + (left ? 90 : -90)), 0);
		Vector3 up = (Cos(pitch-90) * Cos(angle), Cos(pitch-90) * Sin(angle), -Sin(pitch-90));
		Vector3 baseDirection = Cos(-ejectPitch) * side + Sin(-ejectPitch) * up;

		double baseAngle = VectorAngle(baseDirection.x, baseDirection.y);
		baseDirection.xy = RotateVector(baseDirection.xy, -baseAngle);
		double basePitch = -VectorAngle(baseDirection.x, baseDirection.z);

		double casAngle, casPitch;
		[casAngle, casPitch] = invoker.BulletAngle(accuracy, baseAngle, basePitch);

		let casing = Spawn(casingType, offset);
		casing.Vel3dFromAngle(speed, casAngle, casPitch);
		casing.vel += vel;
	}
	// Returns random angle and pitch within cone
	// I have no idea if there's a better way of doing this ¯\_(ツ)_/¯
	// Params:
	//  - accuracy: maximum angle b/w cone's axis, and bullet trajectory
	//  - angle: angle of axis
	//  - pitch: pitch of axis
	double, double BulletAngle(double accuracy, double angle, double pitch)
	{
		Vector3 v = (0, 0, 0);

		if (accuracy > 10)
		{
			// Generate random vector in sphere section
			Vector3 axis = (Cos(pitch) * Cos(angle), Cos(pitch) * Sin(angle), -Sin(pitch));
			while (v == (0, 0, 0) || v.Length() > 1 || ACos(axis dot v.Unit()) > accuracy)
			{
				v = (FRandom(-1, 1), FRandom(-1, 1), FRandom(-1, 1));
			}

			// Extract angle and pitch from trajectory
			angle = VectorAngle(v.x, v.y);
			pitch = -ASin(v.z / v.Length());
		}
		else if (accuracy > 0)
		{
			// Generate random vector in sphere around end of axis
			double r = Sin(accuracy);
			while (v == (0, 0, 0) || v.Length() > r)
			{
				v = (FRandom(-r, r), FRandom(-r, r), FRandom(-r, r));
			}

			Vector3 axis = (Cos(pitch) * Cos(angle), Cos(pitch) * Sin(angle), -Sin(pitch));
			v += axis;

			// Extract angle and pitch from trajectory
			angle = VectorAngle(v.x, v.y);
			pitch = -ASin(v.z / v.Length());
		}

		return angle, pitch;
	}
	action void A_FireCGun() {
		A_SpawnItemEx("GWM_Flash");
		A_StartSound("weapons/chngun", CHAN_WEAPON);
		A_FireBullets(11.2, 0, 1, 5, "ZBulletPuff", missile: "GWM_Tracer", spawnheight: 0);
		ZWL_EjectCasing("GWM_PistolCasing");
	}
	Default
	{
		+THRUGHOST
	}
	States
	{
		Deselect:
			TNT1 A 0 A_StopSound(CHAN_BODY);
			CHGG A 1 A_Lower(12);
			Loop;
		Select:
			CHGG A 1 A_Raise(12);
			Loop;
		Fire:
			// rev the chaingun
			CHGG A 1 A_StartSound("weapons/cgunup", CHAN_BODY);
			CHGG AABBBBCCCDDDEEFFGH 1;
			CHGG A 0 A_StartSound("weapons/cgunloop", CHAN_BODY, CHANF_LOOPING);
		Hold:
			CHGG A 1 {A_FireCGun(); A_Overlay(PSP_FLASH, "FlashAB");}
			CHGG B 1;
			CHGG C 1 {A_FireCGun(); A_Overlay(PSP_FLASH, "FlashCD");}
			CHGG D 1;
			CHGG D 0 A_ReFire("Hold2");
			Goto Unrev;
		Hold2:
			CHGG E 1 {A_FireCGun(); A_Overlay(PSP_FLASH, "FlashEF");}
			CHGG F 1;
			CHGG G 1 {A_FireCGun(); A_Overlay(PSP_FLASH, "FlashGH");}
			CHGG H 1;
			CHGG H 0 A_ReFire;
			Goto Unrev;
		Unrev:
			CHGG H 1 A_StartSound("weapons/cgundn", CHAN_BODY);
			CHGG GFFEEDDDCCCBBBBAAA 1;
			Goto Ready;
		FlashAB:
			CHGF A 1 Bright A_Light1;
			CHGF B 1 Bright A_Light1;
			Stop;
		FlashCD:
			CHGF C 1 Bright A_Light1;
			CHGF D 1 Bright A_Light1;
			Stop;
		FlashEF:
			CHGF E 1 Bright A_Light1;
			CHGF F 1 Bright A_Light1;
			Stop;
		FlashGH:
			CHGF G 1 Bright A_Light1;
			CHGF H 1 Bright A_Light1;
			Stop;
		Spawn:
			CHGG Z -1;
			Stop;
	}
}