class GWM_Chaingun : GWM_WeaponBase
{
	action void A_FireCGun()
	{
		A_SpawnItemEx("GWM_Flash");
		A_StartSound("weapons/chainfire", CHAN_WEAPON);
		A_FireBullets(11.2, 0, 1, 7*Random(2,3), "ZBulletPuff", FBF_USEAMMO|FBF_NORANDOM, missile: "GWM_Tracer", spawnheight: 0);
		ZWL_EjectCasing("GWM_RifleCasing", true, -23, 8, 16, (24, 0, -16));
		A_SetPitch(pitch-0.5);
	}
	Default
	{
		Weapon.SelectionOrder 800; // Has a wind-up, so +100
		Weapon.UpSound "weapons/draw_chaingun";
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 20;
		Weapon.AmmoType "GWM_HighCaliberClip";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Obituary "$KILLCHAINGUN";
		Tag "$TAG_CHAINGUN";
	}
	States
	{
		Ready:
			CHGG A 1 A_WeaponReady;
			Loop;
		Deselect:
			TNT1 A 0 A_StopSound(CHAN_BODY);
			CHGG A 1 A_Lower(12);
			Loop;
		Select:
			CHGG A 1 A_Raise(12);
			Loop;
		Fire:
			// rev the chaingun
			CHGG A 1 A_StartSound("weapons/cgunup", CHAN_BODY);
			CHGG AABBBBCCCDDDEEFFGH 1;
			CHGG A 0 A_StartSound("weapons/cgunloop", CHAN_BODY, CHANF_LOOPING);
			CHGG A 0 A_ReFire("Hold");
			Goto Release;
		Hold:
			CHGG A 1 {A_FireCGun(); A_Overlay(PSP_FLASH, "FlashAB");}
			CHGG B 1 {
				return ZWL_JumpIfEmpty("Release");
			}
			CHGG C 1 {A_FireCGun(); A_Overlay(PSP_FLASH, "FlashCD");}
			CHGG D 1 {
				return ZWL_JumpIfEmpty("Release");
			}
			CHGG D 0 A_ReFire("Hold2");
			Goto Release;
		Hold2:
			CHGG E 1 {A_FireCGun(); A_Overlay(PSP_FLASH, "FlashEF");}
			CHGG F 1 {
				return ZWL_JumpIfEmpty("Release");
			}
			CHGG G 1 {A_FireCGun(); A_Overlay(PSP_FLASH, "FlashGH");}
			CHGG H 1 {
				ZWL_EjectCasing("GWM_ChaingunLink", true, -23, 8, 16, (24, 0, -16));
				return ZWL_JumpIfEmpty("Release");
			}
			CHGG H 0 A_ReFire;
			Goto Release;
		Release:
			CHGG H 1 A_StartSound("weapons/cgundn", CHAN_BODY);
			CHGG GFFEEDDDCCCBBBBAAA 1;
			TNT1 A 0 A_CheckReload;
			Goto Ready;
		FlashAB:
			CHGF A 1 Bright A_Light1;
			CHGF B 1 Bright A_Light1;
			Stop;
		FlashCD:
			CHGF C 1 Bright A_Light1;
			CHGF D 1 Bright A_Light1;
			Stop;
		FlashEF:
			CHGF E 1 Bright A_Light1;
			CHGF F 1 Bright A_Light1;
			Stop;
		FlashGH:
			CHGF G 1 Bright A_Light1;
			CHGF H 1 Bright A_Light1;
			Stop;
		Spawn:
			CHGG Z -1;
			Stop;
	}
}