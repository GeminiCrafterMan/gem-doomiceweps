class GWM_Chaingun : GWM_WeaponBase
{
	action void A_FireCGun()
	{
		A_SpawnItemEx("GWM_Flash");
		ZWL_FireHitscan(0, 8, pellets: 3, spread: 8, tracerType: "GWM_Tracer");
		ZWL_EjectCasing("GWM_RifleCasing", true, -23, 8, 16, (24, 0, -16));
		A_ChangeVelocity(-Cos(pitch)*0.1, 0, sin(pitch)*0.1,CVF_RELATIVE);
		A_SetPitch(pitch+frandom(-.5,.5));
		A_Setangle(angle+frandom(-.5,.5));
	}
	Default
	{
		//$Category Weapons
		//$Title ""Mad Vulcan" Chaingun
		Weapon.SelectionOrder 800; // Has a wind-up, so +100
		Weapon.UpSound "weapons/draw_chaingun";
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 20;
		Weapon.AmmoType "GWM_HighCaliberMagazine";
		AttackSound "weapons/chainfire";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Obituary "$KILLCHAINGUN";
		Tag "$TAG_CHAINGUN";
	}
	States
	{
		Ready:
			CHGG A 1 A_WeaponReady;
			Loop;
		Deselect:
			TNT1 A 0 A_StopSound(CHAN_BODY);
			CHGG A 1 A_Lower(18);
			Loop;
		Select:
			CHGG A 1 A_Raise(18);
			Loop;
		Fire:
			// rev the chaingun
			CHGG A 1 A_StartSound("weapons/cgunup", CHAN_BODY);
			CHGG AABBBBCCCDDEEFGHACEG 1;
			CHGG A 0 A_StartSound("weapons/cgunloop", CHAN_BODY, CHANF_LOOPING);
			CHGG A 0 A_ReFire("Hold");
			Goto Release;
		Hold:
			CHGG A 1 {A_FireCGun(); A_Overlay(PSP_FLASH, "FlashAE");A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5);}
			CHGG E 1 {
				A_FireCGun();
				return ZWL_JumpIfEmpty("Release");
			}
			CHGG B 1 {A_FireCGun(); A_Overlay(PSP_FLASH, "FlashBF"); A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5);}
			CHGG F 1 {
				A_FireCGun();
				return ZWL_JumpIfEmpty("Release");
			}
			CHGG E 0 A_ReFire("Hold2");
			Goto Release;
		Hold2:
			CHGG C 1 {A_FireCGun(); A_Overlay(PSP_FLASH, "FlashCG"); A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5);}
			CHGG G 1 {
				A_FireCGun();
				return ZWL_JumpIfEmpty("Release");
			}
			CHGG D 1 {A_FireCGun(); A_Overlay(PSP_FLASH, "FlashDH"); A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5);}
			CHGG H 1 {
				A_FireCGun();
				ZWL_EjectCasing("GWM_ChaingunLink", true, -23, 8, 16, (24, 0, -16));
				return ZWL_JumpIfEmpty("Release");
			}
			CHGG H 0 A_ReFire;
			Goto Release;
		Release:
			CHGG H 1 A_StartSound("weapons/cgundn", CHAN_BODY);
			CHGG ABCDEEFFFGGGGHHHHH 1;
			//CHGG GFFEEDDDCCCBBBBAAA 1;
			TNT1 A 0 A_CheckReload;
			Goto Ready;
		FlashAE:
			CHGF A 1 Bright A_Light1;
			CHGF E 1 Bright A_Light1;
			Stop;
		FlashBF:
			CHGF B 1 Bright A_Light1;
			CHGF F 1 Bright A_Light1;
			Stop;
		FlashCG:
			CHGF C 1 Bright A_Light1;
			CHGF G 1 Bright A_Light1;
			Stop;
		FlashDH:
			CHGF D 1 Bright A_Light1;
			CHGF H 1 Bright A_Light1;
			Stop;
		Spawn:
			CHGG Z -1;
			Stop;
	}
}