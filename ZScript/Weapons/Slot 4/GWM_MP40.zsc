// I know. ANOTHER bullet-based weapon. However, this one DID exist before, and there's better sprites now, so I'll add it back.
class GWM_MP40 : GWM_WeaponBase
{
	Default
	{
		Weapon.SelectionOrder 1800;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 32;
		Weapon.AmmoType "GWM_LowCaliberMagazine";

		ZWeapon.MagazineSize 32;
		ZWeapon.ReloadSound "weapons/smgclipin";
		ZWeapon.ClickSound "weapons/92fsdryfire";

		+WEAPON.WIMPY_WEAPON
		Weapon.UpSound "weapons/draw_secondary";
		AttackSound "weapons/mp40fire";
		Obituary "$KILLMP40";
		Inventory.PickupMessage "$GOTMP40";
		Tag "$TAG_MP40";
	}
	States
	{
		Ready:
			MP4G A 1 ZWL_WeaponReady; // find a way to make this not have 33 rounds. maybe just reduce the mag size to 31 to compensate for zwl's wacky ass
			Loop;
		Select:
			TNT1 A 0 {
				ZWL_QuickRaise();
				A_WeaponOffset(-64, 160);
			}
			MP4G FFFFFF 1 {
				A_WeaponOffset(8, -16, WOF_ADD);
			}
			MP4G AA 1 {
				A_WeaponOffset(8, -16, WOF_ADD);
			}
			Goto Ready;
		Deselect:
			MP4G AA 1 {
				A_WeaponOffset(-8, 16, WOF_ADD);
			}
			MP4G FFFFFF 1 {
				A_WeaponOffset(-8, 16, WOF_ADD);
			}
			TNT1 A 0 A_WeaponOffset(0, flags: WOF_KEEPY);
			TNT1 A 0 ZWL_QuickLower;
			Stop;
/*		Deselect:
			MP4G A 1 A_Lower(18);
			Loop;
		Select:
			MP4G A 1 A_Raise(18);
			Loop;*/
		Fire:
		// Did my best to replicate 500RPM, since the "fire rate" part of ZWL_FireHitscan is... unreliable, at best
			MP4G A 2 {
				ZWL_FireHitscan(3, 14, tracerType: "GWM_Tracer");
//				A_FireBullets(7.6, 0, 1, 4, "ZBulletPuff", missile: "GWM_Tracer", spawnheight: 0);
				A_SpawnItemEx("GWM_Flash");
				A_SpawnItemEx("GWM_MuzzleFlashLight",0,0,5);
				A_Overlay(-2, "Flash");
				A_Setpitch(pitch-0.5);
				A_WeaponOffset(0, 4, WOF_ADD);
				ZWL_EjectCasing("GWM_PistolCasing", left: true, offset: (24, 0, -10));
			}
			MP4G B 1 {A_WeaponOffset(0, -1, WOF_ADD); A_Setpitch(pitch+0.25);}
			MP4G B 1 {A_WeaponOffset(0, -3, WOF_ADD); A_Setpitch(pitch+0.25);}
			MP4G A 0 { return ZWL_JumpIfEmpty("Fire.Empty"); }
			MP4G A 1 ZWL_ReFire(null, true);
			Goto Ready;
		Fire.Empty:
			TNT1 A 0 ZWL_CheckReload();
			Goto Reload;
		Flash:
			MP4F A 1 Bright A_Light1;
			MP4F B 1 Bright A_Light2;
			Goto LightDone;
		Reload:
			MP4G CCDD 2;
			MP4G E 2 {
				A_StartSound("weapons/smgclipout");
				ZWL_EjectCasing("GWM_PistolEmptyMag", true, 45, 2, 16, (24, 0, -24));
			}
			MP4G EEEE 2;
			// todo: add reload animation
			MP4G D 2 ZWL_Reload;
			MP4G DCA 2;
			Goto Ready;
		Spawn:
			MP4G Z -1;
			Loop;
	}
}