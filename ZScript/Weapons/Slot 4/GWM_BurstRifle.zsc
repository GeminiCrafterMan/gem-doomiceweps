class GWM_BurstRifle : GWM_WeaponBase
{
	action void A_FireRifle()
	{
		A_SpawnItemEx("GWM_Flash");
		ZWL_FireHitscan(1, 12, tracerType: "GWM_Tracer");
		ZWL_EjectCasing("GWM_RifleCasing");
		A_WeaponOffset(0, 6, WOF_ADD);
	}
	Default
	{
		Weapon.SelectionOrder 700;
		Weapon.AmmoUse 1;
		Weapon.AmmoType "GWM_HighCaliberClip";
		Weapon.AmmoGive 31;
		Weapon.UpSound "weapons/draw_primary";

		ZWeapon.MagazineSize 30;
		ZWeapon.ReloadSound "weapons/riflein";
		ZWeapon.ClickSound "weapons/92fsdryfire";

		Inventory.PickupMessage "$GOTBURSTRIFLE";

		Tag "$TAG_BURSTRIFLE";
		AttackSound "weapons/riflesht";
		Obituary "$KILLBURSTRIFLE";
	}
	States
	{
		Ready:
			RIFL A 1 ZWL_WeaponReady(ZRF_ExtraRound);
			Loop;
		Deselect:
			RIFL A 1 A_Lower(18);
			Loop;
		Select:
			RIFL A 1 A_Raise(18);
			Loop;
		Fire:
			TNT1 A 0 ZWL_CheckReload();
			RIFL B 1 Bright A_FireRifle;
			RIFL CA 1 A_WeaponOffset(0, -3, WOF_ADD);
			TNT1 A 0 ZWL_CheckReload();
			RIFL B 1 Bright A_FireRifle;
			RIFL CA 1 A_WeaponOffset(0, -3, WOF_ADD);
			TNT1 A 0 ZWL_CheckReload();
			RIFL B 1 Bright A_FireRifle;
			RIFL CA 1 A_WeaponOffset(0, -3, WOF_ADD);
			RIFL A 9;
			RIFL A 0 ZWL_ReFire(null, true);
			Goto Ready;
		Spawn:
			RIFL Z -1;
			Stop;
		Reload:
			TNT1 A 0 {
				if (!invoker.CheckAmmo(PrimaryFire, false, true))
					player.SetPSPrite(OverlayID(), ResolveState("Ready")); // Disallow reloading if there's no more ammo
			}
			TNT1 A 0 ZWL_JumpIfEmpty("Reload.Empty");
			RIFL D 4;
			TNT1 A 0 A_StartSound("weapons/rifleout");
			RIFL EF 2;
			TNT1 A 0 ZWL_EjectCasing("GWM_PistolEmptyMag", true, 45, 2, 16, (24, 0, -24));
			RIFL GHIJK 3;
			RIFL L 4 ZWL_Reload(-1);
			RIFL MD 2;
			Goto Ready;
		Reload.Empty:
			TNT1 A 0 {
				if (!invoker.CheckAmmo(PrimaryFire, false, true))
					player.SetPSPrite(OverlayID(), ResolveState("Ready")); // Disallow reloading if there's no more ammo
			}
			RIFL D 4;
			TNT1 A 0 A_StartSound("weapons/rifleout");
			RIFL EF 2;
			TNT1 A 0 ZWL_EjectCasing("GWM_PistolEmptyMag", true, 45, 2, 16, (24, 0, -24));
			RIFL GHIJK 3;
			RIFL L 4 ZWL_Reload;
			RIFL MD 2;
			Goto Ready;
	}
}