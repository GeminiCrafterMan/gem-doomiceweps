class GWM_Scrapgun : GWM_WeaponBase
{
	Default
	{
		//$Category Weapons
		Weapon.SlotNumber 4;
		Tag "$TAG_SCRAPGUN";
		Decal "bulletchip";
		Weapon.KickBack 150;
		Weapon.AmmoUse 1;
		Weapon.AmmoType "GWM_ScrapDrum";
		Weapon.AmmoGive 8;
		Inventory.PickupSound "weapons/scrappickup";
		Weapon.UpSound "weapons/scrappickup";
		Inventory.PickupMessage "$GOTSCRAPGUN";
		DamageType "Shotgun";

		ZWeapon.MagazineSize 4;
		ZWeapon.ReloadSound "weapons/scrapload";
		ZWeapon.ClickSound "weapons/92fsdryfire";
		AttackSound "weapons/scrapfire";
	}
	States
	{
		Spawn:
			SCRA Z -1;
			Stop;
		Ready:
			SCRA A 1 ZWL_WeaponReady;
			Loop;
		Deselect:
			SCRA A 1 A_Lower(18);
			Loop;
		Select:
			SCRA A 1 A_Raise(18);
			Loop;
			
		Recoil:
			TNT1 AAA 1 A_SetPitch(pitch+1.5);
			TNT1 AAAAAA 1 A_SetPitch(pitch-0.75);
			Stop;
			
		Fire:
			TNT1 A 0 ZWL_CheckReload();
			SCRA A 1 Bright {
				ZWL_FireProjectile("GWM_ScrapSpawner", 0, offset: (0, 0, 8));
				A_GunFlash();
				A_Light1();
				Radius_Quake(6, 3, 0, 1, 0);
				A_SetPitch(pitch - 1.0);
				A_Overlay(55,"Recoil");
				A_ChangeVelocity(-Cos(pitch)*5, 0, sin(pitch)*5,CVF_RELATIVE);
				
			}
			SCRA B 1 Bright A_Light2;
			SCRA C 1 {
				A_Light0();
				A_SetPitch(pitch + 0.5);
			}
			SCRA D 1 A_SetPitch(pitch + 0.5);
			SCRA EFF 1;
			SCRA I 2 A_PlaySound("weapons/scraprotate", CHAN_AUTO);
			SCRA GGHHH 1;
			TNT1 A 0 ZWL_CheckReload();
			Goto Ready;
		Reload:
			TNT1 A 0 {
				if (!invoker.CheckAmmo(PrimaryFire, false, true))
					player.SetPSPrite(OverlayID(), ResolveState("Ready")); // Disallow reloading if there's no more ammo
			}
			SRLA ABCFE 1;
			SRLA D 1 A_PlaySound("weapons/scrapopen", CHAN_AUTO);
			SRLB ABCDE 2;
			SRLB F 1 A_PlaySound("weapons/scrapeject", CHAN_WEAPON);
			SRLB GHI 1;
			SRLB J 7;
			SRLB KLM 2;
			SRLB NO 1;
			SRLB P 3 ZWL_Reload;
			SRLB N 2 A_PlaySound("weapons/scrapclose", CHAN_AUTO);
			SRLB QR 2;
			SRLB ST 1;
			SRLA DB 2;
			SCRA A 1;
			SCRA HA 3;
			Goto Ready;
	}
}

class GWM_Scrap : FastProjectile
{
	Default
	{
		// Projectile
		DamageFunction (8);
		Decal "ScrapChip";
		Speed 60;
		Height 4;
		Radius 2;
		+BLOODSPLATTER
		+THRUGHOST
		Scale 0.25;
		DamageType "Shotgun";
	}
	States
	{
		Spawn:
			SHRD AABBCCDDEEFFGGHH 1 NoDelay A_SpawnItemEx("GWM_ScrapTrail", 0, 0, 0, 0, 0, 0);
			Loop;
		Death:
			TNT1 A 0 A_SpawnItemEx("ZBulletPuff", 0, 0, 0, 0, 0, 0);
			Stop;
		XDeath:
			TNT1 A 0;
			Stop;
	}
}

class GWM_ScrapTrail : Actor
{
	Default
	{
		Radius 1;
		Height 1;
		Speed 0;
		Alpha 0.6;
		Scale 0.25;
		+NOINTERACTION
		+CLIENTSIDEONLY
		+RANDOMIZE
	}
    States
    {
		Spawn:
			SCTR KLMNOPQRSTUVWX 1 Bright;
			Stop;
    }
}

class GWM_ScrapSpawner : Actor // Not sure if I can really replace this...
{
	const scrap_spread = 6.2;
	const scrap_base_vel = 60;
	Default
	{
		Projectile;
		MissileHeight 0;
		Radius 1;
		Height 2;
		Speed 1;
		Decal "ScrapChip";
		+BLOODSPLATTER
		+NOEXTREMEDEATH
	}
	States
	{
		XDeath:
			TNT1 A 0;
			Stop;
		Spawn:
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 NoDelay A_SpawnItemEx("GWM_Scrap", 0, 0, 0, (Vel.X * scrap_base_vel) + frandom(-scrap_spread, scrap_spread), (Vel.Y * scrap_base_vel) + frandom(-scrap_spread, scrap_spread), (Vel.Z * scrap_base_vel) + frandom(-scrap_spread, scrap_spread), 0, SXF_ABSOLUTEMOMENTUM | SXF_TRANSFERPOINTERS);
			Stop;
		Death:
			Stop;
	}
}