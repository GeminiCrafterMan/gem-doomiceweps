class GWM_BurningMinigun : GWM_WeaponBase
{
	action void A_FireBurnMini()
	{
		A_SpawnItemEx("GWM_Flash");
		ZWL_FireHitscan(0, 9, pellets: 6, spread: 14, tracerType: "GWM_Tracer");
		ZWL_EjectCasing("GWM_RifleCasing", true, -23, 8, 16, (24, 0, -16));
		A_ChangeVelocity(-Cos(pitch)*0.1, 0, sin(pitch)*0.1,CVF_RELATIVE);
		A_SetPitch(pitch+frandom(-.5,.5));
		A_Setangle(angle+frandom(-.5,.5));
	}
	Default
	{
		//$Category Weapons
		//$Title "Burning Minigun
		Weapon.SlotNumber 4;
		Weapon.SelectionOrder 800; // Has a wind-up, so +100
		Weapon.UpSound "weapons/draw_vulcan";
		Weapon.AmmoType1 "GWM_FMJMagazine";
		Weapon.AmmoUse1 1;
		Weapon.AmmoGive1 20;
		Weapon.AmmoType2 "GWM_GasCanister";
		Weapon.AmmoUse2 4;
		Weapon.AmmoGive2 4;
		AttackSound "weapons/vulcanfire";
		Inventory.PickupMessage "$GOTBURNMINI";
		Obituary "$KILLBURNMINI";
		Tag "$TAG_BURNMINI";
		
		GWM_WeaponBase.BaseVersion "GWM_MadVulcan";
		GWM_WeaponBase.BaseVersion2 "GWM_Minigun";
	}
	States
	{
		Ready:
			BRNG A 1 A_WeaponReady;
			Loop;
		Deselect:
			TNT1 A 0 A_StopSound(CHAN_BODY);
			BRNG A 1 A_Lower(18);
			Loop;
		Select:
			BRNG A 1 A_Raise(18);
			Loop;
		Fire:
			// rev the chaingun
			BRNG A 1 A_StartSound("weapons/vulcanup", CHAN_BODY);
			BRNG AABBBBCCCDDAABCDACAC 1;
			BRNG A 0 A_StartSound("weapons/vulcanloop", CHAN_BODY, CHANF_LOOPING);
			BRNG A 0 A_ReFire("Hold");
			Goto Release;
		Hold:
			BRNG E 1 {A_FireBurnMini(); A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5);}
			BRNG F 1 {
				A_FireBurnMini();
				return ZWL_JumpIfEmpty("Release");
			}
			BRNG G 1 {A_FireBurnMini(); A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5);}
			BRNG H 1 {
				A_FireBurnMini();
				return ZWL_JumpIfEmpty("Release");
			}
			BRNG E 0 A_ReFire;
			Goto Release;
		Release:
			BRNG D 1 A_StartSound("weapons/vulcandn", CHAN_BODY);
			BRNG ABCDAABBBCCCCDDDDD 1;
			TNT1 A 0 A_CheckReload;
			Goto Ready;
		Spawn:
			BRNG Z -1;
			Stop;
	}
}