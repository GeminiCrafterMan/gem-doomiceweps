Class GWM_FrostFang : GWM_WeaponBase replaces Blaster
{
	Default
	{
		Weapon.SelectionOrder 700;
		Weapon.UpSound "weapons/draw_primary";
		Tag "$TAG_FROSTFANG";
		Inventory.PickupMessage "$GOTFROSTFANG";
		Weapon.AmmoType "EverIce";
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 20;
		Weapon.SisterWeapon "GWM_FrostFangPowered";
	}
	States
	{
		Ready:
			FSTF A 0 A_Jump(1, "Glow"); //Just a decorative glow
			FSTF A 1 A_WeaponReady;
			Loop;
		Glow:
			FSTF BBBBBBBBBB 1 A_WeaponReady;
			FSTF BBBBBBBBB 1 A_WeaponReady;
			FSTF BBBBBBBB 1 A_WeaponReady;
			Goto Ready;
		Select:
			FSTF A 1 A_Raise(18);
			Loop;
		Deselect:
			FSTF A 1 A_Lower(18);
			Loop;
		Fire:
			FSTF A 3 Bright;
			FSTF B 4 Bright;
			FSTF C 5 Bright;
		Hold:
			FSTF D 2 Bright
			{
				A_PlaySound("FrostFang/Fire1", CHAN_WEAPON);
				A_SpawnItemEx("IceFog2", 16, 0, 32, 0.5, 0, FRandom(-0.5, 0.5), Random(-50, 50), SXF_CLIENTSIDE);
				A_SpawnItemEx("IceFog2", 16, 0, 32, 0.5, 0, FRandom(-0.5, 0.5), Random(-50, 50), SXF_CLIENTSIDE);
				A_SpawnItemEx("IceFog2", 16, 0, 32, 0.5, 0, FRandom(-0.5, 0.5), Random(-50, 50), SXF_CLIENTSIDE);
				A_SpawnItemEx("IceFog2", 16, 0, 32, 0.5, 0, FRandom(-0.5, 0.5), Random(-50, 50), SXF_CLIENTSIDE);
				A_FireProjectile("GWM_FrostFangShard", 0, 1, Random(-5, 5), Random(-5, 5));
			}
			FSTF C 3 Bright A_Jump(12, 3); //Slight rare delay, for flavor
			FSTF B 5 Bright A_ReFire;
			FSTF A 4 Bright;
			Goto Ready;
			FSTF C 12 Bright;
			FSTF B 5 Bright A_ReFire;
			FSTF A 4 Bright;
			Goto Ready;
		Spawn:
			FSTF Z -1;
			Loop;
	 }
}

Class GWM_FrostFangPowered : GWM_FrostFang
{
	Default
	{
		Weapon.AmmoUse 8;
		Weapon.SisterWeapon "GWM_FrostFang";
		+Weapon.Powered_Up
	}

	States
	{
		Fire:
			FSTF A 3 Bright;
			FSTF B 4 Bright;
		Hold:
			FSTF C 5 Bright;
			FSTF D 6 Bright
			{
				A_SpawnItemEx("IceFog2", 16, 0, 32, 0.5, 0, FRandom(-0.5, 0.5), Random(-50, 50), SXF_CLIENTSIDE);
				A_SpawnItemEx("IceFog2", 16, 0, 32, 0.5, 0, FRandom(-0.5, 0.5), Random(-50, 50), SXF_CLIENTSIDE);
				A_SpawnItemEx("IceFog2", 16, 0, 32, 0.5, 0, FRandom(-0.5, 0.5), Random(-50, 50), SXF_CLIENTSIDE);
				A_SpawnItemEx("IceFog2", 16, 0, 32, 0.5, 0, FRandom(-0.5, 0.5), Random(-50, 50), SXF_CLIENTSIDE);
				A_PlaySound("FrostFang/Fire2", CHAN_WEAPON);
				A_FireProjectile("GWM_FrostFangBall");
			}
			FSTF C 6 Bright;
			FSTF B 5 Bright A_ReFire;
			FSTF A 4 Bright;
			Goto Ready;
	 }
}

Class GWM_FrostFangShard : Actor
{
	Default
	{
		Speed 40;
		Radius 6;
		Height 6;
		DamageFunction (16);
		DamageType "Ice";
		DeathSound "FrostFang/Hit1";
		Scale 0.75;
		RenderStyle "Translucent";
		Alpha 0.8;
		Projectile;
		Obituary "$KILLFROSTFANG";
	}

	States
	{
		Spawn:
			FFSD AAABBBCCC 1 Bright A_SpawnItemEx("IceFog", -(Vel.X/1.4), -(Vel.Y/1.4), (Vel.Z/1.4*-1), (Vel.X/1.4), (Vel.Y/1.4), (Vel.Z/1.4), -Angle, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEVELOCITY | SXF_CLIENTSIDE);
			Loop;
		Death:
			FFSD DEFGH 5 Bright;
			Stop;
	}
}

Class GWM_FrostFangBall : Actor
{
	Default
	{
		Radius 10;
		Height 16;
		DamageFunction (30);
		Speed 20;
		RenderStyle "Add";
		Alpha 1.0;
		Scale 2.0;
		DamageType "Ice";
		DeathSound "FrostFang/Hit2";
		Projectile;
		Obituary "$KILLFROSTFANG_POWERED";
	}

	states
	{
		Spawn:
			TNT1 A 1
			{
				A_SpawnItemEx("IceFog2", 0, 0, 0, Random(1, 2), 0, Random(-1, 1), Random(-30, 30), SXF_CLIENTSIDE);
				A_SpawnItemEx("GWM_FrostFangBallParticle", 0, 0, 0, Random(3, 4), 0, FRandom(-0.5, 0.5), Random(-15, 15), SXF_CLIENTSIDE);
			}
			Loop;
		Death:
			FFBL C 0 Bright A_Explode(60, 128, 0);
			FFBL CDEFG 4 Bright;
			Stop;
	}
}

Class GWM_FrostFangBallParticle : Actor
{
	Default
	{
		Radius 1;
		Height 1;
		RenderStyle "Add";
		Alpha 0.8;
		Scale 0.6;
		+NoGravity
		+NoClip
	}

	States
	{
		Spawn:
			IFOG A 4 A_FadeOut(0.2);
			Loop;
	}
}