class GWM_Basilisk : GWM_WeaponBase
{
	bool raging;
	action void A_FireBasilisk()
	{
		A_SpawnItemEx("GWM_Flash");
		ZWL_FireHitscan(0, 9, pellets: 6, spread: 14, tracerType: "GWM_Tracer");
		ZWL_EjectCasing("GWM_RifleCasing", true, -23, 8, 16, (24, 0, -16));
		A_ChangeVelocity(-Cos(pitch)*0.1, 0, sin(pitch)*0.1,CVF_RELATIVE);
		A_SetPitch(pitch+frandom(-.5,.5));
		A_Setangle(angle+frandom(-.5,.5));
	}
	action void A_FireRagingBasilisk()
	{
		A_SpawnItemEx("GWM_MuzzleFlashLight3");
		ZWL_FireHitscan(0, 9, pellets: 9, spread: 18, tracerType: "GWM_Tracer");
		ZWL_EjectCasing("GWM_RifleCasing", true, -23, 8, 16, (24, 0, -16));
		ZWL_EjectCasing("GWM_RifleCasing", true, -23, 8, 16, (24, 0, -16));
		ZWL_EjectCasing("GWM_RifleCasing", true, -23, 8, 16, (24, 0, -16));
		A_ChangeVelocity(-Cos(pitch)*0.175, 0, sin(pitch)*0.175,CVF_RELATIVE);
		A_SetPitch(pitch+frandom(-.7,.7));
		A_Setangle(angle+frandom(-.7,.7));
	}
	Default
	{
		//$Category Weapons
		//$Title "Basilisk
		Weapon.SlotNumber 4;
		Weapon.SelectionOrder 800; // Has a wind-up, so +100
		Weapon.UpSound "weapons/draw_vulcan";
		Weapon.AmmoType "GWM_FMJMagazine";
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 20;
		AttackSound "weapons/vulcanfire";
		Inventory.PickupMessage "$GOTBASILISK";
		Obituary "$KILLBASILISK";
		Tag "$TAG_BASILISK";
		
		GWM_WeaponBase.BaseVersion "GWM_MadVulcan";
		GWM_WeaponBase.BaseVersion2 "GWM_Minigun";
	}
	States
	{
		Ready:
			HBAG A 0 A_Jumpif(invoker.raging == true, "ReadyRage");
			HBAG A 1 A_WeaponReady;
			Loop;
		ReadyRage:
			HBAG ABCD 1 A_WeaponReady;
			HBAG A 0
			{
			 GWM_Player(player.mo).VulcanRage = GWM_Player(player.mo).VulcanRage-1;
				if (GWM_Player(player.mo).VulcanRage < 1)
				{
				 invoker.raging = false;
				}
			}
			HBAG A 0 A_Jumpif(invoker.raging == true, "ReadyRage");
			Goto Release;
		Deselect:
			TNT1 A 0 A_StopSound(CHAN_BODY);
			HBAG A 1 A_Lower(18);
			Loop;
		Select:
			HBAG A 1 A_Raise(18);
			Loop;
		Altfire:
			//rage mode baby
			HBAG A 1 A_Jumpif(GWM_Player(player.mo).VulcanRage < 50, "Ready");
			HBAG A 0 A_StartSound("weapons/vulcanup", CHAN_BODY);
			HBAG AAABBBCCD 1;
			HBAG A 0 A_StartSound("weapons/vulcanloop", CHAN_BODY, CHANF_LOOPING);
			HBAG A 0 { invoker.raging = true; }
			goto Ready;
		Fire:
			// rev the chaingun
			HBAG A 0 A_Jumpif(invoker.raging == true, "FireRage");
			HBAG A 1 A_StartSound("weapons/vulcanup", CHAN_BODY);
			HBAG AABBBBCCCDDAABCDACAC 1;
			HBAG A 0 A_StartSound("weapons/vulcanloop", CHAN_BODY, CHANF_LOOPING);
			HBAG A 0 A_ReFire("Hold");
			Goto Release;
		Hold:
			HBAG A 0 A_Jumpif(invoker.raging == true, "FireRage");
			TNT1 A 0
				{
					++ GWM_Player(player.mo).VulcanInc;
					if(GWM_Player(player.mo).VulcanInc > 12)
					{
						++ GWM_Player(player.mo).VulcanRage;
						GWM_Player(player.mo).VulcanInc == 0;
					}
				}
			HBAG E 1 {A_FireBasilisk(); A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5);}
			HBAG F 1 {
				A_FireBasilisk();
				return ZWL_JumpIfEmpty("Release");
			}
			HBAG G 1 {A_FireBasilisk(); A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5);}
			HBAG H 1 {
				A_FireBasilisk();
				return ZWL_JumpIfEmpty("Release");
			}
			HBAG E 0 A_ReFire;
			Goto Release;
		Release:
			HBAG L 1 A_StartSound("weapons/vulcandn", CHAN_BODY);
			HBAG IJKLMMNNNOOOOPPPDD 1;
			TNT1 A 0 A_CheckReload;
			Goto Ready;
		FireRage:
			HBAG EG 1 {
				A_FireRagingBasilisk();
				A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5);
				return ZWL_JumpIfEmpty("Release");
			}
			HBAG E 0
			{
			 GWM_Player(player.mo).VulcanRage = GWM_Player(player.mo).VulcanRage-1;
			 if (GWM_Player(player.mo).VulcanRage < 1)
				{
				 invoker.raging = false;
				}
			 A_ReFire();
			}
			Goto Ready;
		Spawn:
			HBAG Z -1;
			Stop;
	}
}