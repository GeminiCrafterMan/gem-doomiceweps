class GWM_Minigun : GWM_WeaponBase
{
	action void A_FireMinigun()
	{
		A_SpawnItemEx("GWM_Flash");
		ZWL_FireHitscan(0, 10, pellets: 2, spread: 5, tracerType: "GWM_Tracer");
		ZWL_EjectCasing("GWM_RifleCasing", true, -23, 8, 16, (24, 0, -16));
		A_ChangeVelocity(-Cos(pitch)*0.06, 0, sin(pitch)*0.05,CVF_RELATIVE);
		A_SetPitch(pitch+frandom(-.25,.25));
		A_Setangle(angle+frandom(-.25,.25));
	}
	Default
	{
		//$Category Weapons
		//$Title "Minigun
		Weapon.SlotNumber 4;
		Weapon.SelectionOrder 800; // Has a wind-up, so +100
		Weapon.UpSound "weapons/draw_vulcan";
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 20;
		Weapon.AmmoType "GWM_FMJMagazine";
		AttackSound "weapons/chainfire";
		Inventory.PickupMessage "$GOTMINIGUN";
		Obituary "$KILLMINIGUN";
		Tag "$TAG_MINIGUN";
	}
	States
	{
		Ready:
			MING A 1 A_WeaponReady;
			Loop;
		Deselect:
			TNT1 A 0 A_StopSound(CHAN_BODY);
			MING A 1 A_Lower(18);
			Loop;
		Select:
			MING A 1 A_Raise(18);
			Loop;
		Fire:
			// rev the chaingun
			MING A 1 A_StartSound("weapons/Chainup", CHAN_BODY);
			MING AABBBBCCCDDEEFGH 1;
			MING A 0 A_StartSound("weapons/Chainloop", CHAN_BODY, CHANF_LOOPING);
			MING A 0 A_ReFire("Hold");
			Goto Release;
		Hold:
			MING A 1 {A_FireMinigun(); A_Overlay(PSP_FLASH, "FlashAB");A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5); return ZWL_JumpIfEmpty("Release");}
			MING B 1 {A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5); return ZWL_JumpIfEmpty("Release");}
			MING E 0 A_ReFire("Hold2");
			Goto Release;
		Hold2:
			MING C 1 {A_FireMinigun(); A_Overlay(PSP_FLASH, "FlashCD"); A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5); return ZWL_JumpIfEmpty("Release");}
			MING D 1 {A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5); return ZWL_JumpIfEmpty("Release");}
			MING E 0 A_ReFire("Hold3");
			Goto Release;
		Hold3:
			MING E 1 {A_FireMinigun(); A_Overlay(PSP_FLASH, "FlashEF");A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5); return ZWL_JumpIfEmpty("Release");}
			MING F 1 {A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5); return ZWL_JumpIfEmpty("Release");}
			MING E 0 A_ReFire("Hold4");
			Goto Release;
		Hold4:
			MING G 1 {A_FireMinigun(); A_Overlay(PSP_FLASH, "FlashGH");A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5); return ZWL_JumpIfEmpty("Release");}
			MING H 1 {A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5); return ZWL_JumpIfEmpty("Release");}
			MING H 0 A_ReFire;
			Goto Release;
		Release:
			MING H 1 A_StartSound("weapons/Chaindn", CHAN_BODY);
			MING ABCDEEFFFGGGGHHHHH 1;
			//MING GFFEEDDDCCCBBBBAAA 1;
			TNT1 A 0 A_CheckReload;
			Goto Ready;
		FlashAB:
			MINF A 1 Bright A_Light1;
			MINF B 1 Bright A_Light2;
			Stop;
		FlashCD:
			MINF C 1 Bright A_Light1;
			MINF D 1 Bright A_Light2;
			Stop;
		FlashEF:
			MINF E 1 Bright A_Light1;
			MINF F 1 Bright A_Light2;
			Stop;
		FlashGH:
			MINF G 1 Bright A_Light1;
			MINF H 1 Bright A_Light2;
			Stop;
		Spawn:
			MING Z -1;
			Stop;
	}
}