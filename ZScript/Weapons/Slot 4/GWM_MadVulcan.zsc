class GWM_MadVulcan : GWM_WeaponBase
{
	action void A_FireMVulcan()
	{
		A_SpawnItemEx("GWM_Flash");
		ZWL_FireHitscan(0, 8, pellets: 4, spread: 10, tracerType: "GWM_Tracer");
		ZWL_EjectCasing("GWM_RifleCasing", true, -23, 8, 16, (24, 0, -16));
		A_ChangeVelocity(-Cos(pitch)*0.1, 0, sin(pitch)*0.1,CVF_RELATIVE);
		A_SetPitch(pitch+frandom(-.5,.5));
		A_Setangle(angle+frandom(-.5,.5));
	}
	Default
	{
		//$Category Weapons
		//$Title ""Mad Vulcan" Minigun
		Weapon.SelectionOrder 800; // Has a wind-up, so +100
		Weapon.UpSound "weapons/draw_vulcan";
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 20;
		Weapon.AmmoType "GWM_FMJMagazine";
		AttackSound "weapons/vulcanfire";
		Inventory.PickupMessage "$GOTMADVULCAN";
		Obituary "$KILLMADVULCAN";
		Tag "$TAG_MADVULCAN";
		
		GWM_WeaponBase.BaseVersion "GWM_Minigun";
	}
	States
	{
		Ready:
			VLCG A 1 A_WeaponReady;
			Loop;
		Deselect:
			TNT1 A 0 A_StopSound(CHAN_BODY);
			VLCG A 1 A_Lower(18);
			Loop;
		Select:
			VLCG A 1 A_Raise(18);
			Loop;
		Fire:
			// rev the chaingun
			VLCG A 1 A_StartSound("weapons/vulcanup", CHAN_BODY);
			VLCG AABBBBCCCDDAABCDACAC 1;
			VLCG A 0 A_StartSound("weapons/vulcanloop", CHAN_BODY, CHANF_LOOPING);
			VLCG A 0 A_ReFire("Hold");
			Goto Release;
		Hold:
			VLCG E 1 {A_FireMVulcan(); A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5);}
			VLCG F 1 {
				A_FireMVulcan();
				return ZWL_JumpIfEmpty("Release");
			}
			VLCG G 1 {A_FireMVulcan(); A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5);}
			VLCG H 1 {
				A_FireMVulcan();
				return ZWL_JumpIfEmpty("Release");
			}
			VLCG E 0 A_ReFire;
			Goto Release;
		Release:
			VLCG D 1 A_StartSound("weapons/vulcandn", CHAN_BODY);
			VLCG ABCDAABBBCCCCDDDDD 1;
			//VLCG GFFEEDDDCCCBBBBAAA 1;
			TNT1 A 0 A_CheckReload;
			Goto Ready;
		Spawn:
			VLCG Z -1;
			Stop;
	}
}