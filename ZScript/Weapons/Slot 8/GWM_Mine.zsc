class GWM_Mine : Weapon
{
	Default
	{
		Tag "Mine";
		Weapon.AmmoUse 1;
		Weapon.AmmoType "GWM_MineAmmo";
		Weapon.AmmoGive 2;
		Inventory.PickupMessage "Picked up some land mines!";
		Obituary "I fucked up somewhere. Lemme know!";
	}
	States
	{
		Spawn:
			MINE Z -1;
			Stop;
		Ready:
			MINE A 1 A_WeaponReady;
			Loop;
		Deselect:
			MINE A 1 A_Lower;
			MINE A 0 A_Lower;
			Loop;
		Select:
//			TNT1 A 0 A_Overlay(-2, "LeftHand");
			MINE A 1 A_Raise;
			MINE A 0 A_Raise;
			Loop;
		LeftHand: // Only visual, leave the regular one alone.
			MINE E 1;
			Loop;
		Fire:
		// Toss a mine!
			MINE B 4 A_FireProjectile("GWM_ThrownMine");
			FIST B 4;
			Goto Select;
//		AltFire:
//			MINE A 4 A_Overlay(-2, "Detonate");
//			Goto Ready;
		Detonate:
			MINE F 4 A_StartSound("misc/chat");
			Goto LeftHand;
	}
}
class GWM_ThrownMine : Actor // Okay, so like, I was going to do this myself, but then I saw there was a better mine on the wiki, so fuck it
{
	// This will the mine's activation radius:
	const DETECTRADIUS = 80;
	Default
	{
		Radius 16;
		Height 4;
		Health 1;
		Speed 24;
		Obituary "%o didn't see %k's landmine.";
		BounceType "Hexen";
		DeathSound "weapons/explode";
		BounceSound "weapons/grndimp";
		Projectile;
		+USEBOUNCESTATE +NOBLOOD +SHOOTABLE -NOGRAVITY -NOBLOCKMAP
	}
	States
	{
		Spawn:
			MINE CD 2;
			Loop;
		Bounce:
			MINE CD 1;
			Loop;
		Bounce.Floor:
			MINE # 0 {
				self.Vel.X = 0;
				self.Vel.Y = 0;
				self.Vel.Z = 0;
				bBOUNCEONFLOORS = false;
				bBOUNCEONWALLS = false;
				bBOUNCEONCEILINGS = false;
				bBOUNCEONACTORS = false;
				bMOVEWITHSECTOR = true;
			}
		Ground:
			MINE # 1
			{
				// Create an iterator to cover the effective radius:
				BlockThingsIterator it = BlockThingsIterator.Create(self, DETECTRADIUS);
				while (it.Next())
				{
					// Get a shorter pointer to the found actor (for convenience):
					let obj = it.thing;
					
					// Check the object is either a monster or a player, isn't the same
					// as the projectile's target (so that the shooter can't trigger it),
					// then checks that it's alive and within distance:
					if ((obj.bISMONSTER || obj.player) && (!target || obj != target) && obj.health > 0 && Distance3D(obj) <= DETECTRADIUS)
					{
						// Go to the Boom state sequence if the check passes:
						return ResolveState("Detected");
					}
				}
				
				// Once a second it'll play a DSTINK sound to emulate beeping:
				if (GetAge() % 35 == 0)
				{
					A_StartSound("misc/chat2", volume: 0.4, attenuation: 5);
				}
				
				// If the jump didn't happen, proceed to loop:
				return ResolveState(null);
			}
			Loop;
		Detected:
			// Make the mine jump up as a visual cue,
			// then proceed to the explosion sequence:
			MINE # 8 { vel.z += 8; }
		Bounce.Actor:
		Death:
			TNT1 A 0 {
				bSPECIAL = false;
				bMISSILE = true;
				bNOGRAVITY = true;
				self.Vel.X = 0;
				self.Vel.Y = 0;
				self.Vel.Z = 0;
				A_Scream();
				A_SetRenderstyle(alpha, STYLE_Add);
				A_Explode();
			}
			EXPL ABCDEFGHIJ 1 Bright;
			Stop;
	}
}