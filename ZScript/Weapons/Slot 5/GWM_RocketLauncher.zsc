class GWM_RocketLauncher : RocketLauncher
{
	Default
	{
		Weapon.AmmoUse 1;
		Weapon.AmmoUse2 1;
		Weapon.AmmoType "GWM_RocketAmmo";
		Weapon.AmmoType2 "GWM_RocketAmmo";
		Inventory.PickupMessage "$GOTROCKETLAUNCHER";
	}
	States
	{
		Ready:
			RKTL A 1 A_WeaponReady;
			Loop;
		Deselect:
			RKTL A 1 A_Lower(12);
			Loop;
		Select:
			RKTL A 1 A_Raise(12);
			Loop;
		Fire:
			RKTL A 8 {
				A_OverlayPivot(OverlayID(), 0.66, 1);
				A_Overlay(-2, "Flash");
				A_SpawnItemEx("GWM_Flash");
				A_WeaponOffset(0, 24, WOF_ADD);
				A_StartSound("weapons/rocketfire", CHAN_WEAPON);
				A_FireProjectile("GWM_Rocket");
				A_OverlayScale(OverlayID(), 1.33, 0, WOF_INTERPOLATE);
			}
			RKTL A 3;
			RKTL AAA 3 {
				A_WeaponOffset(0, -8, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.11, 0, WOF_ADD);
			}
			RTKL A 0 A_ReFire;
			Goto Ready;
		AltFire:
			RKTL A 8 {
				A_OverlayPivot(OverlayID(), 0.66, 1);
				A_Overlay(-2, "Flash");
				A_SpawnItemEx("GWM_Flash");
				A_WeaponOffset(0, 24, WOF_ADD);
				A_StartSound("weapons/grenadefire", CHAN_WEAPON);
				A_FireProjectile("GWM_Grenade", 0, true, 0, 0, 0, -6.328125);
				A_OverlayScale(OverlayID(), 1.33, 0, WOF_INTERPOLATE);
			}
			RKTL A 3;
			RKTL AAA 3 {
				A_WeaponOffset(0, -8, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.11, 0, WOF_ADD);
			}
			RTKL A 0 A_ReFire;
			Goto Ready;
		Flash:
			RKTF A 3 Bright {
				A_Light1();
				A_OverlayPivot(OverlayID(), 0.66, 1);
				A_OverlayScale(OverlayID(), 1.33, 0, WOF_INTERPOLATE);
			}
			RKTF B 3 Bright A_Light2;
			Stop;
		Spawn:
			RKTL Z -1;
			Stop;
	}
}
class GWM_Rocket : Actor
{
	Default
	{
		Radius 11;
		Height 8;
		Speed 30; // faster.
		Damage 20;
		Projectile;
		+RANDOMIZE
		+ROCKETTRAIL
		+THRUGHOST
		SeeSound "";
		DeathSound "weapons/explode";
		Obituary "$KILLROCKET";
	}
	States
	{
		Spawn:
			RCKT A 1 Bright;
			Loop;
		Death:
			TNT1 A 0 {
				bMISSILE = false;
				bNOGRAVITY = true;
				self.Vel.X = 0;
				self.Vel.Y = 0;
				self.Vel.Z = 0;
				A_Scream();
				A_SetRenderstyle(alpha, STYLE_Add);
				A_Explode();
			}
			EXPL ABCDEFGHIJ 1 Bright;
			Stop;
	}
}
class GWM_Grenade : Actor
{
	bool missed;
	int timer;
	action State GrenadeTimerJump()
	{
		if (invoker.timer < 81) {
			if (!invoker.missed) {
				return ResolveState(null);
			}
			else invoker.timer++;
		}
		if (invoker.timer >= 81) {
			return ResolveState("Death");
		}
		return State(null);
	}
	Default
	{
		Radius 8;
		Height 8;
		Speed 25;
		Damage 20;
		Projectile;
		-NOGRAVITY
		+RANDOMIZE
		+GRENADETRAIL
		+USEBOUNCESTATE
		+THRUGHOST
		BounceType "Grenade";
		Gravity 0.25;
		SeeSound "";
		DeathSound "weapons/explode";
		BounceSound "weapons/grndimp";
		Obituary "$KILLGRENADE";
		DamageType "Grenade";
	}
	States
	{
		Spawn:
			GNDE A 1 Bright GrenadeTimerJump;
			Loop;
		Bounce:
			TNT1 A 0 {invoker.missed = true;}
			Goto Spawn;
		Death:
			TNT1 A 0 {
				bMISSILE = false;
				bNOGRAVITY = true;
				self.Vel.X = 0;
				self.Vel.Y = 0;
				self.Vel.Z = 0;
				A_Scream();
				A_SetRenderstyle(alpha, STYLE_Add);
				A_Explode();
			}
			EXPL ABCDEFGHIJ 1 Bright;
			Stop;
	}
}