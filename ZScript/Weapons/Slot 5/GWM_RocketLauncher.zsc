class GWM_RocketLauncher : GWM_WeaponBase
{
	Default
	{
		Weapon.SelectionOrder 2500; // Don't blow yourself up!!
		Weapon.UpSound "weapons/draw_primary";
		Weapon.AmmoUse 1;
		Weapon.AmmoUse2 1;
		Weapon.AmmoGive 2;
		Weapon.AmmoType "GWM_RocketAmmo";
		Weapon.AmmoType2 "GWM_RocketAmmo";
		+WEAPON.NOAUTOFIRE
		Inventory.PickupMessage "$GOTROCKETLAUNCHER";
		Tag "$TAG_ROCKETLAUNCHER";
	}
	States
	{
		Ready:
			RKTL A 1 A_WeaponReady;
			Loop;
		Deselect:
			RKTL A 1 A_Lower(18);
			Loop;
		Select:
			RKTL A 1 A_Raise(18);
			Loop;
		Fire:
			RKTL A 8 {
				A_OverlayPivot(OverlayID(), 0.66, 1);
				A_Overlay(-2, "Flash");
				A_SpawnItemEx("GWM_Flash");
				A_WeaponOffset(0, 18, WOF_ADD);
				A_StartSound("weapons/rocketfire", CHAN_WEAPON);
				A_FireProjectile("GWM_Rocket");
				A_OverlayScale(OverlayID(), 1.33, 0, WOF_INTERPOLATE);
			}
			Goto Fire.Done;
		Fire.Done:
			RKTL A 3;
			RKTL AAA 3 {
				A_WeaponOffset(0, -6, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.11, 0, WOF_ADD);
			}
			RTKL A 0 A_ReFire;
			Goto Ready;
		AltFire:
			RKTL A 8 {
				A_OverlayPivot(OverlayID(), 0.66, 1);
				A_Overlay(-2, "Flash");
				A_SpawnItemEx("GWM_Flash");
				A_WeaponOffset(0, 18, WOF_ADD);
				A_StartSound("weapons/grenadefire", CHAN_WEAPON);
				A_FireProjectile("GWM_Grenade", 0, true, 0, 0, 0, -6.328125);
				A_OverlayScale(OverlayID(), 1.33, 0, WOF_INTERPOLATE);
			}
			Goto Fire.Done;
		Flash:
			RKTF ABC 2 Bright {
				A_Light1();
				A_OverlayPivot(OverlayID(), 0.66, 1);
				A_OverlayScale(OverlayID(), 0.11, 0, WOF_ADD);
			}
			RKTF DE 2 Bright A_Light2;
			Stop;
		Spawn:
			RKTL Z -1;
			Stop;
	}
}
class GWM_Rocket : Actor
{
	Default
	{
		Radius 11;
		Height 8;
		Speed 30; // faster.
		DamageFunction (90);
		Projectile;
		+RANDOMIZE
		+THRUGHOST
		SeeSound "";
		DeathSound "weapons/explode";
		Obituary "$KILLROCKET";
	}
	States
	{
		Spawn:
			RCKT A 2 Bright A_SpawnItemEx("GWM_RocketTrail", -20);
			Loop;
		Death:
			TNT1 A 0 {
				bMISSILE = false;
				bNOGRAVITY = true;
				self.Vel.X = 0;
				self.Vel.Y = 0;
				self.Vel.Z = 0;
				A_Scream();
				A_SetRenderstyle(alpha, STYLE_Add);
				A_Explode();
				A_QuakeEx(3, 3, 3, 10, 0, 512, flags: QF_SHAKEONLY);
			}
			EXPL ABCDEFGHIJ 1 Bright;
			Stop;
	}
}
class GWM_Grenade : Actor
{
	bool missed;
	int timer;
	action State GrenadeTimerJump()
	{
		if (invoker.timer < 81) {
			if (!invoker.missed) {
				return ResolveState(null);
			}
			else invoker.timer++;
		}
		if (invoker.timer >= 81) {
			return ResolveState("Death");
		}
		return State(null);
	}
	Default
	{
		Radius 8;
		Height 8;
		Speed 25;
		DamageFunction (90);
		Projectile;
		-NOGRAVITY
		+RANDOMIZE
		+USEBOUNCESTATE
		+THRUGHOST
		BounceType "Grenade";
		Gravity 0.25;
		SeeSound "";
		DeathSound "weapons/explode";
		BounceSound "weapons/grndimp";
		Obituary "$KILLGRENADE";
		DamageType "Grenade";
	}
	States
	{
		Spawn:
			GNDE A 1 Bright {
				GrenadeTimerJump();
				A_SpawnItemEx("GWM_RocketTrail", -20);
			}
			GNDE A 1 Bright GrenadeTimerJump;
			Loop;
		Bounce:
			TNT1 A 0 {invoker.missed = true;}
			Goto Spawn;
		Bounce.Actor:
		Death:
			TNT1 A 0 {
				bMISSILE = false;
				bNOGRAVITY = true;
				self.Vel.X = 0;
				self.Vel.Y = 0;
				self.Vel.Z = 0;
				A_Scream();
				A_SetRenderstyle(alpha, STYLE_Add);
				A_Explode();
				A_QuakeEx(3, 3, 3, 10, 0, 512, flags: QF_SHAKEONLY);
			}
			EXPL ABCDEFGHIJ 1 Bright;
			Stop;
	}
}
class GWM_RocketTrail : Actor
{
	Default
	{
		+NOBLOCKMAP +NOGRAVITY +NOTELEPORT +NOINTERACTION
	}
	States
	{
		Spawn:
			RCKT BCDEF 2 Bright;
			Stop;
	}
}