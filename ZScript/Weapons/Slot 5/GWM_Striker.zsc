class GWM_Striker : GWM_WeaponBase
{
	Default
	{
		//$Category Weapons
		//$Title "Striker
		Weapon.SlotNumber 5;
		Weapon.SelectionOrder 2500; // Don't blow yourself up!!
		Weapon.UpSound "weapons/draw_primary";
		Weapon.AmmoUse 1;
		Weapon.AmmoUse2 3;
		Weapon.AmmoGive 2;
		Weapon.AmmoType "GWM_RocketAmmo";
		Weapon.AmmoType2 "GWM_RocketAmmo";
		+WEAPON.NOAUTOFIRE
		Inventory.PickupMessage "$GOTSTRIKER";
		Tag "$TAG_STRIKER";
		GWM_WeaponBase.BaseVersion "GWM_RocketLauncher";
	}
	States
	{
		Ready:
			RKTL A 1 A_WeaponReady;
			Loop;
		Deselect:
			RKTL A 1 A_Lower(18);
			Loop;
		Select:
			RKTL A 1 A_Raise(18);
			Loop;
		Fire:
			TNT1 A 0 A_Jumpif(GWM_Player(player.mo).ChargedRockets > 0,"Fire2");
			
			RKTL A 2 {
				A_GiveInventory("GWM_FiringRocket",1);
				A_OverlayPivot(OverlayID(), 0.66, 0.66);
				A_Overlay(-2, "Flash");
				A_SpawnItemEx("GWM_Flash");
				A_WeaponOffset(0, 18, WOF_ADD);
				A_StartSound("weapons/rocketfire", CHAN_WEAPON);
				A_FireProjectile("GWM_Rocket");
				A_OverlayScale(OverlayID(), 0.11, 0, WOF_ADD);
			}
			RKTL AA 2 A_OverlayScale(OverlayID(), 0.11, 0, WOF_ADD);
			RKTL A 2;
			Goto Fire.Done;
		Fire.Done:
			RKTL A 3;
			RKTL AAA 3 {
				A_WeaponOffset(0, -6, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.11, 0, WOF_ADD);
			}
			RTKL A 0 A_ReFire;
			Goto Ready;
		Fire2:
			RKTL A 0 {
				A_GiveInventory("GWM_FiringRocket",1);
				A_OverlayPivot(OverlayID(), 0.66, 0.66);
				A_Overlay(-2, "Flash");
				A_SpawnItemEx("GWM_Flash");
				A_SpawnItemEx("GWM_Flash");
				A_WeaponOffset(0, 18, WOF_ADD);
				A_StartSound("weapons/rocketfire", CHAN_WEAPON);
				A_StartSound("weapons/chargedrocketfire", CHAN_AUTO);
				A_FireProjectile("GWM_ChargedRocket");
				A_FireProjectile("GWM_RocketWeave1",0,false);
				A_FireProjectile("GWM_RocketWeave2",0,false);
				A_FireProjectile("GWM_RocketWeave3",0,false);
				A_FireProjectile("GWM_RocketWeave4",0,false);
				A_OverlayScale(OverlayID(), 0.11, 0, WOF_ADD);
			}
			RKTL AA 2 A_OverlayScale(OverlayID(), 0.11, 0, WOF_ADD);
			RKTL AAA 3 {
				A_WeaponOffset(0, -6, WOF_ADD);
				A_OverlayScale(OverlayID(), -0.11, 0, WOF_ADD);
			}
			RTKL A 0 A_ReFire;
			Goto Ready;
			
		AltFire:
			TNT1 A 0 A_Jumpif(GWM_Player(player.mo).ChargedRockets < 2, "Fire");
			RKTL A 2 {
				A_OverlayPivot(OverlayID(), 0.66, 0.66);
				A_Overlay(-2, "Flash");
				A_SpawnItemEx("GWM_Flash");
				A_WeaponOffset(0, 18, WOF_ADD);
				A_StartSound("weapons/rocketfire", CHAN_WEAPON);
				A_StartSound("weapons/chargedrocketfire", CHAN_AUTO);
				
				A_FireProjectile("GWM_HomingRocket");
				A_GiveInventory("GWM_AltFiringRocket",1);
				
				A_OverlayScale(OverlayID(), 0.11, 0, WOF_ADD);
			}
			RKTL AA 2 A_OverlayScale(OverlayID(), 0.11, 0, WOF_ADD);
			RKTL A 2;
			Goto Fire.Done;
		Flash:
			RKTF ABC 2 Bright {
				A_Light1();
				A_OverlayPivot(OverlayID(), 0.66, 0.66);
				A_OverlayScale(OverlayID(), 0.11, 0, WOF_ADD);
			}
			RKTF DE 2 Bright A_Light2;
			Stop;
		Spawn:
			RKTL Z -1;
			Stop;
	}
}
class GWM_RocketWeave1 : GWM_ChargedRocket
{
	int WeaveDist;
	property WeaveDist: WeaveDist;
	int WeaveSpeed;
	property WeaveSpeed: WeaveSpeed;

	Default
	{
		Speed 40; // faster.
		DamageFunction (57);
		Scale 0.625;
		+EXTREMEDEATH
		GWM_RocketWeave1.WeaveDist 3;
		GWM_RocketWeave1.WeaveSpeed 3;
	}
	States
	{
		Spawn:
			RCKT A 1 Bright 
			{
				A_Weave(WeaveSpeed,0,WeaveDist,0);
				A_SpawnItemEx("GWM_SmallRocketTrail", -20,0,0,frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
				A_SpawnItemEx("GWM_SmallRocketTrail", -20,0,0,frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
			}
			Loop;
		Death:
			TNT1 A 0 {
				bMISSILE = false;
				bNOGRAVITY = true;
				self.Vel.X = 0;
				self.Vel.Y = 0;
				self.Vel.Z = 0;
				A_Scream();
				A_SetRenderstyle(alpha, STYLE_Add);
				A_Explode(125,175);
				A_SpawnItemEX("GWM_SmallRocketBlast");
				A_SpawnItemEX("GWM_SmallRocketBlast");
			}
			//EXPL ABCDEFGHIJ 1 Bright;
			Stop;
	}
}
Class GWM_RocketWeave2 : GWM_RocketWeave1
{
	States
	{
		Spawn:
			RCKT A 1 Bright 
			{
				A_Weave(WeaveSpeed,0,-WeaveDist,0);
				A_SpawnItemEx("GWM_SmallRocketTrail", -20,0,0,frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
				A_SpawnItemEx("GWM_SmallRocketTrail", -20,0,0,frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
			}
			Loop;
	}
}
Class GWM_RocketWeave3 : GWM_RocketWeave1
{
	States
	{
		Spawn:
			RCKT A 1 Bright 
			{
				A_Weave(0,WeaveSpeed,0,-WeaveDist*0.5);
				A_SpawnItemEx("GWM_SmallRocketTrail", -20,0,0,frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
				A_SpawnItemEx("GWM_SmallRocketTrail", -20,0,0,frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
			}
			Loop;
	}
}
Class GWM_RocketWeave4 : GWM_RocketWeave1
{
	States
	{
		Spawn:
			RCKT A 1 Bright 
			{
				A_Weave(0,WeaveSpeed,0,WeaveDist*0.5);
				A_SpawnItemEx("GWM_SmallRocketTrail", -20,0,0,frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
				A_SpawnItemEx("GWM_SmallRocketTrail", -20,0,0,frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
			}
			Loop;
	}
}
//class GWM_SmallRocketWeave1
class GWM_SmallRocketTrail : GWM_RocketTrail
{
	Default
	{
		Scale 0.5;
	}
	States
	{
		Spawn:
			RCKT BCDEF 3 Bright;
			Stop;
	}
}
Class GWM_SmallRocketBlast : Actor
{
	Default
	{
		+NoInteraction
	}

	States
	{
	Spawn:
		TNT1 A 1 nodelay A_SpawnItemEX("ShakeItBaby",0,0,5);
		TNT1 AA 1
		{
		A_SpawnItemEX("BlastFlameFX2",frandom(-15,15),frandom(-15,15),frandom(2,45),frandom(-4,4),frandom(-4,4),frandom(-4,4),frandom(1,359));
		A_SpawnItemEX("BlastFlameFX2",frandom(-15,15),frandom(-15,15),frandom(2,45),frandom(-4,4),frandom(-4,4),frandom(-4,4),frandom(1,359));
		A_SpawnItemEX("BlastFlameFX2",frandom(-15,15),frandom(-15,15),frandom(2,45),frandom(-4,4),frandom(-4,4),frandom(-4,4),frandom(1,359));
		A_SpawnItemEX("BlastFlameFX",frandom(-15,15),frandom(-15,15),frandom(2,45),frandom(-12,12),frandom(-12,12),frandom(-12,12),frandom(1,359));
		A_SpawnItemEX("BlastFlameFX",frandom(-15,15),frandom(-15,15),frandom(2,45),frandom(-12,12),frandom(-12,12),frandom(-12,12),frandom(1,359));
		A_SpawnItemEX("BlastFlameFX",frandom(-15,15),frandom(-15,15),frandom(2,45),frandom(-12,12),frandom(-12,12),frandom(-12,12),frandom(1,359));
		A_SpawnItemEX("BlastFlameFX",frandom(-15,15),frandom(-15,15),frandom(2,45),frandom(-12,12),frandom(-12,12),frandom(-12,12),frandom(1,359));
		}
		stop;
	}
}