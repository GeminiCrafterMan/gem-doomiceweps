class GWM_HealthBonus : HealthBonus replaces HealthBonus
{
	Default
	{
		Inventory.MaxAmount 300;
		Inventory.PickupSound "pickups/healthbonus";
	}
}

class GWM_ArmorBonus : ArmorBonus replaces ArmorBonus
{
	Default
	{
		Armor.MaxSaveAmount 300;
		Inventory.PickupSound "pickups/armorbonus";
	}
}

class GWM_Soulsphere : Soulsphere replaces Soulsphere
{
	Default
	{
		Inventory.MaxAmount 300;
	}
}

class GWM_Backpack : Backpack replaces Backpack
{
	Default
	{
		Inventory.PickupSound "pickups/backpack";
	}
}

class GWM_Berserk : Berserk replaces Berserk
{
	override void Touch(Actor toucher) {
		toucher.A_StartSound("player/berserk", CHAN_VOICE);
		Super.Touch(toucher);
	}
	States
	{
		Pickup:
			TNT1 A 0 {
				A_GiveInventory("PowerStrength");
				HealThing(100, 0);
				A_SelectWeapon("GWM_Fist");
			}
			Stop;
	}
}

class GWM_BlurSphere : BlurSphere {}

class GWM_DrainSphere : PowerupGiver
{
	Default
	{
		Inventory.PickupMessage "Life drain!";
		Inventory.Icon "VAMPA0";
		Powerup.Color "FF0000", 0.25;
		Inventory.MaxAmount 0;
		Inventory.UseSound "pickups/slowmo";
		Powerup.Type "PowerDrain";
		Powerup.Duration 1000;
		+INVENTORY.AUTOACTIVATE
		+RANDOMIZE
	}
	States
	{
		Spawn:
			VAMP ABCD 4 Bright;
			Loop;
	}
}

class GWM_InvulnerabilitySphere : InvulnerabilitySphere {}

class GWM_ReflectSphere : PowerupGiver
{
	Default
	{
		Inventory.PickupMessage "Reflective shots!";
		Inventory.Icon "REFLA0";
		Powerup.Color "0011FF", 0.25;
		Inventory.MaxAmount 0;
		Inventory.UseSound "pickups/slowmo";
		Powerup.Type "PowerInvulnerable";
		Powerup.Mode "Reflective";
		Powerup.Duration -30; // 30 seconds
		+INVENTORY.AUTOACTIVATE
		+RANDOMIZE
	}
	States
	{
		Spawn:
			REFL ABCD 4 Bright;
			Loop;
	}
}

class GWM_Stopwatch : CustomInventory
{
	Default
	{
		+NOGRAVITY
		+FLOATBOB
		+INVENTORY.INVBAR
		Inventory.Icon "WTCHA0";
		Tag "Stopwatch";
		Inventory.MaxAmount 4;
		Inventory.PickupMessage "Stopwatch!";
	}
	States
	{
		Spawn:
			WTCH A -1 Bright;
			Loop;
		Use:
			TNT1 A 0 A_Overlay(PSP_WEAPON, "TimeStop");
			Wait;
		TimeStop:
			TNT1 A 0;
			WTCH B 35 A_StartSound("powerups/time_stop_start");
			TNT1 A 0 A_GiveInventory("GWM_StopwatchStopper");
			WTCH B 7;
			TNT1 A 0 A_StartSound("powerups/time_stop_end");
			WTCH B 7;
			TNT1 A 0 A_TakeInventory("GWM_Stopwatch");
			Stop;
	}
}

class GWM_StopwatchStopper : PowerupGiver
{
	Default
	{
		-INVENTORY.INVBAR
		+INVENTORY.QUIET
		+INVENTORY.AUTOACTIVATE
		+INVENTORY.UNDROPPABLE
		Powerup.Type "PowerTimeFreezer";
		Powerup.Color "888888", 0.25;
		Powerup.Colormap 0.5, 0.5, 0.5;
		Powerup.Duration -12; // 12 seconds
	}
}