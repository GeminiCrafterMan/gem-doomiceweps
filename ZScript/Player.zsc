class GWM_Player : DoomPlayer
{
	int prevPlayerState;
	override void Tick()
	{
		super.Tick();
		if(!player || !player.ReadyWeapon) return;
		if (CVar.GetCVar('gwm_shutupmerc', player).GetBool() == false) 
		{
			if (player && prevPlayerState != player.playerstate)
			{
				if (prevPlayerState == PST_DEAD && player.playerstate == PST_LIVE)	// for singleplayer resurrects
				{
					A_StartSound("player/respawn", CHAN_VOICE);
				}
				prevPlayerState = player.playerstate;
			}
		}
		if(player.playerstate == PST_DEAD)
		{
			player.ReadyWeapon.FOVScale = 0.0;
		}
		else if(player.cmd.buttons & BT_ZOOM)
		{
			player.ReadyWeapon.FOVScale = 0.5;
		}
		else
		{
			player.ReadyWeapon.FOVScale = 0.0;
		}
	}
	Default
	{
		Player.DisplayName "Mercenary";
		Player.SoundClass "GWM_Player";
		Player.WeaponSlot 1, "GWM_Mine", "GWM_Fists", "GWM_Lumberjack", "GWM_Impactor", "GWM_Levenbrech";
		Player.WeaponSlot 2, "GWM_Pistol", "GWM_SMG", "GWM_Revolver", "GWM_GoldWand";
		Player.WeaponSlot 3, "GWM_ZombieShotgun", "GWM_IceShotgun", "GWM_PFSuperShotgun", "GWM_Shotgun", "GWM_SuperShotgun", "GWM_ImpalerCrossbow";
		Player.WeaponSlot 4, "GWM_Chaingun", "GWM_BurstRifle", "GWM_MP40", "GWM_FrostFang";
		Player.WeaponSlot 5, "GWM_RocketLauncher", "GWM_VenomCannon";
		Player.WeaponSlot 6, "GWM_PlasmaRifle", "GWM_Melter", "GWM_NoiseCrusher";
		Player.WeaponSlot 7, "GWM_BFG9000", "GWM_Flamethrower", "GWM_TwinPlasmaCannons",  "GWM_Bouncer";
		Player.WeaponSlot 8, "GWM_EldritchAnnihilator" ,"GWM_Perforator"; // Eviternity II's Perforator, but a little different
	}
}
class GWM_DoomPlayer : GWM_Player
{
	Default
	{
		Player.StartItem "GWM_Pistol";
		Player.StartItem "GWM_Fists";
		Player.StartItem "GWM_Mine";
		Player.StartItem "GWM_LowCaliberMagazine", 50;
		Player.StartItem "GWM_QuickKickController";
	}
}
class GWM_HereticPlayer : GWM_Player
{
	Default
	{
		Player.StartItem "GWM_GoldWand";
		Player.StartItem "GWM_Fists";
		Player.StartItem "GoldWandAmmo", 50;
		Player.StartItem "GWM_QuickKickController";
		Player.ColorRange 224, 209;
	}
}


class GWM_QuickKickController : CustomInventory
{
	const KICKLAYER = -100;
	override void DoEffect()
	{
		super.DoEffect();
		if (!owner || !owner.player)
		{
			return;
		}
		let psp = owner.player.FindPSprite(KICKLAYER );
		if (!psp)
		{
			psp = owner.player.GetPSprite(KICKLAYER );
			psp.caller = self;
			psp.bAddBob = false;
			psp.bAddWeapon = false;
			psp.SetState(ResolveState("ReadyKick"));
		}
	}
	Default
	{
		+INVENTORY.UNDROPPABLE
		+INVENTORY.UNTOSSABLE
		+INVENTORY.PERSISTENTPOWER
		+INVENTORY.AUTOACTIVATE //Use is called automatically
		Obituary "$KILLKICK";
	}
	States 
	{
		Use:
			TNT1 A 0 
			{
				// We're passing 'true' to the last argument, so
				// that the layer doesn't get overridden if for
				// some reason the item is used again:
				A_Overlay(KICKLAYER, "ReadyKick", true);
			}
			fail;
		ReadyKick:
			TNT1 A 1
			{
//				Console.Printf("Did effect");
				// Check if +user2 is pressed:
				if (player.cmd.buttons & BT_USER2)
				{
					// move to DoKick if so:
					return ResolveState("DoKick");
				}
				// Otherwise loop this layer
				return ResolveState(null);
			}
			loop;
		DoKick:
			TNT1 A 0 A_OverlayOffset(OverlayID(), -20, WEAPONTOP);
			KICK AB 2;
			KICK CD 1;
			KICK E 3 {
				// redo this shit later
				int dmg = 11;
				if (FindInventory("PowerStrength"))
				{
					A_CustomPunch(dmg*10, norandom:true, flags: 0, pufftype: "GWM_BerserkPuff", range: meleeRange + 20, meleesound: "*fist");
				}
				else
				{
					A_CustomPunch(dmg, norandom:true, flags: 0, pufftype: "GWM_KickPuff", range: meleeRange + 20, meleesound: "*fist");
				}
			}
			KICK DCB 3;
			KICK A 3
			{
				if (player.cmd.buttons & BT_USER2 && player.oldbuttons & BT_USER2)
				{
					return ResolveState("DoKick");
				}
				return ResolveState(null);
			}
			goto ReadyKick; //Note that we go back to ready from here
	}
}
class GWM_KickPuff : GWM_FistPuff
{
	Default
	{
		DamageType "Kick";
	}
}

class GWM_HellKnightPlayer : PlayerPawn
{
	Default
	{
		Health 500;
		ReactionTime 0;
		PainChance 255;
		Radius 24;
		Height 64;
		Speed 0.88;
		Gravity 1.5;
		Mass 1000;
		+NOSKIN
		PainSound "knight/pain";
		DeathSound "knight/death";
		Player.JumpZ 8;
		Player.ViewHeight 60;
		Player.SoundClass "GWM_HKPlayer";
		Player.DisplayName "Hell Knight";
		Player.Face "HKN";
		Player.MorphWeapon "GWM_HellKnightClaws";
	}
	States
	{
		Spawn:
			HKNI A -1;
			Stop;
		See:
			HKNI ABCD 6;
			Loop;
		Melee:
		Missile:
			HKNI EFG 8;
			Goto Spawn;
		Pain:
			HKNI H 2;
			HKNI H 2 A_Pain;
			Goto Spawn;
		Death:
			HKNI I 8;
			HKNI J 8 A_Scream;
			HKNI K 8;
			HKNI L 8 A_NoBlocking;
			HKNI MN 8;
			HKNI O -1;
			Stop;
	}
}