class PlayerSpawnLines : StaticEventHandler
{
    override void WorldLoaded(WorldEvent _event)
    {
        if (_event.IsSaveGame) return;
        
        let player = players[consoleplayer].mo;
        player.A_StartSound("player/battlecry", CHAN_VOICE);
    }
    
    override void PlayerRespawned(PlayerEvent _event)
    {
        let player = players[_event.playernumber].mo;
        player.A_StartSound("player/respawn", CHAN_VOICE);
    }
}

class ThingReplacer : StaticEventHandler
{
    override void CheckReplacement(ReplaceEvent e)
    {
		// Monsters
        CVar replaceMonsters = CVar.FindCVar('gwm_replacemonsters');
        if (replaceMonsters.GetBool())
        {
			// Hitscanners
			if (e.Replacee == "ZombieMan")
			{
				if (random(0, 3) == 3)
					e.Replacement = "GWM_SMGGuy";
				else
					e.Replacement = "GWM_Zombieman";
			}
			if (e.Replacee == "ShotgunGuy")
			{
				if (random(0, 3) == 3)
					e.Replacement = "GWM_IceShotgunGuy";
				else
					e.Replacement = "GWM_KahunaShotgunGuySpawner"; // to spawn a Kahuna Shotgun Guy or a Shadow Hunter
			}
			if (e.Replacee == "ChaingunGuy")
			{
				if (random(0, 1) == 1)
					e.Replacement = "GWM_HeavyRifleman";
				else
					e.Replacement = "GWM_ChaingunGuy";
			}
			// Imps and Bigger Imps
			if (e.Replacee == "DoomImp")
			{
				switch (random(0,4))
				{
					case 4:
						e.Replacement = "GWM_NightmareImpSpawner"; // to spawn a Nightmare Imp or a Nightshade
						break;
					case 3:
						e.Replacement = "GWM_CyberImp";
						break;
					default:
						e.Replacement = "GWM_DoomImp";
				}
			}
			if (e.Replacee == "HellKnight")
				e.Replacement = "GWM_HellKnight";
			if (e.Replacee == "BaronOfHell")
				e.Replacement = "GWM_BaronOfHell";
			// Misc
			if (e.Replacee == "Demon")
			{
				if (random(0, 4) == 4)
					e.Replacement = "GWM_Hellhog";
				else
					e.Replacement = "GWM_Pinky";
			}
			if (e.Replacee == "Cyberdemon")
			{
				if (random(0,1) == 1)
					e.Replacement = "GWM_Superdemon";
				else
					e.Replacement = "GWM_Cyberdemon";
			}
			if (e.Replacee == "Cacodemon")
				e.Replacement = "GWM_Cacodemon";
			if (e.Replacee == "LostSoul")
				e.Replacement = "GWM_LostSoulSpawner"; // to spawn a Lost Soul or a Blot
        }

		// Objects
        CVar replaceObjects = CVar.FindCVar('gwm_replaceobjects');
		if (replaceObjects.getBool())
		{
			if (e.Replacee == "ExplosiveBarrel")
				e.Replacement = "GWM_ExplosiveBarrel";
		}
    }
	override void CheckReplacee(ReplacedEvent e)
	{
		if (e.Replacement == "GWM_BaronOfHell")
			e.Replacee = "BaronOfHell";
		if (e.Replacement == "GWM_Cyberdemon")
			e.Replacee = "Cyberdemon";
		if (e.Replacement == "GWM_Superdemon")
			e.Replacee = "Cyberdemon";
	}
}